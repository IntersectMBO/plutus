{ stdenv
, nodejs
, easyPS
, nix-gitignore
}:

{
  # path to generated purescript sources
  psSrc
  # path to project sources
, src
  # name of the project
, name
  # packages as generated by psc-pacakge2nix
, packages
  # spago packages as generated by spago2nix
, spagoPackages
  # web-common project
, webCommon
  # node_modules to use
, nodeModules
}:
let
  # Cleans the source based on the patterns in ./.gitignore and the additionalIgnores
  cleanSrcs = nix-gitignore.gitignoreSource [ "/*.adoc" "/*.nix" ] src;

in
stdenv.mkDerivation {
  inherit name;
  src = cleanSrcs;
  buildInputs = [ nodeModules easyPS.purs easyPS.spago easyPS.psc-package ];
  buildPhase = ''
    export HOME=$NIX_BUILD_TOP
    shopt -s globstar
    ln -s ${nodeModules}/node_modules node_modules
    ln -s ${psSrc} generated
    ln -s ${webCommon} ../web-common

    sh ${spagoPackages.installSpagoStyle}
    sh ${spagoPackages.buildSpagoStyle}
    ${nodejs}/bin/npm run webpack
  '';
  installPhase = ''
    mv dist $out
  '';
}
