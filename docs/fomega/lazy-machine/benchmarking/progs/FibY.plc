-- Preprocess this file with 'gpp -DARG=20 -DSIZE=4' to get a program to calculate fib(20), for example.
-- SIZE is the size of integers to use.

#ifndef ARG
#define ARG 100
#endif

#ifndef SIZE
#define SIZE 4
#endif

#define Int [(con integer) (con SIZE)]
#define Bool (all E (type) (fun E (fun E E)))
#define True  (abs E (type) (lam x E (lam y E x)))
#define False (abs E (type) (lam x E (lam y E y)))
#define Unit  (all E (type) (fun E E))
#define One   (abs E (type) (lam x E x))

(program 1.0.0

[
{
  (abs T (type)  -- Y combinator
   (lam f (fun T T)
    [
      (lam p (fix A (fun A T))
        [f [(unwrap p) p]])
      (wrap Q (fun Q T) 
        (lam q (fix A (fun A T))
          [f [(unwrap q) q]])
      )
    ]
   ))
   Int}
(lam fib (fun Int Int) 
    (lam n Int
    [ { 
        [{(con lessThanEqualsInteger) (con SIZE)} n (con SIZE ! 2)] -- if n <= 2 
        Int    
        -- Comparison returns a bool: (all A (type) (fun A (fun A A))). 
        -- Instantiate this at A = Int so we can apply it to the banches
      }
	  (con SIZE ! 1) -- then 1
          [{(con addInteger) (con SIZE)} 
              [fib [{(con subtractInteger) (con SIZE)} 
                     n 
                     (con SIZE ! 1)
                   ]
              ]
              [fib [{(con subtractInteger) (con SIZE)} 
                     n 
                     (con SIZE ! 2)
                   ]
              ]
           ]
    ]
    )) 
(con SIZE ! ARG)
]
)

