[source,haskell]
----
{-# LANGUAGE DataKinds                       #-}
{-# LANGUAGE DeriveAnyClass                  #-} -- <1>
{-# LANGUAGE DerivingStrategies              #-}
{-# LANGUAGE NoImplicitPrelude               #-}
{-# LANGUAGE ScopedTypeVariables             #-}
{-# LANGUAGE TemplateHaskell                 #-}
{-# OPTIONS_GHC -fno-warn-missing-signatures #-}

module Parameters.Crowd where

import Language.PlutusTx
import Language.PlutusTx.Prelude
import Ledger
import Ledger.Ada
import Ledger.Typed.Scripts             (wrapValidator)
import Playground.Contract
import Wallet

import Data.Text                        (pack)

data Campaign = Campaign {
      campaignOwner      :: PubKey
    , fundingTarget      :: Ada
    , endDate            :: Slot
    , collectionDeadline :: Slot
} deriving (Show, Generic, ToJSON, FromJSON, ToSchema) -- <2>
makeLift ''Campaign
----

<1> We need this to be able to derive `Generic` and `ToSchema` instances.

<2> We need `ToJSON`, `FromJSON` and `ToSchema` instances
to be able to use `Campaign` as an argument to wallet endpoints in the Playground.

Next, let us figure our which types to use for data- and redeemer scripts!

There are two scenarios where people will want to _claim funds from the
script: The campaign owner _collection_ after a successful campaign
and the contributors demanding a _refund_ after a failed one.

For the validator script to decide whether such a claim is valid,
it has to know which of the two scenarios applies,
which tells us that the _redeemer scripts_ must contain this information.

We therefore will use the following type for the redeemer scripts:

[source,haskell]
----
data CampaignAction = Collect | Refund

makeIsData ''CampaignAction
makeLift ''CampaignAction
----

What about the data scripts? What information should we attach to each
individual campaign contribution?

To decide the validity of a `Collect` claim,
we need to know whether the funding target has been reached and
whether the current slot is between end date and collection deadline, but those
are things we can find out without any additional information.

To decide about a `refund` claim, however, we have to known that the deadline
has passed and whether the
contribution being reclaimed has indeed originally been made by the person
claiming the refund, so we need this information, where a contribution is coming
from, attached to each script output.

So for our data scripts we will use:

[source,haskell]
----
newtype Contributor = Contributor {getContributor :: PubKey} -- <1>
    deriving newtype IsData
makeLift ''Contributor
----

<1> We could simply use `PubKey` instead, but as mentioned in the last chapter,
it is always safer to use `newtype` to avoid confusion.

As before, we write simple helper functions to create data- and redeemer
scripts:

[source,haskell]
----
mkRedeemerValue :: CampaignAction -> RedeemerValue
mkRedeemerValue = RedeemerValue . toData

mkDataValue :: PubKey -> DataValue
mkDataValue = DataValue . toData . Contributor
----

Let us turn our attention to the validator script now! As in the last example,
we will first write a plain Haskell function `validate` that contains all the
logic and then lift this function to a script.

In order to do validation, we have to know whether actions happen at the right
time: The `Collect` action must not happen before `endDate`, but must happen
before `collectionDeadline`. The `Refund` action, on the other hand, must not
happen before `collectionDeadline`.

In contrast to off-chain code running in a wallet, where we can look up the
current slot with `slot`,
the validator is _stateless_, so we have no access to the current slot in it.
We do have, however, the _pending transaction_ and in particular that
transaction's _slot range_. So we know that the current slot must fall into this
slot range, and we can use that information to do transaction validation.

The following helper functions will prove useful for that.
Function `collectionRange` computes the slot range during which collection
is allowed, function `inCollectionRange` checks whether the pending
transaction's slot range is contained in the campaign's collection range.
Functions `refundRange` and `inRefundRange` do the same for the range when
refunds are allowed.

[source,haskell]
----
collectionRange :: Campaign -> SlotRange
collectionRange campaign =
    interval
        (endDate campaign)
        (collectionDeadline campaign)

inCollectionRange :: Campaign -> PendingTx -> Bool
inCollectionRange campaign tx =
    collectionRange campaign `contains` pendingTxValidRange tx

refundRange :: Campaign -> SlotRange
refundRange = intervalFrom . collectionDeadline

inRefundRange :: Campaign -> PendingTx -> Bool
inRefundRange campaign tx =
    refundRange campaign `contains` pendingTxValidRange tx
----

In order to check whether the funding target has been reached,
we must calculate the total amount of Ada contained in all inputs of a pending
transaction that belong to our script,
so we write a helper function `totalInput` to do just that:

[source,haskell]
----
totalInput :: PendingTx -> Ada
totalInput tx = foldl f zero (pendingTxInputs tx)
  where
    f :: Ada -> PendingTxIn -> Ada
    f ada i
        | fromScript i = ada + fromValue (pendingTxInValue i)
        | otherwise    = ada

    fromScript :: PendingTxIn -> Bool                              -- <1>
    fromScript i = case pendingTxInWitness i of
        Nothing     -> False
        Just (h, _, _) -> h == ownHash tx                             -- <2>
----

<1> Does this transaction input correspond to an output of "our" script?

<2> Function `ownHash :: PendingTx -> ValidatorHash` gives us the hash
of the validator script we are currently evaluating.

For refunding, we must check whether all outputs of the pending transaction go
to the original contributor, so we write a function `toContributor` to check
this:

[source,haskell]
----
toContributor :: Contributor -> PendingTxOut -> Bool
toContributor (Contributor key) o = case pendingTxOutType o of
    ScriptTxOut _ _  -> False       -- <1>
    PubKeyTxOut key' -> key' == key -- <2>
----

<1> If the output is locked by a script, it does _not_ go to the contributor,
and we return `False`.

<2> If the output goes to a public key, we check that it is the _right_ public
key.

With these preliminaries done, we can write `validate`:

[source,haskell]
----
type CampaignValidator = Contributor -> CampaignAction -> PendingTx -> Bool

validate :: Campaign -> CampaignValidator
validate campaign _ Collect tx           =
       (tx `txSignedBy` campaignOwner campaign)              -- <1>
    && (totalInput tx >= fundingTarget campaign)             -- <2>
    && inCollectionRange campaign tx                         -- <3>
validate campaign  contributor Refund tx =
       all (toContributor contributor) (pendingTxOutputs tx) -- <4>
    && inRefundRange campaign tx                             -- <5>
    && (tx `txSignedBy` getContributor contributor)          -- <6>
----
<1> Only the campaign owner is allowed to collect the contributions.

<2> The funding target must have been reached.

<3> Collection is only allowed between the end date and the collection deadline.

<4> Refunds must go to the original contributor.

<5> Refunds are only allowed from the collection deadline onwards.

<6> The refund claim must be made by the contributor.

In order to use `validate` to create our validator script for a campaign,
it is not enough to simply lift the function as we did in the last chapter.
This would result in the wrong type, because we still have the additional
`Campaign` argument. Instead we can use `applyScript` to turn a script of type
`Campaign -> CampaignValidator` into one of type `CampaignValidator`:

The issue here is that the specific campaign we use will only be known at
_run time_, whereas `compileScript` works at _compile time_. Function
`applyScript` crucially allows us to manipulate a script at run time,
and this is what enables us to create scripts which depend on run time
parameters.

[source,haskell]
----
mkValidator :: Campaign -> Validator
mkValidator campaign = mkValidatorScript $
    $$(compile [|| v ||]) `applyCode` liftCode campaign
    where v c = wrapValidator (validate c)
----

This is all we have to do for the on-chain code, so let us now turn our
attention to the off-chain wallet endpoints. We start with a function to compute
the script address for a campaign:

[source,haskell]
----
campaignAddress :: Campaign -> Address
campaignAddress = scriptAddress . mkValidator
----

Our first endpoint, `startCampaign`, will be run by the campaign owner.
This endpoint registers a _trigger_ to wait for the campaign's end date
and collect the contributions if the funding target has been reached.

[source,haskell]
----
startCampaign :: forall m. MonadWallet m
              => Ada  -- funding target
              -> Slot -- end date
              -> Slot -- collection deadline
              -> m ()
startCampaign ft ed cd = do
    key <- ownPubKey
    let campaign = Campaign
            { campaignOwner      = key
            , fundingTarget      = ft
            , endDate            = ed
            , collectionDeadline = cd
            }
    logMsg $ pack $ "starting " ++ show campaign
    registerOnce (trigger campaign) (handler campaign)
  where
    trigger :: Campaign -> EventTrigger
    trigger campaign =
        fundsAtAddressGeqT                                  -- <1>
            (campaignAddress campaign)
            (toValue $ fundingTarget campaign)
        `andT`
        slotRangeT (collectionRange campaign)               -- <2>

    handler :: Campaign -> EventHandler m
    handler campaign = EventHandler $ const $ do
        logMsg $ pack $ "collecting from " ++ show campaign
        collectFromScript
            (collectionRange campaign)
            (mkValidator campaign)
            (mkRedeemerValue Collect)                      -- <3>
----

<1> Has the funding target been reached?
<2> Has the end date been reached?
<3> We _collect_ funds.

The second endpoint is for contributors. It allows them to contribute to a
campaign and will also register a trigger that checks whether the collection
deadline has been reached and the funds have not been collected by the owner,
in which case the contribution is reclaimed:

[source,haskell]
----
contribute :: forall m. MonadWallet m => Campaign -> Ada -> m ()
contribute campaign ada = do
    logMsg $ pack $ "contributing to " ++ show campaign
    key <- ownPubKey
    tx  <- payToScript                                           -- <1>
        defaultSlotRange
        (campaignAddress campaign)
        (toValue ada)
        (mkDataValue key)
    registerOnce trigger (handler tx)
  where
    range :: SlotRange
    range = refundRange campaign

    trigger :: EventTrigger
    trigger =
        fundsAtAddressGtT (campaignAddress campaign) mempty      -- <2>
        `andT`
        slotRangeT range                                         -- <3>

    handler :: Tx -> EventHandler m
    handler tx = EventHandler $ const $ do
        let tid = txId tx                                     -- <4>
        logMsg $ pack $ "Reclaiming contribution " ++ show tid
            ++ " from " ++ show campaign
        collectFromScriptTxn                                     -- <5>
            range
            (mkValidator campaign)
            (mkRedeemerValue Refund)                            -- <6>
            tid

$(mkFunctions ['startCampaign, 'contribute])
----

<1> Function `payToScript` is a variant of `payToScript_` which returns the
created and submitted transaction. We need that for our event handler to reclaim
funds from the right transaction output.

<2> Are there contributions to reclaim?

<3> Has the collection deadline been reached?
<4> We get the transaction identifier of a transaction with `hashTx`.

<5> Function `collectFromScriptTxn` is similar to `collectFromScript`,
but it additionally takes a transaction identifier to only collect from script
outputs created by the transaction with that identifier

<6> We demand a _refund_.
