[source,haskell]
----
{-# LANGUAGE DataKinds                       #-}
{-# LANGUAGE DeriveAnyClass                  #-}
{-# LANGUAGE NoImplicitPrelude               #-}
{-# LANGUAGE ScopedTypeVariables             #-}
{-# LANGUAGE TemplateHaskell                 #-}
{-# OPTIONS_GHC -fno-warn-missing-signatures #-}

module Token.Fungible where

import           Language.PlutusTx
import           Language.PlutusTx.Prelude
import           Ledger
import           Ledger.Typed.Scripts       (wrapValidator)
import qualified Ledger.Ada                 as A
import qualified Ledger.Value               as V
import           Playground.Contract
import           Wallet

import           Control.Monad              (void)
import qualified Data.ByteString.Lazy.Char8 as C
import qualified Data.Map                   as Map
import qualified Data.Set                   as Set
import qualified Data.Text                  as T

data Fungible = Fungible
    { name   :: TokenName -- <1>
    , issuer :: PubKey    -- <2>
    } deriving (Show, Generic, ToJSON, FromJSON, ToSchema)

makeLift ''Fungible
----

<1> The name of our currency (for example "Plutus").
<2> The person who is allowed to forge this currency.

The policy will never change, so for its _data scripts_,
we can use the unit type `()`.

For _redeemer scripts_, we want to include the information of
_how much money_ we want to forge, so let us use `Integer` for those.
So we get:

[source,haskell]
----
type FungibleValidator = () -> Integer -> PendingTx -> Bool
----

We are now ready to write down the validation logic, i.e. the
monetary policy of our currency:

[source,haskell]
----
validateFungible :: Fungible -> FungibleValidator
validateFungible f () n tx =
    case pendingTxOutputs tx of
        [o] -> case pendingTxOutType o of
            ScriptTxOut _ _ -> False
            PubKeyTxOut key ->
                   key == issuer f          -- <1>
                && txSignedBy tx (issuer f) -- <2>
                && pendingTxOutValue o == v -- <3>
        _   -> False                        -- <4>
  where
    v :: Value                              -- <5>
    v = V.singleton
            (ownCurrencySymbol tx)          -- <6>
            (name f)
            n
----

<1> The newly forged money must go to the currency issuer.

<2> The forging transaction must be signed by the currency issuer.

<3> The forged value must be the value indicated by the redeemer script.

<4> We expect exactly one output.

<5> The value corresponding to the redeemer script.

<6> Similar to function `ownHash`, function `ownCurrencySymbol` gives us the
`CurrencySymbol` that we are currently validating.

As before, we can now write helper functions to create the various scripts
and addresses:

[source,haskell]
----
fungibleDataScript :: DataValue
fungibleDataScript = DataValue $ toData ()

mkFungibleRedeemerValue :: Integer -> RedeemerValue
mkFungibleRedeemerValue = RedeemerValue . toData

mkFungibleValidator :: Fungible -> Validator
mkFungibleValidator = mkValidatorScript
                    . applyCode $$(compile [|| \f -> wrapValidator (validateFungible f) ||])
                    . liftCode

fungibleAddress :: Fungible -> Address
fungibleAddress = scriptAddress . mkFungibleValidator

fungibleSymbol :: Fungible -> CurrencySymbol
fungibleSymbol f = scriptCurrencySymbol $ mkFungibleValidator f

fungibleValue :: Fungible -> Integer -> Value
fungibleValue f = V.singleton (fungibleSymbol f) (name f)
----

Now we can turn our attention to writing the wallet endpoint `forge`,
which will allow the wallet owner to forge new money (provided she is the issuer
of the currency). In order to do this, we first have to create an UTXO (with
zero value), at the
script address representing the monetary policy. We have to wait until that UTXO
is visible on the blockchain and then we can issue the forging transaction, which
will use the previous UTXO as input to validate its forging.

[source,haskell]
----
forge :: forall m. MonadWallet m
      => String  -- token name
      -> Integer -- quantity to forge
      -> m ()
forge n s = do

    key <- ownPubKey
    let f = Fungible
            { name   = TokenName $ C.pack n
            , issuer = key                                           -- <1>
            }
    logMsg $ T.pack $
        "forging " ++ show s ++ " of " ++ show f
    startWatching $ fungibleAddress f                                -- <2>

    void $ createTxAndSubmit                                         -- <3>
        defaultSlotRange
        Set.empty
        [ scriptTxOut
            zero
            (mkFungibleValidator f)
            fungibleDataScript
        ]
        [fungibleDataScript]

    go f

  where
    go :: Fungible -> m ()
    go f = do
        t <- trigger
        registerOnce t $ handler f

    trigger :: m EventTrigger
    trigger = do
        sl <- slot
        return $ slotRangeT $ intervalFrom $ sl + 1                  -- <4>

    handler :: Fungible -> EventHandler m
    handler f = EventHandler $ const $ do
        outs <- outputsAt $ fungibleAddress f
        case Map.keys outs of
            (ref : _) -> do                                          -- <5>
                let v = fungibleValue f s
                signTxAndSubmit_ Tx
                    { txInputs     = Set.singleton $ scriptTxIn      -- <6>
                                        ref
                                        (mkFungibleValidator f)
                                        (mkFungibleRedeemerValue s)
                                        unitData
                    , txOutputs    = [pubKeyTxOut v $ issuer f]      -- <7>
                    , txFee        = zero
                    , txForge      = v                               -- <8>
                    , txValidRange = defaultSlotRange
                    , txSignatures = Map.empty
                    , txData = Map.singleton (dataValueHash unitData) unitData
                    }
            _         -> go f                                        -- <9>
----

<1> The wallet owner must be the currency issuer.

<2> We need to watch the "monetary policy"-address of our currency in order to
be able to detect when the first transaction has been accepted onto the
blockchain.

<3> The first transaction has no inputs and zero value. All we need is a script
output at the "monetary policy"-address.

<4> We simply wait for one slot.

<5> The UTXO we need as input is on the blockchain.

<6> We use the UTXO created in the first step as input for the forging
transaction.

<7> The forged value goes to the wallet owner.

<8> Here we actually _forge_.

<9> If the transaction from the first step is not yet on the blockchain,
we keep waiting.

Once it is we are finished! Now everybody can use `forge` to forge her own currency!

This raises another question, though: How can we trade this new currency? -
We know that we can send and receive the new currency in the same way that we can send
and receive Ada. However, what if we want to sell some tokens of the new
currency for some Ada? Do we simply send the tokens and hope the buyer will send
Ada back? Do we expect the buyer to trust us and send the Ada first?

With Plutus smart contracts at our disposal, nobody has to trust anybody for
trades like this - we can simply define a script to guarantee that both parties
fulfill their obligations:

[source,haskell]
----
data Trade = Trade
    { value1   :: Value  -- What the seller wants to trade.
    , party1   :: PubKey -- The seller.
    , value2   :: Value  -- What the buyer wants to pay.
    , party2   :: PubKey -- The buyer.
    , deadline :: Slot   -- The trade has to be done before this.
    } deriving (Show, Generic, ToJSON, FromJSON, ToSchema)

makeLift ''Trade
----

We will write a script parameterized by a value of type `Trade` to
make sure that the seller and buyer both pay what they promised before the
deadline or both can get their money back.

This script will again be stateless, so the data script can again be `()`.
For the redeemer script, we distinguish between two possible actions,
_executing_ a successful trade or _reclaiming_ once funds in case of a failed
trade:

[source,haskell]
----
data TradeAction = Execute | Reclaim
    deriving Show

makeIsData ''TradeAction
makeLift ''TradeAction

type TradeValidator = () -> TradeAction -> PendingTx -> Bool

validateTrade :: Trade -> TradeValidator
validateTrade t () Execute tx = case pendingTxOutputs tx of        -- <1>
    [o1, o2] -> case (pendingTxOutType o1, pendingTxOutType o2) of -- <2>
        (PubKeyTxOut key1, PubKeyTxOut key2) ->                    -- <3>
               intervalTo (deadline t)                             -- <4>
                    `contains` pendingTxValidRange tx
            && key1 == party1 t                                    -- <5>
            && key2 == party2 t                                    -- <6>
            && pendingTxOutValue o1 == value2 t                    -- <7>
            && pendingTxOutValue o2 == value1 t                    -- <8>
        _                                    -> False
    _        -> False
validateTrade t () Reclaim tx = case pendingTxOutputs tx of
    [o] -> case pendingTxOutType o of
        PubKeyTxOut key ->
               intervalFrom (deadline t)
                    `contains` pendingTxValidRange tx
            && (   (   key == party1 t
                    && pendingTxOutValue o == value1 t)
                || (   key == party2 t
                    && pendingTxOutValue o == value2 t))
        _               -> False
    _   -> False
----

<1> The case of a successful trade.

<2> There must be exactly two outputs.

<3> Both outputs must be to public key addresses.

<4> This must happen before the deadline.

<5> The first output goes to the seller.

<6> The second output goes to the buyer.

<7> The seller gets the payment.

<8> The buyer gets the sold value.

We write the usual helper functions and have completed the on-chain part:

[source,haskell]
----
mkTradeValidator :: Trade -> Validator
mkTradeValidator =
      mkValidatorScript
    . applyCode $$(compile [|| v ||])
    . liftCode
    where v t = wrapValidator (validateTrade t)

tradeDataScript :: DataValue
tradeDataScript = DataValue $ toData ()

mkTradeRedeemerValue :: TradeAction -> RedeemerValue
mkTradeRedeemerValue = RedeemerValue . toData

tradeAddress :: Trade -> Address
tradeAddress = scriptAddress . mkTradeValidator
----

We need two endpoints, one for the seller, one for the buyer.
For simplicity, we will not trade arbitrary values, but only consider the case
where the seller sells some quantity of a single currency.

The seller will lock the currency she wants to sell at the script address
corresponding to the trade, then register two triggers.

* The first will trigger if the buyer locks the payment before the deadline. In
this case, the seller will execute the trade by creating a transaction that
gives the payment to herself and the currency to the buyer.
* The second will trigger once the deadline has been reached if the seller's
funds are still there.
When this happens, the trade has failed, and the seller will reclaim her currency.

[source,haskell]
----
sell :: forall m. MonadWallet m
     => PubKey  -- currency issuer
     -> String  -- currency name
     -> Integer -- currency quantity
     -> Value   -- price
     -> PubKey  -- buyer
     -> Slot    -- deadline
     -> m ()
sell i c n v2 p2 dl = do
    let f  = Fungible
                { issuer = i
                , name   = TokenName $ C.pack c
                }
        v1 = fungibleValue f n
    p1 <- ownPubKey
    let t = Trade
                { value1   = v1
                , party1   = p1
                , value2   = v2
                , party2   = p2
                , deadline = dl
                }
    logMsg $ T.pack $ "starting sale in " ++ show t

    tx <- payToScript                               -- <1>
        defaultSlotRange
        (tradeAddress t)
        v1
        tradeDataScript
    registerOnce (trigger1 t) (handler1 t)
    registerOnce (trigger2 t) (handler2 t tx)

  where
    trigger1 :: Trade -> EventTrigger               -- <2>
    trigger1 t =
        fundsAtAddressGeqT
            (tradeAddress t)
            (value1 t <> value2 t)
        `andT`
        slotRangeT
            (intervalTo $ deadline t)
    trigger2 t =                                    -- <3>
        fundsAtAddressGeqT
            (tradeAddress t)
            (value1 t)
        `andT`
        slotRangeT
            (intervalFrom $ deadline t)

    handler1 :: Trade -> EventHandler m             -- <4>
    handler1 t = EventHandler $ const $ do
        logMsg $ T.pack $ "executing " ++ show t
        m <- outputsAt $ tradeAddress t
        let ins  = Set.fromList
                [ scriptTxIn
                    r
                    (mkTradeValidator t)
                    (mkTradeRedeemerValue Execute)
                    unitData
                | r <- Map.keys m
                ]
            outs =
                [ pubKeyTxOut (value2 t) (party1 t)
                , pubKeyTxOut (value1 t) (party2 t)
                ]
        void $ createTxAndSubmit
            (intervalTo $ deadline t)
            ins
            outs
            [unitData]

    handler2 :: Trade -> Tx -> EventHandler m       -- <5>
    handler2 t tx = EventHandler $ const $ do
        logMsg $ T.pack $ "reclaiming " ++ show t
        collectFromScriptTxn
            (intervalFrom $ deadline t)
            (mkTradeValidator t)
            (mkTradeRedeemerValue Reclaim)
            (txId tx)
----

<1> Lock the currency at the trade script.

<2> Has the buyer paid before the deadline?

<3> Has the deadline passed and are my funds are still locked?

<4> Execute the trade.

<5> Reclaim the currency.

The `buy` endpoint for the buyer is a bit simpler, because the seller takes care
of executing a successful trade. Therefore the buyer only has to worry about
reclaiming her payment in case of a failed trade:

[source,haskell]
----
buy :: forall m. MonadWallet m
    => PubKey  -- currency issuer
    -> String  -- currency name
    -> Integer -- currency quantity
    -> PubKey  -- seller
    -> Value   -- price
    -> Slot    -- deadline
    -> m ()
buy i c n p1 v2 dl = do
    let f  = Fungible
                { issuer = i
                , name   = TokenName $ C.pack c
                }
        v1 = fungibleValue f n
    p2 <- ownPubKey
    let t = Trade
                { value1   = v1
                , party1   = p1
                , value2   = v2
                , party2   = p2
                , deadline = dl
                }
    logMsg $ T.pack $ "starting buy in " ++ show t

    tx <- payToScript                              -- <1>
        defaultSlotRange
        (tradeAddress t)
        v2
        tradeDataScript
    registerOnce (trigger t) (handler t tx)

  where
    trigger t =                                    -- <2>
        fundsAtAddressGeqT
            (tradeAddress t)
            (value2 t)
        `andT`
        slotRangeT
            (intervalFrom $ deadline t)

    handler :: Trade -> Tx -> EventHandler m       -- <3>
    handler t tx = EventHandler $ const $ do
        logMsg $ T.pack $ "reclaiming " ++ show t
        collectFromScriptTxn
            (intervalFrom $ deadline t)
            (mkTradeValidator t)
            (mkTradeRedeemerValue Reclaim)
            (txId tx)

$(mkFunctions ['forge, 'sell, 'buy])
----

<1> Lock the payment at the trade script.

<2> Has the deadline passed, and is my payment is still locked?

<3> Reclaim the payment.
