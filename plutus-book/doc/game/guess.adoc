[source,haskell]
----
{-# LANGUAGE DataKinds                       #-} -- <1>
{-# LANGUAGE NoImplicitPrelude               #-}
{-# LANGUAGE TemplateHaskell                 #-}
{-# OPTIONS_GHC -fno-warn-missing-signatures #-}

module Game.Guess where

import Language.PlutusTx
import Language.PlutusTx.Builtins (equalsByteString)
import Language.PlutusTx.Prelude
import Ledger
import Ledger.Ada
import Playground.Contract
import Wallet

import Data.ByteString.Lazy.Char8 (pack)

newtype HashedText = HashedText ByteString
newtype ClearText = ClearText ByteString

makeLift ''HashedText
makeLift ''ClearText
----

<1> Needed for on-chain code.

So we define one type, `HashedText`, for the hashed secret word
and another type, `ClearText`, for guesses.

We continue with two helper functions to create data- and redeemer scripts:

[source,haskell]
----
mkDataScript :: String -> DataScript
mkDataScript secret =
    let hashedText = HashedText $ sha2_256 $ pack secret
    in  DataScript $ lifted hashedText

mkRedeemerScript :: String -> RedeemerScript
mkRedeemerScript clear =
    let clearText = ClearText $ pack clear
    in  RedeemerScript $ lifted clearText
----

Function `mkDataScript` takes the secret word, hashes it, wraps it in our
`HashedText` type and finally _lifts_ it to a script for on-chain use.
Function `mkRedeemerScript` takes a guess, wraps it as a `ClearText` and lifts
it to a script.

To implement the validator script,
we remember that it will be called with three arguments, the data script,
the redeemer script and the pending transaction.
We can therefore start with a normal Haskell function `validate`
that takes a `HashedText` (for the data script),
a `ClearText` (for the redeemer script)
and a pending transaction and returns a `Bool` (indicating whether validation
succeeded),
then lift that function to a script:

[source,haskell]
----

type GameValidator = HashedText -> ClearText -> PendingTx -> Bool

validate :: GameValidator
validate (HashedText hashed) (ClearText clear) _ =
    equalsByteString hashed (sha2_256 clear)

gameValidator :: ValidatorScript
gameValidator = ValidatorScript $$(compileScript [|| validate ||])
----

So we turn `validate` into a `TExp GameValidator`
using Oxford brackets `[|| ...  ||]`,
compile this into a `TExp Script` with `compileScript`,
then splice the result as a script using `$( ... )`.

In order to lock funds with our script and later redeem those funds,
we need the script _address_, which we can compute from the validator script
with `scriptAddress`:

[source,haskell]
----
gameAddress :: Address
gameAddress = scriptAddress gameValidator
----

All that is still missing are Wallet endpoints
to interact with the script. We need three:

- A function `startGame` to be invoked by players who want to guess the secret
  word. This function will _watch_ the game address, so that their wallet will
  be informed about newly locked funds.
  (It would be infeasible for a wallet to keep track of all unspent transaction
  outputs on the whole ledger.
  We have to tell it which particular addresses to "watch".)

- A function `lock` taking the secret word and an amount,
  which will lock that amount at the script address.

- A function `guess` for players to try and guess the secret word
  and collect the locked funds if they guess correctly.

[source,haskell]
----
startGame :: MonadWallet m => m ()
startGame = startWatching gameAddress

lock :: MonadWallet m => String -> Ada -> m () -- <1>
lock secret ada = payToScript_
    defaultSlotRange
    gameAddress
    (toValue ada)
    (mkDataScript secret)

guess :: MonadWallet m => String -> m ()
guess word = collectFromScript                 -- <2>
    defaultSlotRange
    gameValidator
    (mkRedeemerScript word)

$(mkFunctions ['startGame, 'lock, 'guess])
----

<1> Function `payToScript_` is another helper function like
    `payToPublicKey_`, which makes it easy for wallet code to
    send funds to an address locked by a script.
    It takes the slot range, the script address, the amount and the data script
    as arguments.

<2> Where `payToScript_` allows us to send funds _to_ a script address,
    function `collectFromScript` allows us to take money _from_ a script
    address. Its arguments are the slot range, the validator script and the
    redeemer script.
