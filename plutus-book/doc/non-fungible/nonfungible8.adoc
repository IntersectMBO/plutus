////
[source,haskell]
----
{-# LANGUAGE DataKinds                       #-}
{-# LANGUAGE DeriveAnyClass                  #-}
{-# LANGUAGE NoImplicitPrelude               #-}
{-# LANGUAGE ScopedTypeVariables             #-}
{-# LANGUAGE TemplateHaskell                 #-}
{-# OPTIONS_GHC -fno-warn-missing-signatures #-}

module NonFungible.NonFungible8 where

import           Prelude                    (Applicative (..))

import           Language.PlutusTx
import           Language.PlutusTx.Prelude  hiding (Applicative (..))
import           Ledger
import           Ledger.Typed.Scripts       (wrapValidator)
import qualified Ledger.Ada                 as A
import qualified Ledger.Value               as V
import           Playground.Contract
import           Wallet

import           Control.Monad (void)
import           Control.Monad.Except       (MonadError (..))
import qualified Data.ByteString.Lazy.Char8 as C
import qualified Data.Map.Strict            as Map
import qualified Data.Set                   as Set
import qualified Data.Text                  as T
----
////

[source,haskell]
----
-- The admin token is parameterized by a transaction
-- output, which in turn is given by the hash of a
-- transaction and the output index.
type Admin = (TxId, Integer)

-- Convert the reference to an output to a hash-index
-- pair.
mkAdmin :: TxOutRef -> Admin
mkAdmin (TxOutRef h i) = (h, i)

-- We need no data in data- and redeemer-scripts,
-- so both can be of unit type.
type AdminValidator = () -> () -> PendingTx -> Bool

validateAdmin :: Admin -> AdminValidator
validateAdmin (h, i) () () tx =
       spendsOutput tx h i               -- <1>
    && case pendingTxOutputs tx of
        (o : _) -> V.valueOf
            (pendingTxOutValue o)
            (ownCurrencySymbol tx)
            adminTokenName
            == 1                         -- <2>
        []      -> False                 -- <3>
----

<1> We make sure that a transaction forging the admin token
_spends_ the output represented by hash `h` and index `i`.

<2> The admin token must be included in the first output with quantity _one_.

<3> There must be at least one output.

We add some standard helpers:

[source,haskell]
----
adminRedeemer :: RedeemerValue
adminRedeemer = RedeemerValue $ toData ()

mkAdminValidator :: Admin -> Validator
mkAdminValidator = mkValidatorScript
                       . applyCode $$(compile [|| \a -> wrapValidator (validateAdmin a) ||])
                       . liftCode

adminAddress :: Admin -> Address
adminAddress = scriptAddress . mkAdminValidator

adminSymbol :: Admin -> CurrencySymbol
adminSymbol admin = scriptCurrencySymbol $ mkAdminValidator admin

adminTokenName :: TokenName
adminTokenName = TokenName emptyByteString

-- The value of the admin token.
adminValue :: Admin -> Value
adminValue admin = V.singleton (adminSymbol admin) adminTokenName 1
----

We need to make some changes to our existing code to incorporate the admin-token
idea:

[source,haskell,highlight='3-3']
----
data NonFungible = NonFungible
    { issuer        :: PubKey
    , adminCurrency :: CurrencySymbol                      -- <1>
    } deriving (Show, Generic, ToJSON, FromJSON, ToSchema)

makeLift ''NonFungible
----

<1> Our `NonFungible` record needs a new field, the
currency symbol of the admin token.

[source,haskell,highlight='14-14,20-20']
----
type NonFungibleValidator =
       ()
    -> TokenName
    -> PendingTx
    -> Bool

validateNonFungible :: NonFungible -> NonFungibleValidator
validateNonFungible nf () name tx =
       txSignedBy tx (issuer nf)
    && case (pendingTxInputs tx, pendingTxOutputs tx) of
        ([i], os@(o : _)) ->
            let inValue = pendingTxInValue i
            in     foldMap pendingTxOutValue os
                    == (inValue + v2)               -- <1>
                && pendingTxOutValue o
                    == (inValue + v)
                && V.valueOf inValue s name == 0
                && V.valueOf
                    inValue
                    (adminCurrency nf)                     -- <2>
                    adminTokenName
                   == 1
                && case pendingTxOutType o of
                    ScriptTxOut vh _ -> vh == ownHash tx
                    _                -> False
        _                 -> False
  where
    s :: CurrencySymbol
    s = ownCurrencySymbol tx

    v, v2 :: Value
    v  = V.singleton s name 1
    v2 = v + v
----

<1> We only need to forge _two_ tokens, not _three_ anymore.

<2> The admin token must be part of the script input.

////
[source,haskell]
----
mkNonFungibleRedeemer :: String -> RedeemerValue
mkNonFungibleRedeemer name = RedeemerValue $ toData $ TokenName $ C.pack name

mkNonFungibleValidator :: NonFungible -> Validator
mkNonFungibleValidator = mkValidatorScript
                       . applyCode $$(compile [|| \nf -> wrapValidator (validateNonFungible nf) ||])
                       . liftCode

nonFungibleAddress :: NonFungible -> Address
nonFungibleAddress = scriptAddress . mkNonFungibleValidator

nonFungibleSymbol :: NonFungible -> CurrencySymbol
nonFungibleSymbol nf = scriptCurrencySymbol $ mkNonFungibleValidator nf

nonFungibleValue :: NonFungible -> String -> Value
nonFungibleValue nf name = V.singleton
    (nonFungibleSymbol nf)
    (TokenName $ C.pack name)
    1

mkNonFungibleTxOut :: NonFungible -> Value -> TxOut
mkNonFungibleTxOut nf v =
    scriptTxOut
        v
        (mkNonFungibleValidator nf)
        unitData
----
////

We add one helper to check whether the admin token is included in a given
transaction output:

[source,haskell]
----
hasAdminToken :: CurrencySymbol -> (TxOutRef, TxOutTx) -> Bool
hasAdminToken s (_, o) =
    V.valueOf (txOutValue $ txOutTxOut o) s adminTokenName == 1
----

Now we can write our endpoints. The biggest change is in `start`, where
we now first have to forge the admin token before we can start our policy.

We also rename `start` to `start'`, because for later use we want a version
of `start` that _returns_ the symbol of our policy
.
It is not allowed to have wallet endpoints in the Playground return types other than `()`, though,
so afterwards we will define `start` in terms of `start'`.

[source,haskell]
----
start' :: forall m. MonadWallet m => m CurrencySymbol
start' = do

    key  <- ownPubKey
    outs <- outputsAt $ pubKeyAddress key
    case Map.toList outs of
        []             -> throwError $
            OtherError $ T.pack "need at least one output"
        ((ref, o) : _) -> do                               -- <1>
            let admin = mkAdmin ref
            startWatching $ adminAddress admin
            logMsg $ T.pack $
                "starting admin " ++ show admin
            void $ createTxAndSubmit                       -- <2>
                defaultSlotRange
                Set.empty
                [scriptTxOut
                    zero
                    (mkAdminValidator admin)
                    unitData]
                [unitData]
            go1 ref $ txOutValue $ txOutTxOut o                         -- <3>
            pure (adminSymbol admin)

  where
    go1 :: TxOutRef -> Value -> m ()
    go1 ref v = do
        t <- trigger
        registerOnce t $ handler1 ref v

    trigger :: m EventTrigger
    trigger = do
        sl <- slot
        return $ slotRangeT $ intervalFrom $ sl + 1

    handler1 :: TxOutRef -> Value -> EventHandler m
    handler1 ref v = EventHandler $ const $ do
        let admin = mkAdmin ref
        outs <- outputsAt $ adminAddress admin
        case Map.keys outs of
            []         -> go1 ref v                        -- <4>
            (ref' : _) -> do
                key <- ownPubKey
                let i1 = pubKeyTxIn key ref                -- <5>
                    i2 = scriptTxIn                        -- <6>
                            ref'
                            (mkAdminValidator admin)
                            unitRedeemer
                            unitData
                    o  = pubKeyTxOut                       -- <7>
                            (v + adminValue admin)
                            key
                signTxAndSubmit_ Tx
                    { txInputs     = Set.fromList [i1, i2]
                    , txOutputs    = [o]
                    , txFee        = zero
                    , txForge      = adminValue admin
                    , txValidRange = defaultSlotRange
                    , txSignatures = Map.empty
                    , txData       = Map.empty
                    }
                logMsg $ T.pack $
                    "forging admin token " ++
                    show (adminSymbol admin)

                go2 (adminSymbol admin)                    -- <8>

    go2 :: CurrencySymbol -> m ()
    go2 s = do
        t <- trigger
        registerOnce t $ handler2 s

    handler2 :: CurrencySymbol -> EventHandler m
    handler2 s = EventHandler $ const $ do
        key  <- ownPubKey
        outs <- outputsAt $ pubKeyAddress key
        case find (hasAdminToken s) $ Map.toList outs of
            Nothing       -> go2 s
            Just (ref, o) -> do                            -- <9>
                let nf = NonFungible
                            { issuer        = key
                            , adminCurrency = s
                            }
                logMsg $ T.pack $
                    "starting tokens " ++ show nf
                let v  = V.singleton s adminTokenName 1
                    i  = pubKeyTxIn key ref                -- <10>
                    o1 = scriptTxOut                       -- <11>
                            v
                            (mkNonFungibleValidator nf)
                            unitData
                    o2 = pubKeyTxOut                       -- <12>
                            ((txOutValue $ txOutTxOut o) - v)
                            key
                void $ createTxAndSubmit
                    defaultSlotRange
                    (Set.singleton i)
                    [o1, o2]
                    [unitData]

start :: MonadWallet m => m ()
start = void start'
----

<1> We pick one of our outputs as the unique output used by the admin token
policy.

<2> We create a script output for our admin token policy.

<3> We wait until the script output becomes available.

<4> If the script output is not yet available, we keep waiting.

<5> First input is our unique output.

<6> Second input is the script output we waited for.

<7> The output goes to us, and it includes the value from the unique input (no
sense in wasting that money...) and the newly forged admin token.

<8> We wait again, this time for the admin token to become available.

<9> We pick the output containing the admin token.

<10> Input will be the output containing the admin token.

<11> The first output to the monetary policy for our tokens contains the admin
token.

<12> The second output gets the excess value contained in the input.

[source,haskell,highlight='33-33,43-43']
----
forge :: forall m. MonadWallet m
      => CurrencySymbol -- admin token symbol
      -> String         -- token name
      -> m ()
forge s n = do

    key <- ownPubKey
    let nf = NonFungible
                { issuer        = key
                , adminCurrency = s
                }
    logMsg $ T.pack $
        "forging " ++ n ++
        " (symbol " ++ show (nonFungibleSymbol nf) ++
        ") of " ++ show nf

    outs <- outputsAt $ nonFungibleAddress nf
    case findOut s $ Map.toList outs of
        Just (ref, o) -> do
            let v    = nonFungibleValue nf n
                v2   = v + v
                vIn  = txOutValue $ txOutTxOut o
                vOut = vIn + v
            signTxAndSubmit_ Tx
                { txInputs     = Set.singleton $ scriptTxIn
                                    ref
                                    (mkNonFungibleValidator nf)
                                    (mkNonFungibleRedeemer n)
                                    unitData
                , txOutputs    = [ mkNonFungibleTxOut nf vOut
                                 , pubKeyTxOut v key
                                 ]
                , txFee        = zero
                , txForge      = v2                               -- <1>
                , txValidRange = defaultSlotRange
                , txSignatures = Map.empty
                , txData       = Map.singleton (dataValueHash unitData) unitData
                }
        _         -> throwError $
                        OtherError $ T.pack "'start' has not run"
  where
    findOut :: CurrencySymbol
            -> [(TxOutRef, TxOutTx)]
            -> Maybe (TxOutRef, TxOutTx)
    findOut = find . hasAdminToken                                -- <2>

$(mkFunctions ['start, 'forge])
----

<1> We only forge the token twice, not thrice any longer.

<2> Function `findOut` is much simpler now: We just look for the presence of the
admin token.
