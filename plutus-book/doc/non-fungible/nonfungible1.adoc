[source,haskell]
----
{-# LANGUAGE DataKinds                       #-}
{-# LANGUAGE DeriveAnyClass                  #-}
{-# LANGUAGE NoImplicitPrelude               #-}
{-# LANGUAGE ScopedTypeVariables             #-}
{-# LANGUAGE TemplateHaskell                 #-}
{-# OPTIONS_GHC -fno-warn-missing-signatures #-}

module NonFungible.NonFungible1 where

import           Language.PlutusTx
import           Language.PlutusTx.Prelude
import           Ledger
import           Ledger.Typed.Scripts       (wrapValidator)
import qualified Ledger.Ada                 as A
import qualified Ledger.Value               as V
import           Playground.Contract
import           Wallet

import           Control.Monad (void)
import           Control.Monad.Except       (MonadError (..))
import qualified Data.ByteString.Lazy.Char8 as C
import qualified Data.Map.Strict            as Map
import qualified Data.Set                   as Set
import qualified Data.Text                  as T

data NonFungible = NonFungible
    { issuer :: PubKey                                     -- <1>
    } deriving (Show, Generic, ToJSON, FromJSON, ToSchema)

makeLift ''NonFungible
----

<1> All we have to remember (for now) is our own public key.

The <<endmatter#data-script>> can be `()` again,
the <<endmatter#redeemer-script>> should contain the name of the token we want
to forge. This leads to the following validation logic:

[source,haskell]
----
type NonFungibleValidator =
       ()
    -> TokenName
    -> PendingTx
    -> Bool

validateNonFungible :: NonFungible -> NonFungibleValidator
validateNonFungible nf () name tx =
       txSignedBy tx (issuer nf)                 -- <1>
    && foldMap pendingTxOutValue (pendingTxOutputs tx) == v -- <2>
  where
    v :: Value
    v = V.singleton
            (ownCurrencySymbol tx)               -- <3>
            name
            1
----

<1> The transaction must be signed by us, so nobody else can forge new tokens.

<2> The sum of all output values should consist of exactly the newly forged
token.

<3> The <<endmatter#currency-symbol>> of the token will be the hash of our
<<endmatter#validator-script>>.

We create some helper functions next:

[source,haskell]
----
mkNonFungibleRedeemer :: String -> RedeemerValue
mkNonFungibleRedeemer name = RedeemerValue $ toData $ TokenName $ C.pack name

mkNonFungibleValidator :: NonFungible -> Validator
mkNonFungibleValidator = mkValidatorScript
                       . applyCode $$(compile [|| \nf -> wrapValidator (validateNonFungible nf) ||])
                       . liftCode

nonFungibleAddress :: NonFungible -> Address
nonFungibleAddress = scriptAddress . mkNonFungibleValidator

nonFungibleSymbol :: NonFungible -> CurrencySymbol
nonFungibleSymbol nf = scriptCurrencySymbol $ mkNonFungibleValidator nf

-- Given a token name (represented by a string for convenience),
-- return the value of the corresponding token.
nonFungibleValue :: NonFungible -> String -> Value
nonFungibleValue nf name = V.singleton
    (nonFungibleSymbol nf)
    (TokenName $ C.pack name)
    1

-- Create a script output for our monetary policy.
mkNonFungibleTxOut :: NonFungible -> TxOut
mkNonFungibleTxOut nf =
    scriptTxOut
        zero
        (mkNonFungibleValidator nf)
        unitData                           -- <1>
----

<1> The `unitData` helper creates a data script of type `()` for us.

Now we can write the wallet endpoints. We will provide two:

- `start` to initialize our business by creating one output of the script
  representing our monetary policy and
- `forge` to actually forge a new token. For simplicity, we will always send the
  newly forged token to our own wallet.

[source,haskell]
----
start :: MonadWallet m => m ()
start = do

    key <- ownPubKey
    let nf = NonFungible {issuer = key}   -- <1>
    logMsg $ T.pack $
        "starting " ++ show nf
    startWatching $ nonFungibleAddress nf -- <2>

    void $ createTxAndSubmit              -- <3>
        defaultSlotRange
        Set.empty
        [mkNonFungibleTxOut nf]
        [unitData]
----

<1> We will be the issuer, of course.

<2> We need to watch the address corresponding to our monetary policy,
because we will need to find an output at that address in order to forge.

<3> We create one output corresponding to our monetary policy.

[source,haskell]
----
forge :: forall m. MonadWallet m
      => String -- token name
      -> m ()
forge n = do

    key <- ownPubKey
    let nf = NonFungible {issuer = key}
    logMsg $ T.pack $
        "forging " ++ n ++ " of " ++ show nf

    outs <- outputsAt $ nonFungibleAddress nf                       -- <1>
    case Map.keys outs of
        [ref] -> do                                                 -- <2>
            let v = nonFungibleValue nf n                           -- <3>
            signTxAndSubmit_ Tx
                { txInputs     = Set.singleton $ scriptTxIn         -- <4>
                                    ref
                                    (mkNonFungibleValidator nf)
                                    (mkNonFungibleRedeemer n)
                                    unitData
                , txOutputs    = [ pubKeyTxOut v $ issuer nf        -- <5>
                                 , mkNonFungibleTxOut nf
                                 ]
                , txFee        = zero
                , txForge      = v                                  -- <6>
                , txValidRange = defaultSlotRange
                , txSignatures = Map.empty
                , txData       = Map.singleton (dataValueHash unitData) unitData
                }
        _         -> throwError $                                   -- <7>
                        OtherError $ T.pack "'start' has not run"

$(mkFunctions ['start, 'forge])
----

<1> We grab all outputs corresponding to our monetary policy.

<2> There should be exactly one such output.

<3> Value of the token we want to forge.

<4> We need the monetary policy output as input for our forging transaction.

<5> We need two outputs, one for the forged token to go to, one for the monetary
policy (so that we have that available for forging the next token).

<6> Here we actually _forge_.

<7> The output is not there, so we probably forgot to run `start` first.
