////
[source,haskell]
----
{-# LANGUAGE DataKinds                       #-}
{-# LANGUAGE DeriveAnyClass                  #-}
{-# LANGUAGE NoImplicitPrelude               #-}
{-# LANGUAGE ScopedTypeVariables             #-}
{-# LANGUAGE TemplateHaskell                 #-}
{-# OPTIONS_GHC -fno-warn-missing-signatures #-}

module NonFungible.NonFungible6 where

import           Language.PlutusTx
import qualified Language.PlutusTx.AssocMap as AM
import           Language.PlutusTx.Prelude
import           Ledger
import           Ledger.Typed.Scripts       (wrapValidator)
import qualified Ledger.Ada                 as A
import qualified Ledger.Value               as V
import           Playground.Contract
import           Wallet

import           Control.Monad (void)
import           Control.Monad.Except       (MonadError (..))
import qualified Data.ByteString.Lazy.Char8 as C
import qualified Data.Map.Strict            as Map
import qualified Data.Set                   as Set
import qualified Data.Text                  as T

data NonFungible = NonFungible
    { issuer :: PubKey
    } deriving (Show, Generic, ToJSON, FromJSON, ToSchema)

makeLift ''NonFungible

type NonFungibleValidator =
       ()
    -> TokenName
    -> PendingTx
    -> Bool
----
////

[source,haskell]
----
validateNonFungible :: NonFungible -> NonFungibleValidator
validateNonFungible nf () name tx =
       txSignedBy tx (issuer nf)
    && case (pendingTxInputs tx, pendingTxOutputs tx) of
        ([i], os@(o : _)) ->                                -- <1>
            let inValue = pendingTxInValue i                -- <2>
            in     foldMap pendingTxOutValue os
                    == (inValue + v3)                -- <3>
                && pendingTxOutValue o
                    == (inValue + v2)                -- <4>
                && V.valueOf inValue s name == 0            -- <5>
                && case pendingTxOutType o of
                    ScriptTxOut vh _ -> vh == ownHash tx        -- <6>
                    _                -> False
        _                 -> False
  where
    s :: CurrencySymbol
    s = ownCurrencySymbol tx

    v, v2, v3 :: Value
    v  = V.singleton s name 1
    v2 = v + v
    v3 = v2 + v
----

<1> We expect exactly one input, the script input we are currently validating,
and at least one output, the first of which is the new script output.

<2> The input value will consist of all tokens we have already forged.

<3> We forge the token thrice.

<4> The new script value must "trap" two of the newly forged tokens.

<5> Here we check that the input value does not already contain the token we
want to forge.

<6> The first output (which contains the "trapped" two extra tokens) must go to the
monetary policy script again.

////
[source,haskell]
----
mkNonFungibleRedeemer :: String -> RedeemerValue
mkNonFungibleRedeemer name = RedeemerValue $ toData $ TokenName $ C.pack name

mkNonFungibleValidator :: NonFungible -> Validator
mkNonFungibleValidator = mkValidatorScript
                       . applyCode $$(compile [|| \nf -> wrapValidator (validateNonFungible nf) ||])
                       . liftCode

nonFungibleAddress :: NonFungible -> Address
nonFungibleAddress = scriptAddress . mkNonFungibleValidator

nonFungibleSymbol :: NonFungible -> CurrencySymbol
nonFungibleSymbol nf = scriptCurrencySymbol $ mkNonFungibleValidator nf

nonFungibleValue :: NonFungible -> String -> Value
nonFungibleValue nf name = V.singleton
    (nonFungibleSymbol nf)
    (TokenName $ C.pack name)
    1
----
////

[source,haskell]
----
mkNonFungibleTxOut :: NonFungible -> Value -> TxOut -- <1>
mkNonFungibleTxOut nf v =
    scriptTxOut
        v
        (mkNonFungibleValidator nf)
        unitData
----

<1> We add another argument of type `Value` to this helper function,
because our script outputs will not all have value zero any longer.

////
[source,haskell]
----
start :: MonadWallet m => m ()
start = do

    key <- ownPubKey
    let nf = NonFungible {issuer = key}
    logMsg $ T.pack $
        "starting " ++ show nf
    startWatching $ nonFungibleAddress nf

    void $ createTxAndSubmit
        defaultSlotRange
        Set.empty
        [mkNonFungibleTxOut nf zero]
        [unitData]
----
////

[source,haskell,highlight='12-26,28-28,35-42,49-55']
----
forge :: forall m. MonadWallet m
      => String -- token name
      -> m ()
forge n = do

    key <- ownPubKey
    let nf = NonFungible {issuer = key}
    logMsg $ T.pack $
        "forging " ++ n ++ " of " ++ show nf

    outs <- outputsAt $ nonFungibleAddress nf
    case findOut (nonFungibleSymbol nf) $ Map.toList outs of        -- <1>
        Just (ref, o) -> do
            let v    = nonFungibleValue nf n
                v2   = v + v
                v3   = v2 + v
                vIn  = txOutValue $ txOutTxOut o
                vOut = vIn + v2
            signTxAndSubmit_ Tx
                { txInputs     = Set.singleton $ scriptTxIn
                                    ref
                                    (mkNonFungibleValidator nf)
                                    (mkNonFungibleRedeemer n)
                                    unitData
                , txOutputs    = [ mkNonFungibleTxOut nf vOut       -- <2>
                                 , pubKeyTxOut v key                -- <3>
                                 ]
                , txFee        = zero
                , txForge      = v3                                 -- <4>
                , txValidRange = defaultSlotRange
                , txSignatures = Map.empty
                , txData       = Map.singleton (dataValueHash unitData) unitData
                }
        _         -> throwError $
                        OtherError $ T.pack "'start' has not run"
  where
    findOut :: CurrencySymbol
            -> [(TxOutRef, TxOutTx)]
            -> Maybe (TxOutRef, TxOutTx)
    findOut s xs =
        let ys = filter f xs                                        -- <5>
        in  case find (ours s . txOutTxOut . snd) ys of               -- <6>
                Just x  -> Just x
                Nothing -> find (zeroValue . txOutTxOut . snd) ys                     -- <7>

    f :: (TxOutRef, TxOutTx) -> Bool
    f (_, o) = case txOutTxData o of
        Nothing  -> False
        Just ds -> ds == unitData

    ours :: CurrencySymbol -> TxOut -> Bool
    ours s o = case AM.lookup s $ V.getValue $ txOutValue o of
        Nothing -> False
        Just m  -> any (>= 2) [i | (_, i) <- AM.toList m]

    zeroValue :: TxOut -> Bool
    zeroValue o = txOutValue o == zero

$(mkFunctions ['start, 'forge])
----

<1> We use the helper function `findOut` to find the right script output (if it exists).

<2> The first output must be the script output; its value is the value of the
script input plus _two_ of the newly forged tokens.

<3> The third token goes to our own address.

<4> We forge _three_ new tokens.

<5> First we filter for data scripts with the right type as before.

<6> Then we look for outputs that contain one token at least twice. Such an
output must have been created by us.

<7> If we do not find such an output, we settle for one with value zero. This
could come from somebody else, but that would not do us any harm.
