////
[source,haskell]
----
{-# LANGUAGE DataKinds                       #-}
{-# LANGUAGE DeriveAnyClass                  #-}
{-# LANGUAGE NoImplicitPrelude               #-}
{-# LANGUAGE ScopedTypeVariables             #-}
{-# LANGUAGE TemplateHaskell                 #-}
{-# OPTIONS_GHC -fno-warn-missing-signatures #-}

module NonFungible.NonFungible2 where

import           Language.PlutusTx
import           Language.PlutusTx.Prelude
import           Ledger
import           Ledger.Typed.Scripts       (wrapValidator)
import qualified Ledger.Ada                 as A
import qualified Ledger.Value               as V
import           Playground.Contract
import           Wallet
import           Wallet.Emulator            (walletPubKey)

import           Control.Monad (void)
import           Control.Monad.Except       (MonadError (..))
import qualified Data.ByteString.Lazy.Char8 as C
import qualified Data.Map.Strict            as Map
import           Data.Maybe                 (maybeToList)
import qualified Data.Set                   as Set
import qualified Data.Text                  as T

data NonFungible = NonFungible
    { issuer :: PubKey
    } deriving (Show, Generic, ToJSON, FromJSON, ToSchema)

makeLift ''NonFungible

type NonFungibleValidator =
       ()
    -> TokenName
    -> PendingTx
    -> Bool

validateNonFungible :: NonFungible -> NonFungibleValidator
validateNonFungible nf () name tx =
       txSignedBy tx (issuer nf)
    && foldMap pendingTxOutValue (pendingTxOutputs tx) == v
  where
    v :: Value
    v = V.singleton
            (ownCurrencySymbol tx)
            name
            1

mkNonFungibleRedeemer :: String -> RedeemerValue
mkNonFungibleRedeemer name = RedeemerValue $ toData $ TokenName $ C.pack name

mkNonFungibleValidator :: NonFungible -> Validator
mkNonFungibleValidator = mkValidatorScript
                       . applyCode $$(compile [|| \nf -> wrapValidator (validateNonFungible nf) ||])
                       . liftCode

nonFungibleAddress :: NonFungible -> Address
nonFungibleAddress = scriptAddress . mkNonFungibleValidator

nonFungibleSymbol :: NonFungible -> CurrencySymbol
nonFungibleSymbol nf = scriptCurrencySymbol $ mkNonFungibleValidator nf

nonFungibleValue :: NonFungible -> String -> Value
nonFungibleValue nf name = V.singleton
    (nonFungibleSymbol nf)
    (TokenName $ C.pack name)
    1

mkNonFungibleTxOut :: NonFungible -> TxOut
mkNonFungibleTxOut nf =
    scriptTxOut
        zero
        (mkNonFungibleValidator nf)
        unitData

start :: MonadWallet m => m ()
start = do

    key <- ownPubKey
    let nf = NonFungible {issuer = key}
    logMsg $ T.pack $
        "starting " ++ show nf
    startWatching $ nonFungibleAddress nf

    void $ createTxAndSubmit
        defaultSlotRange
        Set.empty
        [mkNonFungibleTxOut nf]
        [unitData]

forge :: forall m. MonadWallet m
      => String -- token name
      -> m ()
forge n = do

    key <- ownPubKey
    let nf = NonFungible {issuer = key}
    logMsg $ T.pack $
        "forging " ++ n ++ " of " ++ show nf

    outs <- outputsAt $ nonFungibleAddress nf
    case Map.keys outs of
        [ref] -> do
            let v = nonFungibleValue nf n
            signTxAndSubmit_ Tx
                { txInputs     = Set.singleton $ scriptTxIn
                                    ref
                                    (mkNonFungibleValidator nf)
                                    (mkNonFungibleRedeemer n)
                                    unitData
                , txOutputs    = [ pubKeyTxOut v $ issuer nf
                                 , mkNonFungibleTxOut nf
                                 ]
                , txFee        = zero
                , txForge      = v
                , txValidRange = defaultSlotRange
                , txSignatures = Map.empty
                , txData       = Map.singleton (dataValueHash unitData) unitData
                }
        _         -> throwError $
                        OtherError $ T.pack "'start' has not run"
----
////

[source,haskell]
----

-- This will be run by a different wallet.
prank :: MonadWallet m => Wallet -> m ()
prank w = do

    let nf = NonFungible {issuer = walletPubKey w} -- <1>
    logMsg $ T.pack $
        "playing prank on " ++ show nf

    let v = A.toValue 1
    (ins, mout) <- createPaymentWithChange v
    pk          <- ownPubKey
    void $ createTxAndSubmit                       -- <2>
        defaultSlotRange
        ins
        ( scriptTxOut
            zero
            (mkNonFungibleValidator nf)
            unitData
        : pubKeyTxOut v pk
        : maybeToList mout)
        [unitData]

$(mkFunctions ['start, 'forge, 'prank])
----

<1> We use `walletPubKey` from module `Wallet.Emulator` to
easily lookup the public key belonging to a wallet.

<2> We create a transaction with an input of 1 ada and two outputs,
one giving us the 1 ada back and another (with zero value) to the monetary
policy script.

Why do we need an input of 1 ada, which we immediately pay back anyway? Why can
we not simply do the same that `start` does? If we did that, the transaction
the prankster creates would have the same identifier as that created by `start`,
because both transactions would have exactly the same inputs and outputs.
The script output would then be indistinguishable from the regular one created by `start`.
But the whole point of the prank
is to create a _different_ script output to break our code!

Note that this is only an issue when we test in the Playground; On a real
blockchain, each transaction will have to pay a _fee_, so there will never be
two transactions with the same inputs and outputs
since the blockchain prevents double spending. Thus there will never be two
valid transactions on the blockchain which have the same identifier.
