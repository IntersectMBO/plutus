[source,haskell]
----
{-# LANGUAGE DataKinds                       #-}
{-# LANGUAGE DeriveAnyClass                  #-}
{-# LANGUAGE NoImplicitPrelude               #-}
{-# LANGUAGE ScopedTypeVariables             #-}
{-# LANGUAGE TemplateHaskell                 #-}
{-# OPTIONS_GHC -fno-warn-missing-signatures #-}

module Multi.Vesting where

import           Language.PlutusTx
import           Language.PlutusTx.Prelude  hiding (foldMap)
import           Ledger
import           Ledger.Ada
import           Ledger.Typed.Scripts       (wrapValidator)
import           Playground.Contract
import           Wallet

import           Control.Monad              (void)
import           Data.Foldable              (foldMap)
import           Data.Map                   (Map)
import qualified Data.Map                   as Map
import           Data.Set                   (Set)
import qualified Data.Set                   as Set
import           Data.Text                  (pack)

data Tranche = Tranche
    { date   :: Slot
    , amount :: Ada
    } deriving (Show, Generic, ToJSON, FromJSON, ToSchema)

makeLift ''Tranche

data Vesting = Vesting
    { tranche1 :: Tranche
    , tranche2 :: Tranche
    , owner    :: PubKey
    } deriving (Show, Generic, ToJSON, FromJSON, ToSchema)

makeLift ''Vesting
----

Let us also write two helper functions to work with tranches and
vesting schemes:

[source,haskell]
----
-- The total amount locked in the scheme.
totalAmount :: Vesting -> Ada
totalAmount v = amount (tranche1 v) + amount (tranche2 v)

-- Funds guaranteed to be available in that range.
availableFrom:: Vesting -> SlotRange -> Ada
availableFrom v r =
           availableFromTranche (tranche1 v)
    + availableFromTranche (tranche2 v)
  where
    availableFromTranche :: Tranche -> Ada
    availableFromTranche t =
        if intervalFrom (date t) `contains` r
            then amount t
            else 0
----

Now it is time to think about suitable types for the data- and redeemer scripts.
The only thing that changes during the lifetime of a vesting scheme
is how much money has already been collected,
and that information can be observed without any additional data.
This means we can use the unit type `()` for our data scripts.

The role of the redeemer script is to indicate the intent of collecting from the
script, but in this example, there is only one intent: withdrawing funds.
So we can choose `()` for the redeemer scripts, too.

[source,haskell]
----
dataValue :: DataValue
dataValue = DataValue $ toData ()

-- The redeemer is passed the data script (unit), which it just ignores.
redeemerValue :: RedeemerValue
redeemerValue = RedeemerValue $ toData ()
----

We are now in a position to define the validator script.
Once more we will define an ordinary Haskell function `validate` first
that does all the work, and we will parameterize the validator script by
the vesting scheme with the technique that we learned in the last chapter.

Function `validate` has to check that we do not collect more from the script
than is allowed at that time, i.e. that we lock enough money back into the
script, and that the transaction is signed by the vesting scheme owner.

[source,haskell]
----
type VestingValidator = () -> () -> PendingTx -> Bool

validate :: Vesting -> VestingValidator
validate v () () tx =
    let hash       = ownHash tx                                      -- <1>
        available  = availableFrom v (pendingTxValidRange tx)        -- <2>
        unreleased = totalAmount v - available                 -- <3>
        remaining  = adaLockedBy tx hash                             -- <4>
    in     (remaining >= unreleased)
        && (tx `txSignedBy` owner v)

mkValidator :: Vesting -> Validator
mkValidator = mkValidatorScript
                  . applyCode $$(compile [|| \v -> wrapValidator (validate v) ||])
                  . liftCode
----

<1> Our validator hash.

<2> The funds that have already been released.

<3> The funds have not yet been released.

<4> The funds this transaction leaves in the contract.

Now we are ready to define our wallet endpoints, of which we need three:

* `registerScheme` - used by the owner to start watching his vesting scheme
* `vest` - for somebody to lock funds in a vesting scheme
* `withdraw` - for the owner to withdraw funds from his vesting scheme

[source,haskell]
----
vestingAddress :: Vesting -> Address
vestingAddress = scriptAddress . mkValidator

registerScheme :: MonadWallet m => Tranche -> Tranche -> m ()
registerScheme t1 t2 = do
    key <- ownPubKey
    let v = Vesting
            { tranche1 = t1
            , tranche2 = t2
            , owner    = key
            }
    logMsg $ pack $ "registering " ++ show v
    startWatching $ vestingAddress v

vest :: MonadWallet m => Vesting -> m ()
vest v = do
    logMsg $ pack $ "vesting in " ++ show v
    payToScript_
        defaultSlotRange
        (vestingAddress v)
        (toValue $ totalAmount v)
        dataValue
----

Function `withdraw` is the most interesting and complicated one:
Unless we want to withdraw all remaining funds,
we must construct a transaction with two outputs, one to our public key for the
withdrawn funds, and one to the script to lock the remaining amount.
The inputs of this transaction will be all the UTxOs that belong to the script
(although under normal circumstances, there will always be exactly one such
output).

As mentioned in the introduction to this chapter, we will use
`createTxAndSubmit` for this purpose.

[source,haskell]
----
withdraw :: MonadWallet m => Tranche -> Tranche -> Ada -> m ()
withdraw t1 t2 ada = do

    key <- ownPubKey
    let v = Vesting
            { tranche1 = t1
            , tranche2 = t2
            , owner    = key
            }
    logMsg $ pack $
        "withdrawing " ++ show ada ++
        " from " ++ show v

    utxos <- outputsAt $ vestingAddress v         -- <1>

    sl <- slot
    let range = intervalFrom sl

    let o = pubKeyTxOut (toValue ada) key

    let change = locked utxos - ada         -- <2>
        (outs, datas) = if change > 0
            then ([ o
                 , scriptTxOut
                    (toValue change)
                    (mkValidator v)
                    dataValue
                 ], [dataValue])
            else ([o], [])

    void $ createTxAndSubmit
        range
        (ins v utxos)
        outs
        datas

  where
    ins :: Vesting                                -- <3>
        -> Map TxOutRef TxOutTx
        -> Set TxIn
    ins v utxos = Set.fromList
        [ scriptTxIn
            r
            (mkValidator v)
            redeemerValue
            dataValue
        | r <- Map.keys utxos
        ]

    locked :: Map TxOutRef TxOutTx -> Ada           -- <4>
    locked utxo = fromValue $ foldMap (txOutValue . txOutTxOut) utxo

$(mkFunctions ['registerScheme, 'vest, 'withdraw])
----

<1> We use `outputsAt` to get all UTxOs locked by our script.

<2> How much change do we have to lock back into the script?

<3> The inputs for our transaction, one for each UTxO locked by our script.

<4> How much money is still locked in the script?
