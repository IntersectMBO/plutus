{-# LANGUAGE OverloadedStrings #-}
{-# LANGUAGE QuasiQuotes       #-}

module Main (main) where

import Control.Lens
import Data.Map qualified as Map
import Data.Text (Text)
import Data.Text qualified as Text
import Data.Text.IO qualified as Text
import PlutusTx.Options qualified as O
import Prettyprinter
import PyF (fmt)

main :: IO ()
main = Text.writeFile "doc/plutus/tutorials/plugin-options" optionsTable

optionsTable :: Text
optionsTable = [fmt|
..
   This file is generated by running plutus-tx-plugin:gen-plugin-opts-doc.
   Do not modify by hand.

.. list-table::
   :header-rows: 1
   :widths: 35 15 15 50

   * - Option
     - Value Type
     - Default
     - Description
{Text.unlines $ uncurry genRow <$> Map.toList O.pluginOptions}
|]

genRow :: O.OptionKey -> O.PluginOption -> Text
genRow k (O.PluginOption tr _ field desc) = [fmt|
   * - ``{k}``
     - {valueType}
     - {show (pretty defaultValue)}
     - {desc}
|]
  where
    valueType = show tr
    defaultValue = O.defaultPluginOptions ^. field
