[#plutus-tx]
= Plutus Tx Tutorial

This tutorial will walk you through the basics of using the Plutus Tx
compiler to create embedded programs that can be used when generating
transactions.

== What is Plutus Tx?

Plutus Tx is the name that we give to specially-delimited sections of a
Haskell program which will be compiled into Plutus Core (usually to go
in a transaction, hence the "Tx").

This means that Plutus Tx _is just Haskell_. Strictly, only a subset of
Haskell is supported, but most simple Haskell should work, and the
compiler will tell you if you use something that is unsupported.

The key technique that the Plutus Platform uses is called _staged
metaprogramming_. What that means is that the main Haskell program
_generates_ another program, in this case the Plutus Core program that
will run on the blockchain. Plutus Tx is the mechanism that we use to
write those programs. But the fact that it is just Haskell means that we
can use all the same techinques we use in the main program, and we can
share types and defintions between the two.

== Writing basic PlutusTx programs

[source,haskell]
----
-- Necessary language extensions for the Plutus Tx compiler to work.
{-# LANGUAGE TemplateHaskell     #-}
{-# LANGUAGE DataKinds           #-}
{-# LANGUAGE ScopedTypeVariables #-}
module Tutorial.PlutusTx where

import Language.PlutusTx -- <1>
import Language.PlutusTx.Lift -- <2>
import Language.PlutusTx.Builtins -- <3>

import Language.PlutusCore
import Language.PlutusCore.Pretty
import Language.PlutusCore.Quote
import Language.PlutusCore.Evaluation.CkMachine
import Data.Text.Prettyprint.Doc -- <4>
----
<1> Main Plutus Tx module.
<2> Additional support for lifting.
<3> Builtin functions.
<4> Used for examples.

Plutus Tx makes some use of Template Haskell. There are a few reasons
for this:

. Template Haskell allows us to do work at compile time, which
is when we do Plutus Tx compilation.
. It allows us to wire up the machinery that actually invokes the Plutus Tx compiler.

Consequently, we will see some use of Template Haskell quotes. A
Template Haskell quote is introduced with the special brackets `[||` and
`||]`, and will have type `Q (TExp a)`. This means it represents an
expression of type `a`, which lives in the `Q` type of quotes. You can
splice a definition with this type into your program using the `$$`
operator.

NOTE: There is also an abbreviation `TExpQ a` for `Q (TExp a)`, which avoids
some parentheses.

The key function we will use is the `compile` function. `compile` has
type `TExpQ a -> TExpQ (CompiledCode a)`. What does this mean? -
`TExpQ` we have already seen - `CompiledCode a` is a compiled
Plutus Core program corresponding to a Haskell program of type `a`

What this means is that `compile` lets you take a (quoted) Haskell
program and turn it into a (quoted) Plutus Core program, which you can
then splice into the main program. This happens when you _compile_ the
main Haskell program (since that’s when Template Haskell runs).

If you know about staged metaprogramming already you may be confused.
Shouldn’t we be generating the Plutus Core program at _runtime_, not at
compile time? That would be convenient, but we need the Haskell compiler
to help us compile arbitrary Haskell, so we have do this at compile
time. We will see later that we _can_ lift some values from Haskell to
Plutus Core at runtime, and this turns out to be enough to allow us to
write programs that depend on runtime values.

To reiterate: `compile` takes a Template Haskell quote, so what you
write inside the quote is just normal Haskell.

Here’s the most basic program we can write: one that just evaluates to
the integer `1`.

NOTE: The Plutus Core syntax will look unfamiliar. This is fine, since it is
the "assembly language" and you won’t need to inspect the output of
the compiler. However, for the purposes of this tutorial it’s
instructive to look at it to get a vague idea of what’s going on.

[source,haskell]
----
{- |
>>> pretty $ getPlc integerOne
(program 1.0.0
  (con 8 ! 1)
)
-}
integerOne :: CompiledCode Integer
integerOne = $$(compile -- <3> <4>
    [|| -- <2>
        (1 :: Integer) -- <1>
    ||])
----
<1> We always use unbounded integers in Plutus Core, so we have to pin
down this numeric literal to an `Integer` rather than an `Int`.
<2> The quote has type `TExpQ Integer`.
<3> `compile` turns the `TExpQ Integer` into a `TExpQ (CompiledCode Integer)`.
<4> The splice inserts the `TExpQ (CompiledCode Integer)` into the program.

We can see how the metaprogramming works here: the Haskell program `1`
was turned into a `CompiledCode Integer` at compile time, which we
spliced into our Haskell program, and which we can then inspect at
runtime to see the generated Plutus Core (or to put it on the
blockchain).

The most important thing to get comfortable with here is the pattern we
saw in the first example: a TH quote, wrapped in a call to `compile`,
wrapped in a `$$` splice. This is how we write all of our Plutus Tx
programs.

Here’s a slightly more complex program, namely the identity function on
integers.

[source,haskell]
----
{- |
>>> pretty $ getPlc integerIdentity
(program 1.0.0
  (lam ds [(con integer) (con 8)] ds)
)
-}
integerIdentity :: CompiledCode (Integer -> Integer)
integerIdentity = $$(compile [|| \(x:: Integer) -> x ||])
----

So far, so familiar: we compiled a lambda into a lambda (the "lam").

== Functions and datatypes

You can also use functions inside your expression. In practice, you
will usually want to define the entirety of your Plutus Tx program as a
definition outside the quote, and then simply call it inside the quote.

[source,haskell]
----
{-# INLINABLE plusOne #-} -- <2>
plusOne :: Integer -> Integer
plusOne x = x `addInteger` 1 -- <1>

{-# INLINABLE myProgram #-} -- <2>
myProgram :: Integer
myProgram =
    let
        plusOneLocal :: Integer -> Integer
        plusOneLocal x = x `addInteger` 1 -- <1>

        localPlus = plusOneLocal 1
        externalPlus = plusOne 1
    in localPlus `addInteger` externalPlus -- <1>

functions :: CompiledCode Integer
functions = $$(compile [|| myProgram ||])
----
<1> `addInteger` comes from `Language.PlutusTx.Builtins`, and is
which is mapped to the builtin integer addition in Plutus Core.
<2> Functions for reuse are marked with GHC’s `INLINABLE` pragma.
This is usually necessary for non-local functions to
be usable in Plutus Tx blocks, as it instructs GHC to keep the
information that the Plutus Tx compiler needs. While this is not always
necessary, it is a good idea to simply mark all such functions as
`INLINABLE`.

We can use normal Haskell datatypes and pattern matching freely:

[source,haskell]
----
matchMaybe :: CompiledCode (Maybe Integer -> Integer)
matchMaybe = $$(compile [|| \(x:: Maybe Integer) -> case x of
    Just n -> n
    Nothing -> 0
   ||])
----

Unlike functions, datatypes do not need any kind of special annotation to be
used inside the
expression, hence we can use types like `Maybe` from the `Prelude`.
This works for your own datatypes too!

Here’s a small example with a datatype of our own representing a
potentially open-ended end date.

[source,haskell]
----
-- | Either a specific end date, or "never".
data EndDate = Fixed Integer | Never

-- | Check whether a given time is past the end date.
pastEnd :: CompiledCode (EndDate -> Integer -> Bool)
pastEnd = $$(compile [|| \(end::EndDate) (current::Integer) -> case end of
    Fixed n -> n `lessThanEqInteger` current
    Never -> False
   ||])
----

== The Plutus Tx Prelude and Plutus Tx Builtins

The `Language.PlutusTx.Prelude` module is a drop-in replacement for the
normal Haskell Prelude, but with some functions redefined to be easier
for the Plutus Tx compiler to handle. You should use the Plutus Tx
Prelude whenever you are writing code that you expect to compile with
the Plutus Tx compiler.

To use the Plutus Tx Prelude, use the `NoImplicitPrelude` language
pragma, and import `Language.PlutusTx.Prelude`.

Plutus Tx has some builtin types and functions available for working
with primitive data (integers and bytestrings), as well as a few special
functions. These builtins are also exported from the Plutus Tx Prelude.

The `error` builtin deserves a special mention. `error` causes the
transaction to abort when it is evaluated, which is the way that
validation failure is signaled.

== Lifting values

So far we’ve seen how to define pieces of code _statically_ (when you
compile your main Haskell program), but you are likely to want to do so
_dynamically_ (when you run your main Haskell program). For example, you
might be writing the body of a transaction to initiate a crowdfunding
smart contract, which would need to be parameterized by user input
determining the size of the goal, the campaign start and end times, etc.

You can do this by writing the static code as a _function_, and then
passing an argument at runtime by _lifting_ it and then applying the two
programs together. As a very simple example, let’s write an add-one
function.

[source,haskell]
----
addOne :: CompiledCode (Integer -> Integer)
addOne = $$(compile [|| \(x:: Integer) -> x `addInteger` 1 ||])
----

Now, suppose we want to apply this to `4` at runtime, giving us a
program that computes to `5`. Well, we need to _lift_ the argument (`4`)
from Haskell to Plutus Core, and then we need to apply the function to
it.

[source,haskell]
----
{- |
>>> let program = addOneToN 4
>>> pretty program
(program 1.0.0
  [
    [
      (lam
        addInteger
        (fun [(con integer) (con 8)] (fun [(con integer) (con 8)] [(con integer) (con 8)]))
        (lam ds [(con integer) (con 8)] [ [ addInteger ds ] (con 8 ! 1) ])
      )
      { (builtin addInteger) (con 8) }
    ]
    (con 8 ! 4)
  ]
)
>>> pretty $ runCk program
(con 8 ! 5)
-}
addOneToN :: Integer -> CompiledCode Integer
addOneToN n =
    addOne
    `applyCode` -- <1>
    unsafeLiftCode n -- <2>
----
<1> `applyCode` applies one `CompiledCode` to another.
<2> `unsafeLiftCode` lifts the argument `n` into a `CompiledCode Integer`.

We lifted the argument using the `unsafeLiftCode` function. In order to use this, a type
must have an instance of the `Lift` class. In practice, you should
generate these with the `makeLift` TH function from
`Language.PlutusTx.Lift`. Lifting makes it easy to use the same types
both inside your Plutus Tx program and in the external code that uses
it.

NOTE: `unsafeLiftCode` is "unsafe" because it ignores any errors that might occur from
lifting something that isn't supported.

The combined program applies the original compiled lambda to the lifted
value (notice that the lambda is a bit complicated now since we have
compiled the addition into a builtin). We’ve then used the CK evaluator
for Plutus Core to evaluate the program and check that the result was
what we expected

Here’s an example with our custom datatype. The output is the encoded
version of `False`.

[source,haskell]
----
makeLift ''EndDate

{- |
>>> let program = pastEndAt Never 5
>>> pretty $ runCk program
(abs
  out_Bool (type) (lam case_True out_Bool (lam case_False out_Bool case_False))
)
-}
pastEndAt :: EndDate -> Integer -> CompiledCode Bool
pastEndAt end current =
    pastEnd
    `applyCode`
    unsafeLiftCode end
    `applyCode`
    unsafeLiftCode current
----
