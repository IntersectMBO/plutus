[#wallet-api]
= Smart Contracts
ifndef::imagesdir[:imagesdir: ./images]

This tutorial shows how to implement a crowdfunding campaign as a Plutus
contract, using the wallet API to submit it to the blockchain.

You can run this code in the
link:{playground}[Plutus Playground] - see <<03-testing-contract>>.

WARNING: The wallet API and by extension the wallet API tutorial is a
work in progress and may be changed without much warning.

The tutorial has three parts. In <<03-contract>> we write the contract, including
all the data types we need, validator scripts, and contract endpoints
that handle the interactions between wallet and blockchain. In <<03-testing-contract>> we
show how to test the contract. <<03-exercises>> contains a number of questions and
exercises related to this contract.

[#03-contract]
== Contract definition

We need the same language extensions and imports as
xref:02-validator-scripts#validator-scripts[before]:

[source,haskell]
----
{-# LANGUAGE DataKinds           #-}
{-# LANGUAGE TemplateHaskell     #-}
{-# LANGUAGE DeriveGeneric       #-}
{-# LANGUAGE DerivingStrategies  #-}
{-# LANGUAGE ScopedTypeVariables #-}
{-# LANGUAGE TypeApplications    #-}
{-# LANGUAGE OverloadedStrings   #-}
{-# LANGUAGE NamedFieldPuns      #-}
{-# LANGUAGE NoImplicitPrelude   #-}
module Tutorial.WalletAPI where

import           Language.PlutusTx.Prelude
import qualified Language.PlutusTx            as PlutusTx
import qualified Ledger.Interval              as I
import           Ledger                       (Address, DataValue(..), PubKey(..),
                                               RedeemerValue(..), Slot(..),
                                               TxId, Validator, mkValidatorScript)
import qualified Ledger                       as L
import qualified Ledger.Ada                   as Ada
import           Ledger.Ada                   (Ada)
import           Ledger.Typed.Scripts         (wrapValidator)
import           Ledger.Validation            (PendingTx, PendingTx'(..), PendingTxIn'(..), PendingTxOut)
import qualified Ledger.Validation            as V
import           Wallet                       (MonadWallet, EventHandler(..), EventTrigger)
import qualified Wallet                       as W
----

=== Datatypes

The crowdfunding campaign has the following parameters:

* Funding target
* End date
* Collection deadline
* Campaign owner

If the funding target is reached at the end date, then the campaign
owner may collect all the funds. If it isn’t reached, or if the owner
does not collect the funds before the collection deadline, then the
contributors are entitled to a refund.

In Haskell:

[source,haskell]
----
data Campaign = Campaign {
      fundingTarget      :: Ada, -- <1>
      endDate            :: Slot, -- <2>
      collectionDeadline :: Slot, -- <2>
      campaignOwner      :: PubKey -- <3>
 }

PlutusTx.makeLift ''Campaign -- <4>
----
<1> The type of Ada values is
link:{wallet-api-haddock}/Ledger-Ada.html#v:Ada[`Ada`].
<2> Dates are expressed in terms of slots, and their type is
link:{wallet-api-haddock}/Ledger-Slot.html#v:Slot[`Slot`].
<3> The campaign owner is identified by their public key.
<4> Just like we did in the xref:02-validator-scripts#validator-scripts[guessing game],
we need to call `makeLift` for data types that we want to lift to
Plutus Core.

Now we need to figure out what the campaign will look like on the
blockchain. Which transactions are involved, who submits them, and in
what order?

Each contributor pays their contribution to the address of the campaign
script. When the slot `endDate` is reached, the campaign owner submits a
single transaction, spending all inputs from the campaign address and
paying them to a pubkey address. If the funding target isn’t reached, or
the campaign owner fails to collect the funds, then each contributor can
claim a refund, in the form of a transaction that spends their own
contribution. This means that the validator script is going to be run
once per contribution, and we need to tell it which of the two
outcomes it should check for.

We can encode the two possible actions in a data type called `Action`.

[source,haskell]
----
data CampaignAction = Collect | Refund

PlutusTx.makeIsData ''CampaignAction
PlutusTx.makeLift ''CampaignAction
----

Now we need one final bit of information, namely the identity (public
key) of each contributor, so that we know the rightful recipient of a refund.
This data can’t be part of the redeemer script because a reclaim
could be made by anyone, not just the original contributor. Therefore
the public key is going to be stored in the data script of the
contribution.

[source,haskell]
----
newtype Contributor = Contributor PubKey
    deriving newtype PlutusTx.IsData
PlutusTx.makeLift ''Contributor
----

[NOTE]
.What is the role of the data script?
====
Pay-to-script outputs contain a (hash of a) validator script and a data script, but their
address is the hash of the validator script only, not of the data
script. The wallet uses the address to track the state of a contract, by
watching the outputs at that address. So the separate data script allows
us to have multiple outputs belonging to the same contract but with
different data scripts.

In the crowdfunding campaign the data script contains a `Contributor`
value, which is used to verify the "refund" transaction. If that data
was part of the validator script, then each contribution would go to a
unique address, and the campaign owner would have to be informed of all
the addresses through some other mechanism.
====

=== The validator script

The general form of a validator script is
`Data -> RedeemerValue -> PendingTx -> Bool`. The types of data
and redeemer scripts are `Contributor` and `CampaignAction`,
respectively, so the signature of the validator script is:

[source,haskell]
----
type CampaignValidator =
     Contributor
     -> CampaignAction
     -> PendingTx
     -> Bool
----

If we want to implement `CampaignValidator` we need to have access to
the parameters of the campaign, so that we can check if the selected
`CampaignAction` is allowed. In Haskell we can do this by writing a
function `validator {2c} Campaign -> CampaignValidator` that takes a
`Campaign` and produces a `CampaignValidator`.

We then need to compile this into on-chain code using `PlutusTx.compile`,
which we do in `mkValidator`.

[source,haskell]
----
mkValidator :: Campaign -> Validator
mkValidator campaign = mkValidatorScript $
  $$(PlutusTx.compile [|| v ||])
  `PlutusTx.applyCode` -- <1>
  PlutusTx.liftCode campaign -- <2>
  where v c = wrapValidator (validator c)

validator :: Campaign -> CampaignValidator
----
<1> `applyCode` applies one `CompiledCode` to another.
<2> `liftCode campaign` gives us the on-chain representation of `campaign`.

[NOTE]
.Parameterizing validators
====
You may wonder why we have to use `L.applyScript` to supply the `Campaign`
argument. Why can we not write `L.lifted campaign` inside the
validator script? The reason is that `campaign` is not known at the time
the validator script is compiled. The names of `lifted` and `compile`
indicate their chronological order: `validator` is compiled (via a
compiler plugin) to Plutus Core when GHC compiles the contract module,
and the `campaign` value is lifted to Plutus Core at runtime, when the
contract module is executed. But we know that `validator` is a
function, and that is why we can apply it to the campaign definition.
====

Before we check whether `act` is permitted, we define a number of
intermediate values that will make the checking code much more readable.
These definitions are placed inside a `let` block, which is closed by a
corresponding `in` below.

In the declaration of the function we pattern match on the arguments
to get the information we care about:

[source,haskell]
----
validator
    Campaign {fundingTarget, endDate, collectionDeadline, campaignOwner} -- <.>
    con
    act
    p@PendingTx{pendingTxInputs=ins, pendingTxOutputs=outs,pendingTxValidRange=txnValidRange} = -- <.> <.>
----
<.> This binds the parameters of the `Campaign`.
<.> This binds `ins` to the list of all inputs of the current transaction,
`outs` to the list of all its outputs, and `txnValidRange` to the
validity interval of the pending transaction.
<.> The underscores in the match stand for fields whose values are not
we are not interested int. The fields are
`pendingTxFee` (the fee of this transaction), `pendingTxForge` (how
much, if any, value was forged) and `PendingTxIn` (the current
link:{wallet-api-haddock}/Ledger-Validation.html#t:PendingTxIn[transaction
input]) respectively.

[NOTE]
.Validity ranges
====
In the extended UTXO model with scripts that underlies Plutus, each
transaction has a validity range, an interval of slots during which it
may be validated by core nodes.

The validity interval is passed to validator scripts via the `PendingTx` argument,
and it is the only information we have about the current time. For example, if
`txnValidRange` was the interval between slots 10 (inclusive) and 20 (exclusive), then we would
know that the current slot number is greater than or equal to 10, and
less than 20. In terms of clock
time we could say that the current time is between the beginning of slot
10 and the end of slot 19.
====

Then we compute the total value of all transaction inputs, using `foldr`
on the list of inputs `ins`.

NOTE: There is a limit on the number of
inputs a transaction may have, and thus on the number of contributions
in this crowdfunding campaign. In this tutorial we ignore that limit,
because it depends on the details of the implementation of Plutus on the
Cardano chain.

[source,haskell]
----
    let
        totalInputs :: Ada
        totalInputs =
            let addToTotal PendingTxIn{pendingTxInValue=vl} total = -- <.>
                  let adaVl = Ada.fromValue vl
                  in total + adaVl
            in foldr addToTotal zero ins -- <.>
----
<.> Defines a function that adds the Ada
value of a `PendingTxIn` to the total.
<.> Applies `addToTotal` to each transaction input,
summing up the results.

We now have all the information we need to check whether the action
`act` is allowed:

[source,haskell]
----
    in case act of
        Refund ->
            let
                Contributor pkCon = con
----

In the `Refund` branch we check that the outputs of this transaction all
go to the contributor identified by `pkCon`. To that end we define a
predicate

[source,haskell]
----
                contribTxOut :: PendingTxOut -> Bool
                contribTxOut o =
                  case V.pubKeyOutput o of
                    Nothing -> False
                    Just pk -> pk == pkCon
----

We check if `o` is a pay-to-pubkey output. If it isn’t, then the
predicate `contribTxOut` is false. If it is, then we check if the public
key matches the one we got from the data script.

The predicate `contribTxOut` is applied to all outputs of the current
transaction:

[source,haskell]
----
                contributorOnly = all contribTxOut outs
----

For the contribution to be refundable, three conditions must hold. The
collection deadline must have passed, all outputs of this transaction
must go to the contributor `con`, and the transaction was signed by the
contributor.

[source,haskell]
----
            in I.before collectionDeadline txnValidRange && -- <.>
               contributorOnly &&
               p `V.txSignedBy` pkCon
----
<.> To check whether the collection deadline has passed, we use
`before {2c} a -> Interval a -> Bool`.

The second branch is valid in a successful campaign.

[source,haskell]
----
        Collect ->
----

In the `Collect` case, the current slot must be between `deadline` and
`collectionDeadline`, the target must have been met, and and transaction
has to be signed by the campaign owner.

[source,haskell]
----
            I.contains (I.interval endDate collectionDeadline) txnValidRange && -- <.>
            totalInputs >= fundingTarget &&
            p `V.txSignedBy` campaignOwner
----
<.> We use
`interval {2c} Slot -> Slot -> SlotRange` and
`contains {2c} Interval a -> Interval a -> Bool`
to ensure that the transaction's validity range,
`txnValidRange`, is completely contained in the time between campaign
deadline and collection deadline.


=== Contract endpoints

Now that we have the validator script, we need to set up contract
endpoints for contributors and the campaign owner. The endpoints for the
crowdfunding campaign are more complex than the endpoints of the
guessing game because we need to do more than just create or spend a
single transaction output. As a contributor we need to watch the
campaign and claim a refund if it fails. As the campaign owner we need
to collect the funds, but only if the target has been reached before the
deadline has passed.

Both tasks can be implemented using _blockchain triggers_.

==== Blockchain triggers

The wallet API allows us to specify a pair of
link:{wallet-api-haddock}/Wallet-API.html#t:EventTrigger[`EventTrigger`]
and
link:{wallet-api-haddock}/Wallet-API.html#v:EventHandler[`EventHandler`]
to automatically run `collect`. An event trigger describes a condition
of the blockchain and can be true or false. There are four basic
triggers:
link:{wallet-api-haddock}/Wallet-API.html#v:slotRangeT[`slotRangeT`]
is true when the slot number is in a specific range,
link:{wallet-api-haddock}/Wallet-API.html#v:fundsAtAddressGeqT[`fundsAtAddressGeqT`]
is true when the total value of unspent outputs at an address is within
a range,
link:{wallet-api-haddock}/Wallet-API.html#v:alwaysT[`alwaysT`]
is always true and
link:{wallet-api-haddock}/Wallet-API.html#v:neverT[`neverT`]
is never true. We also have boolean connectives
link:{wallet-api-haddock}/Wallet-API.html#v:andT[`andT`],
link:{wallet-api-haddock}/Wallet-API.html#v:orT[`orT`]
and
link:{wallet-api-haddock}/Wallet-API.html#v:notT[`notT`]
to describe more complex conditions.

We will need to know the address of a campaign, which amounts to hashing
the output of `mkValidator`:

[source,haskell]
----
campaignAddress :: Campaign -> Address
campaignAddress cmp = L.scriptAddress (mkValidator cmp)
----

Contributors put their public key in a data script:

[source,haskell]
----
mkDataValue :: PubKey -> DataValue
mkDataValue pk = DataValue (PlutusTx.toData (Contributor pk))
----

When we want to spend the contributions we need to provide a
link:{wallet-api-haddock}/Ledger-Scripts.html#v:RedeemerValue[`RedeemerValue`]
value. In our case this is just the `CampaignAction`:

[source,haskell]
----
mkRedeemer :: CampaignAction -> RedeemerValue
mkRedeemer action = RedeemerValue (PlutusTx.toData action)
----

==== The `collect` endpoint

The `collect` endpoint does not require any user input, so it can be run
automatically as soon as the campaign is over, provided the campaign
target has been reached. The function `collectFundsTrigger` gives us the
`EventTrigger` that describes a successful campaign.

[source,haskell]
----
collectFundsTrigger :: Campaign -> EventTrigger
collectFundsTrigger c = W.andT
    (W.fundsAtAddressGeqT (campaignAddress c) (Ada.toValue (fundingTarget c))) -- <.>
    (W.slotRangeT (W.interval (endDate c) (collectionDeadline c))) -- <.>
----
<.> We use `W.intervalFrom` to create an open-ended interval that starts
at the funding target.
<.> With `W.interval` we create an interval from the campaign's end date
(inclusive) to the collection deadline (inclusive).

`fundsAtAddressGeqT` and `slotRangeT` take `Value` and `Interval Slot`
arguments respectively. The
link:{wallet-api-haddock}/Wallet-API.html#t:Interval[`Interval`]
type is part of the `wallet-api` package.

The campaign owner can collect contributions when two conditions hold:
The funds at the address must have reached the target, and the current
slot must be greater than the campaign deadline but smaller than the
collection deadline.

Now we can define an event handler that collects the contributions:

[source,haskell]
----
collectionHandler :: MonadWallet m => Campaign -> EventHandler m
collectionHandler cmp = EventHandler $ \_ -> do
----

`EventHandler` is a function of one argument, which we ignore in this
case (the argument tells us which of the conditions in the trigger are
true, which can be useful if we used
link:{wallet-api-haddock}/Wallet-API.html#v:orT[`orT`]
to build a complex condition). In our case we don’t need this
information because we know that both the
link:{wallet-api-haddock}/Wallet-API.html#v:fundsAtAddressGeqT[`fundsAtAddressGeqT`]
and the
link:{wallet-api-haddock}/Wallet-API.html#v:slotRangeT[`slotRangeT`]
conditions hold when the event handler is run, so we can call
link:{wallet-api-haddock}/Wallet-API.html#v:collectFromScript[`collectFromScript`]
immediately.


[source,haskell]
----
    W.logMsg "Collecting funds"
    let redeemerValue = mkRedeemer Collect
        range          = W.interval (endDate cmp) (collectionDeadline cmp)
    W.collectFromScript range (mkValidator cmp) redeemerValue -- <.>
----
<.> To collect the funds we use
link:{wallet-api-haddock}/Wallet-API.html#v:collectFromScript[`collectFromScript`],
which expects a validator script and a redeemer script.

NOTE: The trigger mechanism is a feature of the wallet, not of the
blockchain. That means that the wallet needs to be running when the
condition becomes true, so that it can react to it and submit
transactions. Anything that happens in an
link:{wallet-api-haddock}/Wallet-API.html#t:EventHandler[`EventHandler`]
is a normal interaction with the blockchain facilitated by the wallet.

With that, we can write the `scheduleCollection` endpoint to register a
`collectFundsTrigger` and collect the funds automatically if the
campaign is successful:

[source,haskell]
----
scheduleCollection :: MonadWallet m => Campaign -> m ()
scheduleCollection cmp = W.register (collectFundsTrigger cmp) (collectionHandler cmp)
----

Now the campaign owner only has to run `scheduleCollection` at the
beginning of the campaign and the wallet will collect the funds
automatically.

This takes care of the functionality needed by campaign owners. We need
another contract endpoint for making contributions and claiming a refund
in case the goal was not reached.

==== The `contribute` endpoint

After contributing to a campaign we do not need any user input to
determine whether we are eligible for a refund of our contribution.
Eligibility is defined entirely in terms of the blockchain state, and
therefore we can use the event mechanism to automatically process our
refund.

To contribute to a campaign we need to pay the desired amount to a
script address, and provide our own public key as the data script. In
the link:./02-validator-scripts#validator-scripts[guessing game] we used
link:{wallet-api-haddock}/Wallet-API.html#v:payToScript_[`payToScript_`],
which returns `()` instead of the transaction that was submitted. For
the crowdfunding contribution we need to hold on the transaction. Why?

Think back to the `guess` action of the game. We used
link:{wallet-api-haddock}/Wallet-API.html#v:collectFromScript[`collectFromScript`]
to collect _all_ outputs at the game address. This works only if all all
outputs are unlocked by the same redeemer (see also xref:02-exercises[exercise 3 of the
previous tutorial]).

In our crowdfunding campaign, the redeemer is a signed `Action`. In case
of a refund, we sign the `Refund` action with our public key, allowing
us to unlock our own contribution. But if we try to use the same
redeemer to unlock other contributions the script will fail,
invalidating the entire transaction. We therefore need a way to restrict
the outputs that
link:{wallet-api-haddock}/Wallet-API.html#v:collectFromScript[`collectFromScript`]
spends. To achieve this, the wallet API provides
link:{wallet-api-haddock}/Wallet-API.html#v:collectFromScriptTxn[`collectFromScriptTxn`],
which takes an additional `TxId` parameter and only collects outputs
produced by that transaction. To get the `TxId` parameter we need to
hold on to the transaction that commits our contribution, which we can
do with
link:{wallet-api-haddock}/Wallet-API.html#v:payToScript[`payToScript`].

[source,haskell]
----
refundHandler :: MonadWallet m => TxId -> Campaign -> EventHandler m
refundHandler txid cmp = EventHandler $ \_ -> do
    W.logMsg "Claiming refund"
    let redeemer  = mkRedeemer Refund
        range     = W.intervalFrom (collectionDeadline cmp)
    W.collectFromScriptTxn range (mkValidator cmp) redeemer txid
----

Now we can register the refund handler when we make the contribution.
The condition for being able to claim a refund is:

[source,haskell]
----
refundTrigger :: Campaign -> EventTrigger
refundTrigger c = W.andT
    (W.fundsAtAddressGtT (campaignAddress c) zero)
    (W.slotRangeT (W.intervalFrom (collectionDeadline c)))
----

The `contribute` action has two effects: It makes the contribution using
the wallet API’s `payToScript` function, and it registers a trigger to
automatically claim a refund if it is possible to do so.

[source,haskell]
----
contribute :: MonadWallet m => Campaign -> Ada -> m ()
contribute cmp adaAmount = do
    pk <- W.ownPubKey
    let dataValue = mkDataValue pk
        amount = Ada.toValue adaAmount

    tx <- W.payToScript W.defaultSlotRange (campaignAddress cmp) amount dataValue -- <.>
    W.logMsg "Submitted contribution"

    let tid = L.txId tx -- <.>

    W.register (refundTrigger cmp) (refundHandler tid cmp)
    W.logMsg "Registered refund trigger"
----
<.> `payToScript` returns the transaction that was submitted
(unlike `payToScript_` which returns unit).
<.> `L.txId` gives the `TxId` of a transaction.

[#03-testing-contract]
== Testing the contract

There are two ways to test a Plutus contract. We can run it
interactively in the
{playground}[Playground], or test it like
any other program by writing some unit and property tests. Both methods
give the same results because they do the same thing behind the scenes:
Generate some transactions and evaluate them on the mockchain. The
emulator performs the same validity checks (including running the
compiled scripts) as the slot leader would for the real blockchain, so
we can be confident that our contract works as expected when we deploy
it.

=== Playground

We need to tell the Playground what our contract endpoints are, so that
it can generate a UI for them. This is done by adding a call to
link:{haddock}/plutus-playground-lib-0.1.0.0/html/Playground-Contract.html#v:mkFunctions[`mkFunctions`]
for the endpoints to the end of the script:

....
$(mkFunctions ['scheduleCollection, 'contribute])
....

NOTE: We can’t use the usual Haskell syntax highlighting for this line
because the entire script is compiled and executed as part of the test
suite for the `wallet-api` project. The Playground-specific
link:{haddock}/plutus-playground-lib-0.1.0.0/html/Playground-Contract.html#v:mkFunctions[`mkFunctions`]
is defined in a different library (`plutus-playground-lib`) and it is
not available for this tutorial.

Alternatively, you can click the "Crowdfunding" button in the
Playground to load the sample contract including the `mkFunctions` line.
Note that the sample code differs slightly from what is written in this
tutorial, because it does not include some of the intermediate
definitions of contract endpoints such as `startCampaign` (which was
superseded by `scheduleCollection`) and `contribute` (superseded by
`contribute2`).

Either way, once the contract is defined we click "Compile" to get a
list of endpoints:

image:compile-contract.gif[Compiling a contract]

We can then simulate a campaign by adding actions for
`scheduleCollection` and `contribute`. Note that we also need to add a
number of empty blocks to make sure the time advances past the `endDate`
of the campaign.

image:actions.PNG[Contract actions]

A click on "Evaluate" runs the simulation and returns the result. We
can see in the logs that the campaign finished successfully:

image:logs.png[Logs]

=== Emulator

Testing contracts with unit and property tests requires more effort than
running them in the Playground, but it has several advantages. In a unit
test we have much more fine-grained control over the mockchain. For
example, we can simulate network outages that cause a wallet to fall
behind in its notifications, and we can deploy multiple contracts on the
same mockchain to see how they interact. And by writing smart contracts
the same way as all other software we can use the same tools
(versioning, continuous integration, release processes, etc.) without
having to set up additional infrastructure.

We plan to write a tutorial on this soon. Until then we would like to
refer you to the test suite in the `plutus-use-cases` project in the Plutus
repository.

You can run the test suite with
`nix build -f default.nix localPackages.plutus-use-cases` or
`cabal test plutus-use-cases`.

[#03-exercises]
== Exercises

[arabic]
. Run traces for successful and failed campaigns
. Change the validator script to produce more detailed log messages
using `traceH`
. Write a variation of the crowdfunding campaign that uses

....
data Campaign = Campaign {
      fundingTargets     :: [(Slot, Ada)],
      collectionDeadline :: Slot,
      campaignOwner      :: PubKey
 }
....

where `fundingTargets` is a list of slot numbers with associated Ada
amounts. The campaign is successful if the funding target for one of the
slots has been reached _before_ that slot begins. For example, campaign
with `Campaign [(Slot 20, Ada 100), (Slot 30, Ada 200)]` is successful
if the contributions amount to 100 Ada or more by slot 20, or 200 Ada or
more by slot 30.
