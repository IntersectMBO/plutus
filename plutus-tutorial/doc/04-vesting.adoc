[#multi-stage]
= Multi-stage contracts

In this part of the tutorial we will implement a simple vesting scheme,
where money is locked by a contract and may only be retrieved after some
time has passed.

This is our first example of a contract that covers multiple transactions,
with a contract state that changes over time.

== Contract definition

We need similar language extensions and imports to
xref:02-validator-scripts#validator-scripts[before]:

[source,haskell]
----
{-# LANGUAGE DataKinds           #-}
{-# LANGUAGE TemplateHaskell     #-}
{-# LANGUAGE ScopedTypeVariables #-}
{-# LANGUAGE DeriveGeneric       #-}
{-# LANGUAGE NoImplicitPrelude   #-}
module Tutorial.Vesting where

import           Language.PlutusTx.Prelude

import qualified Language.PlutusTx         as PlutusTx

import           Ledger                    (Address, DataScript(..), RedeemerScript(..), Slot, TxOutRef, TxIn, ValidatorScript(..))
import qualified Ledger                    as L
import           Ledger.Ada                (Ada)
import qualified Ledger.Ada                as Ada
import qualified Ledger.Ada                as ATH
import qualified Ledger.Interval           as Interval
import qualified Ledger.Slot               as Slot
import           Ledger.Validation         (PendingTx(..))
import qualified Ledger.Validation         as V
import           Ledger.Value              (Value)
import qualified Ledger.Value              as Value

import           Wallet                    (MonadWallet(..), PubKey)
import qualified Wallet                    as W
import qualified Wallet.API                as WAPI
import qualified Wallet.Emulator.Types     as EM

import qualified Data.Map                  as Map
import qualified Data.Set                  as Set -- <1>

import           GHC.Generics              (Generic)
----
<1> We need a few more standard Haskell data structures for this tutorial.

In our vesting scheme the money will be released in two _tranches_ (parts):
A smaller part will be available after an initial number of slots have
passed, and the entire amount will be released at the end. The owner of the
vesting scheme does not have to take out all the money at once: They can take out
any amount up to the total that has been released so far. The remaining
funds stay locked and can be retrieved later.

=== Datatypes

Let's start with the data types.

[source,haskell]
----
-- | Tranche of a vesting scheme.
data VestingTranche = VestingTranche {
    vestingTrancheDate   :: Slot,
    -- ^ When this tranche is released
    vestingTrancheAmount :: Value -- <1>
    -- ^ How much money is locked in this tranche
    } deriving (Generic)

PlutusTx.makeLift ''VestingTranche

-- | A vesting scheme consisting of two tranches. Each tranche defines a date
--   (slot) after which an additional amount of money can be spent.
data Vesting = Vesting {
    vestingTranche1 :: VestingTranche,
    -- ^ First tranche
    vestingTranche2 :: VestingTranche,
    -- ^ Second tranche
    vestingOwner    :: PubKey
    -- ^ The recipient of the scheme (who is authorised to take out money once
    --   it has been released)
    } deriving (Generic)

PlutusTx.makeLift ''Vesting

-- | The total amount vested
totalAmount :: Vesting -> Value
totalAmount (Vesting tranche1 tranche2 _) =
    vestingTrancheAmount tranche1 `Value.plus` vestingTrancheAmount tranche2

-- | The amount guaranteed to be available from a given tranche in a given slot range.
availableFrom :: VestingTranche -> Slot.SlotRange -> Value
availableFrom (VestingTranche d v) range =
    -- The valid range is an open-ended range starting from the tranche vesting date
    let validRange = Interval.from d
    -- If the valid range completely contains the argument range (meaning in particular
    -- that the start slot of the argument range is after the tranche vesting date), then
    -- the money in the tranche is available, otherwise nothing is available.
    in if validRange `Slot.contains` range then v else Value.zero
----
<1> `Value` is the general type of assets on chain, which includes tokens other than `Ada`.
Most of the functions work very similarly, so there is usually little reason not
to use `Value` instead of `Ada`.

=== The validator script

What should our data and redeemer scripts be? The vesting scheme only has a
single piece of information that we need to keep track of, namely how much
money is still locked in the contract. We can get this information from the
contract's transaction output, so we don't need to store it in the data
script. The type of our data script is therefore the unit type `()`.

The redeemer script usually carries the parameters of the action that is
performed on the contract. In this vesting scheme however, there is only
a single action (withdraw), and its only parameter is the amount withdrawn,
which we obtain by comparing the amounts locked in the scheme before and
after the transaction. Therefore the redeemer script is also of type `()`.

That gives our validator script the signature: `Vesting -> () -> () -> PendingTx -> Bool`

[source,haskell]
----
vestingValidatorScript :: Vesting -> ValidatorScript
vestingValidatorScript v = ValidatorScript $
    $$(L.compileScript [|| vestingValidator ||]) `L.applyScript` L.lifted v

vestingValidator :: Vesting -> () -> () -> PendingTx -> Bool
vestingValidator v@(Vesting vestingTranche1 vestingTranche2 owner) _ _ p@PendingTx{pendingTxValidRange = range} =
    let
        -- We need the hash of this validator script in order to ensure
        -- that the pending transaction locks the remaining amount of funds
        -- at the contract address.
        ownHash = V.ownHash p

        -- Value that has been released so far under the scheme.
        released = availableFrom vestingTranche1 range
            `Value.plus` availableFrom vestingTranche2 range

        -- Value that has not been released yet.
        unreleased :: Value
        unreleased = totalAmount v `Value.minus` released
----

To check whether the withdrawal is legitimate we need to:
. Ensure that the amount taken out does not exceed the current limit
. Check whether the transaction has been signed by the vesting owner

We will call these conditions `con1` and `con2`.

[source,haskell]
----
        -- 'con1' is true if the amount that remains locked in the contract
        -- is greater than or equal to 'unreleased'.
        con1 :: Bool
        con1 =
            let remaining = V.valueLockedBy p ownHash -- <1>
            in remaining `Value.geq` unreleased

        -- 'con2' is true if the pending transaction 'p' has  been signed
        -- by the owner of the vesting scheme
        con2 :: Bool
        con2 = V.txSignedBy p owner

    in con1 && con2
----
<1> We use the `valueLockedBy` function to get the amount of value paid by pending
transaction `p` to the script address `ownHash`.

=== Contract endpoints

We need three endpoints:

* `vestFunds` to lock the funds in a vesting scheme
* `registerVestingScheme`, used by the owner to start watching the scheme's address
* `withdraw`, used by the owner to take out some funds.

The first two are very similar to endpoints we defined for earlier
contracts.

[source,haskell]
----
contractAddress :: Vesting -> Address
contractAddress vst = L.scriptAddress (vestingValidatorScript vst)

vestFunds :: MonadWallet m => Vesting -> m ()
vestFunds vst = do
    let amt = totalAmount vst
        adr = contractAddress vst
        dataScript = DataScript (L.lifted ())
    W.payToScript_ W.defaultSlotRange adr amt dataScript

registerVestingScheme :: MonadWallet m =>  Vesting -> m ()
registerVestingScheme vst = WAPI.startWatching (contractAddress vst)
----

The last endpoint, `withdraw`, is different. We need to create a
transaction that spends the contract's current unspent transaction output
*and* puts the Ada that remains back at the script address.

We are going to use the wallet API to build the transaction "by hand",
that is without using `collectFromScript`.
The signature of `createTxAndSubmit` is
`WalletAPI m => SlotRange -> Set.Set TxIn -> [TxOut] -> m Tx`. So we need a slot range,
a set of inputs and a list of outputs.

[source,haskell]
----
withdraw :: (MonadWallet m) => Vesting -> Value -> m ()
withdraw vst vl = do

    let address = contractAddress vst
        validator = vestingValidatorScript vst

    -- The transaction's validity range should begin with the current slot and
    -- last indefinitely.
    range <- fmap WAPI.intervalFrom WAPI.slot

    -- The input should be the UTXO of the vesting scheme.
    utxos <- WAPI.outputsAt address -- <1>

    let
        -- the redeemer script containing the unit value ()
        redeemer  = RedeemerScript (L.lifted ())

        -- Turn the 'utxos' map into a set of 'TxIn' values
        mkIn :: TxOutRef -> TxIn
        mkIn r = L.scriptTxIn r validator redeemer

        ins = Set.map mkIn (Map.keysSet utxos)
----
<1> We can get the outputs at an address (as far as they are known by the wallet) with
`outputsAt`, which returns a map of `TxOutRef` to `TxOut`.

Our transaction has either one or two outputs.
If the scheme is finished (no money is left in it) then
there is only one output, a pay-to-pubkey output owned by
us.
If any money is left in the scheme then there will be an additional
pay-to-script output locked by the vesting scheme's validator script
that keeps the remaining value.

[source,haskell]
----
    ownOutput <- W.ownPubKeyTxOut vl -- <1>

    -- Now to compute the difference between 'vl' and what is currently in the
    -- scheme:
    let
        currentlyLocked = Map.foldr (\txo vl' -> vl' `Value.plus` L.txOutValue txo) Value.zero utxos
        remaining = currentlyLocked `Value.minus` vl

        otherOutputs = if Value.eq Value.zero remaining
                       then []
                       else [L.scriptTxOut remaining validator (DataScript (L.lifted ()))]

    -- Finally we have everything we need for `createTxAndSubmit`
    _ <- WAPI.createTxAndSubmit range ins (ownOutput:otherOutputs)

    pure ()
----
<1> We can create a public key output to our own key with `ownPubKeyTxOut`.


== Exercises

* Write an extended version of `registerVestingScheme` that also
registers a trigger to collect the remaining funds at the end of the
scheme.
