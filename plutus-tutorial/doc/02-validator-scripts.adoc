[#validator-scripts]
= Ledger and Wallet basics
ifndef::imagesdir[:imagesdir: ./images]

This tutorial explains the basics of writing a Plutus contract, using a
simple guessing game as an example.

You can run this code in the
link:{playground}[Plutus Playground] - see <<02-testing-contract>>.

WARNING: The wallet API and by extension the wallet API tutorial is a
work in progress and may be changed without notice.

This tutorial has three parts. In <<02-contract>> we write the contract,
including all the data types we need, validator scripts, and contract
endpoints that handle the interactions between wallet and blockchain. In
<<02-testing-contract>> we show how to test the contract. <<02-exercises>> contains a number of
questions and exercises.

[#02-contract]
== Contract definition

We need some language extensions and imports:

[source,haskell]
----
{-# LANGUAGE DataKinds           #-}
{-# LANGUAGE TemplateHaskell     #-}
{-# LANGUAGE FlexibleContexts    #-}
{-# LANGUAGE DerivingStrategies  #-}
{-# LANGUAGE TypeApplications    #-}
{-# LANGUAGE ScopedTypeVariables #-} -- <.>
{-# LANGUAGE DeriveGeneric       #-} -- <.>
{-# LANGUAGE OverloadedStrings   #-} -- <.>
{-# LANGUAGE NoImplicitPrelude   #-} -- <.>
module Tutorial.ValidatorScripts where

import           Language.PlutusTx.Prelude    hiding (Applicative (..)) -- <.>

import qualified Language.PlutusTx            as PlutusTx -- <.>

import           Ledger                       (Address, DataValue(..), RedeemerValue(..),
                                               Validator, mkValidatorScript)
import qualified Ledger                       as L -- <.>
import qualified Ledger.Ada                   as Ada
import           Ledger.Ada                   (Ada)
import           Ledger.Typed.Scripts         (wrapValidator)
import           Ledger.Validation            as V -- <.>

import           Wallet                       (MonadWallet)

import qualified Wallet                       as W -- <.>

import qualified Data.ByteString.Lazy.Char8   as C
----
<.> Needed by the Plutus Tx compiler plugin.
<.> Needed to allow contract endpoints to be automatically generated in the Plutus Playground.
<.> Allows us to use string literals for log messages without having to convert them to `Text` first.
<.> Allows us to use the Plutus Tx Prelude as a replacement for the Haskell Prelude.
<.> The Plutus Tx Prelude.
<.> `Language.PlutusTx` lets us translate code between
Haskell and Plutus Core (see the xref:01-plutus-tx#plutus-tx[PlutusTx tutorial]).
<.> link:{wallet-api-haddock}/Ledger.html[`Ledger`] has data types for the ledger model.
<.> link:{wallet-api-haddock}/Ledger-Validation.html[`Ledger.Validation`] contains types and
functions that can be used in on-chain code.
<.> link:{wallet-api-haddock}/Wallet.html[`Wallet`]
is the wallet API. It covers interactions with the wallet, for example
generating the transactions that actually get the crowdfunding contract
onto the blockchain.

=== Datatypes

The guessing game involves two moves: First, player A chooses a secret
word, and uses the game validator script to lock some Ada (the prize)
in a transaction output, providing the hash of the secret word as the data script. Second, player
B guesses the secret, by attempting to spend the locked transaction output
using the guess as a redeemer script.

[NOTE]
.Why does the data script need to be hashed?
====
We need to store enough information that we can validate the guess, but the data script will be
posted on the blockchain, so if we put the real answer there then someone could just look at it
to work out what to guess! Storing the hash means we can verify the guess without giving the
answer away.
====

Both the hashed secret and the cleartext guess are represented as
`ByteString` values in the on-chain code.

[source,haskell]
----
newtype HashedText = HashedText ByteString
    deriving newtype PlutusTx.IsData
newtype ClearText = ClearText ByteString -- <.>
    deriving newtype PlutusTx.IsData

PlutusTx.makeLift ''HashedText
PlutusTx.makeLift ''ClearText -- <.>

mkDataValue :: String -> DataValue
mkDataValue word =
    let hashedWord = sha2_256 (C.pack word)
    in  DataValue (PlutusTx.toData (HashedText hashedWord)) -- <.>

mkRedeemerValue :: String -> RedeemerValue
mkRedeemerValue word =
    let clearWord = C.pack word
    in RedeemerValue (PlutusTx.toData (ClearText clearWord)) -- <.>
----
<.> To avoid any confusion between cleartext and hash we wrap them in data
types called `HashedText` and `ClearText`, respectively.
<.> To enable values of our string types to be lifted to Plutus Core, we
need to call `makeLift`.
<.> `mkDataValue` creates a data script for the guessing game by hashing
the string and lifting the hash to its on-chain representation.
<.> `mkRedeemerValue` creates a redeemer script for the guessing game by
lifting the string to its on-chain representation.

=== The validator script

-- TODO: Update for Data

The general form of a validator script is
`Data -> Redeemer -> PendingTx -> Bool`. That is, the validator
script is a function of three arguments that produces a value of type
`Bool` indicating whether the validation was a success (or fails with an
error). As contract authors we can freely choose the types of
`Data`, and `Redeemer`. The third argument has to be of type
link:{wallet-api-haddock}/Ledger-Validation.html#t:PendingTx[`PendingTx`]
because that is the information about the current transaction, provided
by the validating node.

In our case, the data script is a `HashedText`, and the redeemer is a
`ClearText`. This gives us a script with the signature
`HashedText -> ClearText -> PendingTx -> Bool`.

[source,haskell]
----
-- | The validator script of the game.
validator
  :: HashedText
  -> ClearText
  -> PendingTx
  -> Bool
----

The actual game logic is very simple: we compare the hash of the
`guessed` argument with the `actual` secret hash, returning whether
or not they match.

[source,haskell]
----
validator (HashedText actual) (ClearText guessed) _ =
    if actual == (sha2_256 guessed) -- <.>
    then traceH "RIGHT!" True -- <.>
    else traceH "WRONG!" False
----
<.> We have an instance of `Eq` for `ByteString`, so we can just use `==` here to compare for equality.
<.> `traceH {2c} String -> a -> a` returns its second argument after adding
its first argument to the log output of this script. The log output is
only available in the emulator and on the playground, and will be
ignored when the code is run on the real blockchain.

Finally, we can compile this into on-chain code.

[source,haskell]
----
-- | The validator script of the game.
gameValidator :: Validator
gameValidator = mkValidatorScript $$(PlutusTx.compile [|| v ||]) -- <.>
    where v = wrapValidator validator
----
<.> The reference to the validator script that we defined
is wrapped in Template Haskell quotes, and then the
result of `PlutusTx.compile` is spliced in (see xref:01-plutus-tx#plutus-tx[PlutusTx tutorial] for further explanation of this pattern).

=== Contract endpoints

We can now use the wallet API to create a transaction that produces an
output locked by the game validator. This means that the _address_ of the
output is the hash of the validator script, and the output can only be
spent if the correct redeemer is provided so that the validator accepts
the spend.

To create the output we need to know the address, that is the hash of
the `gameValidator` script:

[source,haskell]
----
gameAddress :: Address
gameAddress = L.scriptAddress gameValidator
----

Contract endpoints are functions that use the wallet API to interact
with the blockchain. To contract users, endpoints are the visible
interface of the contract. A contract environment (such as the Playground)
may provide a UI for entering
the parameters of the actions provided by the endpoints.

When writing smart contracts we define their endpoints as functions that
return a value of type `MonadWallet m => m ()`. This type indicates that
the function uses the wallet API to produce and spend transaction
outputs on the blockchain.

The first endpoint we need for our game is the function `lock`. It pays
the specified amount of Ada to the script address.

[source,haskell]
----
-- | The "lock" contract endpoint.
lock :: (MonadWallet m, W.SigningProcessAPI m) => String -> Ada -> m ()
lock word adaValue = W.payToScript_ -- <.>
     W.defaultSlotRange -- <.>
     gameAddress
     (Ada.toValue adaValue) -- <.>
     (mkDataValue word)
----
<.> `payToScript_` is a function of the wallet API. It makes a payment to a script address
of the specified value.footnote:[
The underscore is a Haskell naming convention, indicating that
link:{wallet-api-haddock}/Wallet-API.html#v:payToScript_[`payToScript_`]
is a variant of
link:{wallet-api-haddock}/Wallet-API.html#v:payToScript[`payToScript`]
which ignores its return value and produces a `()` instead.]
<.> Transactions have a _validity range_ of slots that controls when they can be validated.
The default range is "always".
<.> "Value" on Cardano is more general than just `Ada`. We will see more about this later,
but for now `toValue` allows us to convert our `Ada` into a general `Value`.

The second endpoint, `guess`, creates a transaction that spends the game
output using the guessed word as a redeemer.

[source,haskell]
----
-- | The "guess" contract endpoint.
guess :: (W.SigningProcessAPI m, W.ChainIndexAPI m, MonadWallet m) => String -> m ()
guess word = W.collectFromScript -- <.>
      W.defaultSlotRange
      gameValidator
      (mkRedeemerValue word)
----
<.> `collectFromScript` is a function of the wallet API. It consumes the
unspent transaction outputs at a script address and pays them to a
public key address owned by this wallet. It takes the validator script
and the redeemer scripts as arguments.

If we run `guess` now, nothing will happen. Why? Because in order to
spend all outputs at the script address, the wallet needs to be aware of
this address _before_ the outputs are produced. That way, it can scan
incoming blocks from the blockchain for outputs at that address, and
doesn’t have to keep a record of all unspent outputs of the entire
blockchain. So before the game starts, players need to run the following
action:

[source,haskell]
----
-- | The "startGame" contract endpoint, telling the wallet to start watching
--   the address of the game script.
startGame :: W.ChainIndexAPI m => m ()
startGame = W.startWatching gameAddress -- <.>
----
<.> `startWatching` is a function of the chain index API. It instructs the wallet
to keep track of all outputs at the address.

[NOTE]
.What if I need to know about transactions that happened in the past?
====
At the moment, the wallet API assumes that you only care about transactions
that happen after the contract begins. This may well change in the future, however.
====

Player 2 needs to call `startGame` before Player 1 uses the `lock` endpoint,
to ensure that Player 2's wallet is watching of the game address.

Endpoints can have any number of parameters: `lock` has two parameters,
`guess` has one and `startGame` has none. For each endpoint we include a
call to `mkFunction` at the end of the contract definition, by writing
`$(mkFunction 'lock)`, `$(mkFunction 'guess)` and
`$(mkFunction 'startGame)` in three separate lines. This causes the
Haskell compiler to generate a schema for the endpoint. The Plutus
Playground then uses this schema to present an HTML form to the user
where the parameters can be entered.

[#02-testing-contract]
== Testing the contract

To test this contract, open the
link:{playground}[Plutus Playground] and click
the "Game" button above the editor field. Then click "Compile".

You can now create a _trace_ using the endpoints `lock`, `guess` and
`startGame`. A trace represents a series of events in the execution of
a conract, such as participants taking actions or time passing.

For a successful run of the game, click Wallet 1’s
`startGame` button, then Wallet 2’s `lock` button and finally Wallet 1’s
`guess` button. Three boxes appear in the "Actions" section, numbered
1 to 3. In the second box, type "plutus" in the first input and 8 in
the second input. In the third box type "plutus". The trace should
look like the screenshot below.

image:game-actions.PNG[A trace for the guessing game]

Now click "Evaluate". This button sends the contract code and the
trace to the server, and populates the "Transactions" section of the
UI with the results. The logs tell us that there were three successful
transactions. The first transaction is the initial transaction of the
blockchain. It distributes the initial funds to the participating
wallets. The second and third transactions are related to our game: One
transaction from the `lock` action (submitted by Wallet 2) and one
transaction from the `guess` action (submitted by Wallet 1).

image:game-results.PNG[Emulator log for a successful game]

If you change the word "plutus" in the third item of the trace to
"pluto" and click "Evaluate", the log shows that validation of the
`guess` transaction failed.

image:game-logs.PNG[Emulator log for a failed attempt]

[#02-exercises]
== Exercises

[arabic]
. Run traces for a successful game and a failed game in the Playground,
and examine the logs after each trace.
. Change the error case of the validator script to
`traceH "WRONG!" (error ())` and run the trace again with a wrong
guess. Note how this time the log does not include the error message.
. Look at the trace shown below. What will the logs say after running
"Evaluate"?

image:game-actions-2.PNG[A trace for the guessing game]
