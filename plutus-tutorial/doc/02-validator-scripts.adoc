[#validator-scripts]
= Wallet API I: Basics
ifndef::imagesdir[:imagesdir: ./images]

This tutorial explains how to get Plutus onto the blockchain, using a
simple guessing game as an example.

You can run this code in the
link:{playground}[Plutus Playground] - see <<testing-contract-02>>.

WARNING: The wallet API and by extension the wallet API tutorial is a
work in progress and may be changed without notice.

This tutorial has three parts. In part 1 we write the contract,
including all the data types we need, validator scripts, and contract
endpoints that handle the interactions between wallet and blockchain. In
part 2 we show how to test the contract. Part 3 contains a number of
questions and exercises.

== Contract definition

We need some language extensions and imports:

[source,haskell]
----
{-# LANGUAGE DataKinds           #-}
{-# LANGUAGE TemplateHaskell     #-}
{-# LANGUAGE ScopedTypeVariables #-} -- <1>

{-# LANGUAGE DeriveGeneric       #-} -- <2>

{-# LANGUAGE OverloadedStrings   #-} -- <3>

{-# LANGUAGE NoImplicitPrelude   #-} -- <4>
module Tutorial.ValidatorScripts where

import           Language.PlutusTx.Prelude -- <5>

import qualified Language.PlutusTx            as PlutusTx -- <6>

import qualified Ledger.Interval              as I
import           Ledger                       (Address, DataScript(..), PubKey(..), RedeemerScript(..),
                                               Signature(..), Slot(..), TxId, ValidatorScript(..))
import qualified Ledger                       as L -- <7>
import qualified Ledger.Ada                   as Ada
import           Ledger.Ada                   (Ada)
import           Ledger.Validation            (PendingTx(..), PendingTxIn(..), PendingTxOut)
import qualified Ledger.Validation            as V -- <8>

import           Wallet                       (WalletAPI(..), WalletDiagnostics(..), MonadWallet,
                                               EventHandler(..), EventTrigger)
import qualified Wallet                       as W -- <9>

import           GHC.Generics                 (Generic)

import qualified Data.ByteString.Lazy.Char8   as C
----
<1> Needed by the Plutus Tx compiler plugin.
<2> Needed to allow contract endpoints to be automatically generated in the Plutus Playground.
<3> Allows us to use string literals for log messages without having to convert them to `Text` first.
<4> Allows us to use the Plutus Tx Prelude as a replacement for the Haskell Prelude.
<5> The Plutus Tx Prelude.
<6> `Language.PlutusTx` lets us translate code between
Haskell and Plutus Core (see the xref:01-plutus-tx#plutus-tx[PlutusTx tutorial]).
<7> link:{wallet-api-haddock}/Ledger.html[`Ledger`] has data types for the ledger model.
<8> link:{wallet-api-haddock}/Ledger-Validation.html[`Ledger.Validation`] contains types and
functions that can be used in on-chain code.
<9> link:{wallet-api-haddock}/Wallet.html[`Wallet`]
is the wallet API. It covers interactions with the wallet, for example
generating the transactions that actually get the crowdfunding contract
onto the blockchain.

=== Datatypes

The guessing game involves two moves: First, player A chooses a secret
word, and uses the game validator script to lock some Ada (the prize),
providing the hash of the secret word as the data script. Second, player
B guesses the secret, by attempting to spend A’s transaction output
using the guess as a redeemer script.

Both the hashed secret and the cleartext guess are represented as
`ByteString` values in on-chain code. `ByteString` represents the usual
Haskell bytestrings in on-chain code.

[source,haskell]
----
data HashedText = HashedText ByteString
data ClearText = ClearText ByteString -- <1>

PlutusTx.makeLift ''HashedText
PlutusTx.makeLift ''ClearText -- <2>

mkDataScript :: String -> DataScript
mkDataScript word =
    let hashedWord = V.plcSHA2_256 (C.pack word)
    in  DataScript (L.lifted (HashedText hashedWord)) -- <3>

mkRedeemerScript :: String -> RedeemerScript
mkRedeemerScript word =
    let clearWord = C.pack word
    in RedeemerScript (L.lifted (ClearText clearWord)) -- <4>
----
<1> To avoid any confusion between cleartext and hash we wrap them in data
types called `HashedText` and `ClearText`, respectively.
<2> To enable values of our string types to be lifted to Plutus Core, we
need to call `makeLift`.
<3> `mkDataScript` creates a data script for the guessing game by hashing
the string and lifting the hash to its on-chain representation.
<4> `mkRedeemerScript` creates a redeemer script for the guessing game by
lifting the string to its on-chain representation

=== The validator script

The general form of a validator script is
`DataScript -> Redeemer -> PendingTx -> Bool`. That is, the validator
script is a function of three arguments that produces a value of type
`Bool` indicating whether the validation was a success (or fails with an
error). As contract authors we can freely choose the types of
`DataScript`, `Redeemer`. The third argument has to be of type
link:{wallet-api-haddock}/Ledger-Validation.html#t:PendingTx[`PendingTx`]
because that is the information about the current transaction, provided
by the slot leader.

In our case, the data script is a `HashedText`, and the redeemer is a
`ClearText`. This gives us a script with the signature
`HashedText -> ClearText -> PendingTx -> Bool`.

[source,haskell]
----
-- | The validator script of the game.
validator
  :: HashedText -- <1>
  -> ClearText -- <2>
  -> PendingTx
  -> Bool
----
<1> The type of the data script is `HashedText`.
<2> The type of the redeemer is `ClearText`.

The actual game logic is very simple: We compare the hash of the
`guessed` argument with the `actual` secret hash, and throw an error if
the two don’t match. We can use functions from the Plutus prelude, imported as `P`.

[source,haskell]
----
validator (HashedText actual) (ClearText guessed) _ =
    if equalsByteString actual (sha2_256 guessed) -- <1>
    then traceH "RIGHT!" True -- <2>
    else traceH "WRONG!" False
----
<1> `equalsByteString {2c} ByteString -> ByteString -> Bool`
compares two `ByteString` values for equality.
<2> `traceH {2c} String -> a -> a` returns its second argument after adding
its first argument to the log output of this script. The log output is
only available in the emulator and on the playground, and will be
ignored when the code is run on the real blockchain.

Finally, we can use `L.compileScript` to compile this into on-chain
code. The reference to the validator script that we defined needs to be
wrapped in Template Haskell _quotes_ (`[||` and `||]`), and then the
result of `L.compileScript` must be _spliced_ in with `$$`. However, the
form that we use here is the same every time, so you don’t need to
understand how Template Haskell works in detail.

[source,haskell]
----
-- | The validator script of the game.
gameValidator :: ValidatorScript
gameValidator = ValidatorScript $$(L.compileScript [|| validator ||])
----

=== Contract endpoints

We can now use the wallet API to create a transaction that produces an
output locked by the game validator. This means the address of the
output is the hash of the validator script, and the output can only be
spent if the correct redeemer is provided.

To create the output we need to know the address, that is the hash of
the `gameValidator` script:

[source,haskell]
----
gameAddress :: Address
gameAddress = L.scriptAddress gameValidator
----

Contract endpoints are functions that use the wallet API to interact
with the blockchain. To contract users, endpoints are the visible
interface of the contract. They provide a UI (HTML form) for entering
the parameters of the actions we may take as part of the contract.

When writing smart contracts we define their endpoints as functions that
return a value of type `MonadWallet m => m ()`. This type indicates that
the function uses the wallet API to produce and spend transaction
outputs on the blockchain.

Since `MonadWallet` is a sub-class of `Monad` we can use Haskell’s `do`
notation, allowing us to list our instructions to the wallet in a
sequence (see https://en.wikibooks.org/wiki/Haskell/do_notation[here]
for more information).

The first endpoint we need for our game is the function `lock`. It pays
the specified amount of Ada to the script address. Paying to a script
address is a common task at the beginning of a contract, and the wallet
API implements it in
link:{wallet-api-haddock}/Wallet-API.html#v:payToScript_[`payToScript_`].
The underscore is a Haskell naming convention, indicating that
link:{wallet-api-haddock}/Wallet-API.html#v:payToScript_[`payToScript_`]
is a variant of
link:{wallet-api-haddock}/Wallet-API.html#v:payToScript[`payToScript`]
which ignores its return value and produces a `()` instead.

[source,haskell]
----
-- | The "lock" contract endpoint.
lock :: MonadWallet m => String -> Ada -> m ()
lock word adaValue =
    W.payToScript_ W.defaultSlotRange gameAddress (Ada.toValue adaValue) (mkDataScript word)
----

The second endpoint, `guess`, creates a transaction that spends the game
output using the guessed word as a redeemer.

[source,haskell]
----
-- | The "guess" contract endpoint.
guess :: MonadWallet m => String -> m ()
guess word = W.collectFromScript W.defaultSlotRange gameValidator (mkRedeemerScript word) -- <1>
----
<1> `collectFromScript` is a function of the wallet API. It consumes the
unspent transaction outputs at a script address and pays them to a
public key address owned by this wallet. It takes the validator script
and the redeemer scripts as arguments.

If we run `guess` now, nothing will happen. Why? Because in order to
spend all outputs at the script address, the wallet needs to be aware of
this address before the outputs are produced. That way, it can scan
incoming blocks from the blockchain for outputs at that address, and
doesn’t have to keep a record of all unspent outputs of the entire
blockchain. So before the game starts, players need to run the following
action:

[source,haskell]
----
-- | The "startGame" contract endpoint, telling the wallet to start watching
--   the address of the game script.
startGame :: MonadWallet m => m ()
startGame = W.startWatching gameAddress -- <1>
----
<1> `startWatching` is a function of the wallet API. It instructs the wallet
to keep track of all outputs at the address.

Player 2 needs to call `startGame` before Player 1 uses the `lock` endpoint,
to ensure that Player 2's wallet is aware of the game address.

Endpoints can have any number of parameters: `lock` has two parameters,
`guess` has one and `startGame` has none. For each endpoint we include a
call to `mkFunction` at the end of the contract definition, by writing
`$(mkFunction 'lock)`, `$(mkFunction 'guess)` and
`$(mkFunction 'startGame)` in three separate lines. This causes the
Haskell compiler to generate a schema for the endpoint. The Plutus
Playground then uses this schema to present an HTML form to the user
where the parameters can be entered.

[#testing-contract-02]
== Testing the contract in the Playground

To test this contract, open the
link:{playground}[Plutus Playground] and click
the "Game" button above the editor field. Then click "Compile".

You can now create a trace using the endpoints `lock`, `guess` and
`startGame`. For a successful run of the game, click Wallet 1’s
`startGame` button, then Wallet 2’s `lock` button and finally Wallet 1’s
`guess` button. Three boxes appear in the "Actions" section, numbered
1 to 3. In the second box, type "plutus" in the first input and 8 in
the second input. In the third box type "plutus". The trace should
look like the screenshot below.

image:game-actions.PNG[A trace for the guessing game]

Now click "Evaluate". This button sends the contract code and the
trace to the server, and populates the "Transactions" section of the
UI with the results. The logs tell us that there were three successful
transactions. The first transaction is the initial transaction of the
blockchain. It distributes the initial funds to the participating
wallets. The second and third transactions are related to our game: One
transaction from the `lock` action (submitted by Wallet 2) and one
transaction from the `guess` action (submitted by Wallet 1).

image:game-results.PNG[Emulator log for a successful game]

If you change the word "plutus" in the third item of the trace to
"pluto" and click "Evaluate", the log shows that validation of the
`guess` transaction failed.

image:game-logs.PNG[Emulator log for a failed attempt]

== Exercises

[arabic]
. Run traces for a successful game and a failed game in the Playground,
and examine the logs after each trace.
. Change the error case of the validator script to
`traceH "WRONG!" (error ())` and run the trace again with a wrong
guess. Note how this time the log does not include the error message.
. Look at the trace shown below. What will the logs say after running
"Evaluate"?

image:game-actions-2.PNG[A trace for the guessing game]

== Next steps

The xref:03-wallet-api#wallet-api[next part] of the tutorial shows how to
implement a crowdfunding campaign. It covers blockchain triggers and the
validity range of transactions.
