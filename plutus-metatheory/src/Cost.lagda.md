# Costs


This module contains the basic definitions and structures for modelling costs.

```

module Cost where 

```

## Imports

```
open import Function using (_âˆ˜_)
open import Data.Bool using (if_then_else_)
open import Data.Fin using (Fin;zero;suc)
open import Data.Integer using (+_)
open import Data.Nat using (â„•;zero;suc;_+_;_*_;_âˆ¸_;_âŠ”_;_âŠ“_;_<áµ‡_;_â‰¡áµ‡_)
open import Data.Nat.DivMod using (_/_)
open import Data.Nat.Properties using (+-assoc;+-identityÊ³)
open import Data.Nat.Show using () renaming (show to showâ„•)
open import Data.Integer using (â„¤;âˆ£_âˆ£)
open import Data.Float using (Float;fromâ„•;_Ã·_;_â‰¤áµ‡_) renaming (show to showð”½;_*_ to _*f_)
import Data.List as L

open import Data.Maybe using (Maybe;just;nothing;maybeâ€²;fromMaybe) renaming (map to mapMaybe; _>>=_ to _>>=m_ )
open import Data.Product using () renaming (_,_ to _,,_)
open import Data.String using (String;_++_;padLeft;padRight;length)
open import Data.Vec using (Vec;replicate;[];_âˆ·_;sum;foldr) 
                     renaming (lookup to lookupVec)
open import Algebra using (IsMonoid)
open import Relation.Nullary using (yes;no)
open import Relation.Binary.PropositionalEquality using (_â‰¡_;refl;isEquivalence;congâ‚‚)
open import Text.Printf using (printf)

open import Utils using (_Ã—_;_,_;_âˆ·_;[];DATA;List;map;ByteString)
open DATA

open import Relation.Binary using (StrictTotalOrder)
open import Data.Char using (_â‰ˆáµ‡_)
open import Data.String.Properties using (<-strictTotalOrder-â‰ˆ)
open import Data.Tree.AVL.Map <-strictTotalOrder-â‰ˆ as AVL 
          using (Map;empty;unionWith;singleton) 
          renaming (lookup to lookupAVL)
open import Builtin using (Builtin;showBuiltin;builtinList;lengthBS;arity)
open Builtin.Builtin
open import Builtin.Signature using (_âŠ¢â™¯)
open _âŠ¢â™¯
open import RawU using (TmCon) 
open TmCon
open import Builtin.Constant.AtomicType using (AtomicTyCon)
open AtomicTyCon

open import Cost.Base 
open import Cost.Model
```

## Interface with Haskell Machine Parameters

```
{-# FOREIGN GHC import Data.SatInt (fromSatInt) #-}
{-# FOREIGN GHC import Data.Functor.Identity (Identity, runIdentity) #-}
{-# FOREIGN GHC import PlutusCore.Evaluation.Machine.ExBudget (ExBudget(..))  #-}
{-# FOREIGN GHC import PlutusCore.Evaluation.Machine.ExMemory (ExCPU(..), ExMemory(..)) #-}
{-# FOREIGN GHC import PlutusCore.Evaluation.Machine.ExBudgetingDefaults (defaultCekMachineCosts) #-}
{-# FOREIGN GHC import UntypedPlutusCore.Evaluation.Machine.Cek.CekMachineCosts (CekMachineCostsBase(..)) #-}

postulate HCekMachineCosts : Set 
postulate HExBudget : Set 

{-# COMPILE GHC HCekMachineCosts = type CekMachineCostsBase Identity #-}
{-# COMPILE GHC HExBudget = type ExBudget #-}

postulate getCekStartupCost : HCekMachineCosts â†’  HExBudget
postulate getCekVarCost     : HCekMachineCosts â†’  HExBudget
postulate getCekConstCost   : HCekMachineCosts â†’  HExBudget
postulate getCekLamCost     : HCekMachineCosts â†’  HExBudget
postulate getCekDelayCost   : HCekMachineCosts â†’  HExBudget
postulate getCekForceCost   : HCekMachineCosts â†’  HExBudget
postulate getCekApplyCost   : HCekMachineCosts â†’  HExBudget
postulate getCekBuiltinCost : HCekMachineCosts â†’  HExBudget
postulate getCekConstrCost  : HCekMachineCosts â†’  HExBudget
postulate getCekCaseCost    : HCekMachineCosts â†’  HExBudget

{-# COMPILE GHC getCekStartupCost = runIdentity . cekStartupCost  #-}
{-# COMPILE GHC getCekVarCost     = runIdentity . cekVarCost      #-}
{-# COMPILE GHC getCekConstCost   = runIdentity . cekConstCost    #-}
{-# COMPILE GHC getCekLamCost     = runIdentity . cekLamCost      #-}
{-# COMPILE GHC getCekDelayCost   = runIdentity . cekDelayCost    #-}
{-# COMPILE GHC getCekForceCost   = runIdentity . cekForceCost    #-}
{-# COMPILE GHC getCekApplyCost   = runIdentity . cekApplyCost    #-}
{-# COMPILE GHC getCekBuiltinCost = runIdentity . cekBuiltinCost  #-}
{-# COMPILE GHC getCekConstrCost  = runIdentity . cekConstrCost   #-}
{-# COMPILE GHC getCekCaseCost    = runIdentity . cekCaseCost     #-}

postulate getCPUCost : HExBudget â†’ CostingNat
postulate getMemoryCost : HExBudget â†’ CostingNat

{-# COMPILE GHC getCPUCost = fromSatInt . (\(ExCPU x) -> x) . exBudgetCPU  #-}
{-# COMPILE GHC getMemoryCost = fromSatInt . (\(ExMemory x) -> x) . exBudgetMemory  #-}

postulate defaultHCekMachineCosts : HCekMachineCosts

{-# COMPILE GHC defaultHCekMachineCosts = defaultCekMachineCosts #-}
``` 

## Execution Budget

An execution budget consist of two costs: CPU and memory costs.

```
record ExBudget : Set where
  constructor mkExBudget 
  field
    ExCPU : CostingNat -- CPU usage
    ExMem : CostingNat -- Memory usage

open ExBudget

fromHExBudget : HExBudget â†’ ExBudget 
fromHExBudget hb = mkExBudget (getCPUCost hb) (getMemoryCost hb)
```

The type for execution budget should be a Monoid.
We show that this is the case for `ExBudget`.

```
-- unit of the monoid
Îµâ‚¬ : ExBudget 
Îµâ‚¬ = mkExBudget 0 0 

-- binary operation
_âˆ™â‚¬_ : ExBudget â†’ ExBudget â†’ ExBudget 
(mkExBudget xCPU xMem) âˆ™â‚¬ (mkExBudget yCPU yMem) = mkExBudget (xCPU + yCPU) (xMem + yMem)

-- Note: working with `Monoid` instances would imply working in Setâ‚, so we avoid it
-- and just state that `ExBudget` is a Monoid

isMonoidExBudget : IsMonoid _â‰¡_ _âˆ™â‚¬_ Îµâ‚¬
isMonoidExBudget = record { 
       isSemigroup = record { 
           isMagma = record { isEquivalence = isEquivalence ; âˆ™-cong = Î» {refl refl â†’ refl }} 
           ; assoc = Î» x y z â†’ congâ‚‚ mkExBudget (+-assoc (ExCPU x) (ExCPU y) (ExCPU z)) 
                                                (+-assoc (ExMem x) (ExMem y) (ExMem z)) } 
     ; identity = (Î» x â†’ refl) ,, Î» x â†’ congâ‚‚ mkExBudget (+-identityÊ³ (ExCPU x)) (+-identityÊ³ (ExMem x)) }
``` 

## Memory usage of type constants

For each type constant we calculate its size, as a measure of memory usage.

First we bring some functions from Haskell world.

```
postulate â„•toWords : â„¤ â†’ CostingNat 
postulate g1ElementCost : CostingNat
postulate g2ElementCost : CostingNat
postulate mlResultElementCost : CostingNat 

{-# FOREIGN GHC {-# LANGUAGE MagicHash #-} #-}
{-# FOREIGN GHC import GHC.Exts (Int (I#)) #-}
{-# FOREIGN GHC import GHC.Integer  #-}
{-# FOREIGN GHC import GHC.Integer.Logarithms  #-}
{-# FOREIGN GHC import GHC.Prim #-}
{-# FOREIGN GHC import PlutusCore.Crypto.BLS12_381.G1 as BLS12_381.G1 #-}
{-# FOREIGN GHC import PlutusCore.Crypto.BLS12_381.G2 as BLS12_381.G2 #-}
{-# FOREIGN GHC import PlutusCore.Crypto.BLS12_381.Pairing as BLS12_381.Pairing #-}

{-# COMPILE GHC â„•toWords = \i -> fromIntegral $ I# (integerLog2# (abs i) `quotInt#` integerToInt 64) + 1 #-}
{-# COMPILE GHC g1ElementCost = toInteger (BLS12_381.G1.memSizeBytes `div` 8) #-}
{-# COMPILE GHC g2ElementCost = toInteger (BLS12_381.G2.memSizeBytes `div` 8) #-}
{-# COMPILE GHC mlResultElementCost = toInteger (BLS12_381.Pairing.mlResultMemSizeBytes `div` 8) #-}
```

For each constant we return the corresponding size.

```
byteStringSize : ByteString â†’ CostingNat 
byteStringSize x = let n = âˆ£ lengthBS x âˆ£ in ((n âˆ¸ 1) / 8) + 1

-- cost of a Data node
dataNodeMem : CostingNat 
dataNodeMem = 4

sizeData : DATA â†’ CostingNat 
sizeDataList : List DATA â†’ CostingNat 
sizeDataDataList : List (DATA Ã— DATA) â†’ CostingNat

sizeData (ConstrDATA _ xs) = dataNodeMem + sizeDataList xs
sizeData (MapDATA []) = dataNodeMem
sizeData (MapDATA xs) = dataNodeMem + sizeDataDataList xs
sizeData (ListDATA xs) = dataNodeMem + sizeDataList xs
sizeData (iDATA x) = dataNodeMem +  â„•toWords x
sizeData (bDATA x) = dataNodeMem + byteStringSize x 

-- The following tow functions are stupid but
-- they make the termination checker happy
sizeDataDataList [] = 0
sizeDataDataList ((x , y) âˆ· xs) = sizeData x + sizeData y + sizeDataDataList xs

sizeDataList [] = 0
sizeDataList (x âˆ· xs) = sizeData x + sizeDataList xs

-- This the main sizing function for constants
defaultConstantMeasure : TmCon â†’ CostingNat
defaultConstantMeasure (tmCon (atomic aInteger) x) = â„•toWords x
defaultConstantMeasure (tmCon (atomic aBytestring) x) = byteStringSize x
defaultConstantMeasure (tmCon (atomic aString) x) = length x -- each Char costs 1
defaultConstantMeasure (tmCon (atomic aUnit) x) = 1
defaultConstantMeasure (tmCon (atomic aBool) x) = 1
defaultConstantMeasure (tmCon (atomic aData) d) = sizeData d
defaultConstantMeasure (tmCon (atomic aBls12-381-g1-element) x) = g1ElementCost
defaultConstantMeasure (tmCon (atomic aBls12-381-g2-element) x) = g2ElementCost
defaultConstantMeasure (tmCon (atomic aBls12-381-mlresult) x) = mlResultElementCost
defaultConstantMeasure (tmCon (list t) []) = 0
defaultConstantMeasure (tmCon (list t) (x âˆ· xs)) = 
       defaultConstantMeasure (tmCon t x) 
     + defaultConstantMeasure (tmCon (list t) xs)
defaultConstantMeasure (tmCon (pair t u) (x , y)) = 
      1 + defaultConstantMeasure (tmCon t x) 
        + defaultConstantMeasure (tmCon u y)
```

## Cost of executing a builtin

To calculate the cost of a builtin we obtain the corresponding builtin model, 
and run the cpu and memory model using the vector of argument sizes.

```
builtinCost : (b : Builtin) â†’ BuiltinModel (arity b) â†’ Vec CostingNat (arity b) â†’ ExBudget
builtinCost b bc cs = mkExBudget (runModel (costingCPU bc) cs) (runModel (costingMem bc) cs)
``` 


## Default Machine Parameters

The machine parameters for `ExBudget` for a given Cost Model

```
CostModel = HCekMachineCosts Ã— ModelAssignment

cekMachineCostFunction : HCekMachineCosts â†’ StepKind â†’ ExBudget
cekMachineCostFunction mc BConst = fromHExBudget (getCekConstCost mc)
cekMachineCostFunction mc BVar = fromHExBudget (getCekVarCost mc)
cekMachineCostFunction mc BLamAbs = fromHExBudget (getCekLamCost mc)
cekMachineCostFunction mc BApply = fromHExBudget (getCekApplyCost mc)
cekMachineCostFunction mc BDelay = fromHExBudget (getCekDelayCost mc)
cekMachineCostFunction mc BForce = fromHExBudget (getCekForceCost mc)
cekMachineCostFunction mc BBuiltin = fromHExBudget (getCekBuiltinCost mc)
cekMachineCostFunction mc BConstr = fromHExBudget (getCekConstCost mc)
cekMachineCostFunction mc BCase = fromHExBudget (getCekCaseCost mc)

exBudgetCategoryCost' : CostModel â†’ ExBudgetCategory â†’ ExBudget 
exBudgetCategoryCost' (cekMc , _) (BStep x) = cekMachineCostFunction cekMc x
exBudgetCategoryCost' (_ , ma) (BBuiltinApp b cs) = builtinCost b (ma b) cs
exBudgetCategoryCost' (cekMc , _) BStartup = fromHExBudget (getCekStartupCost cekMc)

machineParameters : CostModel â†’ MachineParameters ExBudget
machineParameters costmodel = record {
    cekMachineCost = exBudgetCategoryCost' costmodel
  ; Îµ = Îµâ‚¬
  ; _âˆ™_ = _âˆ™â‚¬_
  ; costMonoid = isMonoidExBudget
  ; constantMeasure = defaultConstantMeasure 
 } 

exBudgetCategoryCost : ModelAssignment â†’ ExBudgetCategory â†’ ExBudget 
exBudgetCategoryCost ma = exBudgetCategoryCost' (defaultHCekMachineCosts , ma)

defaultMachineParameters : ModelAssignment â†’ MachineParameters ExBudget
defaultMachineParameters assignModel = record {
    cekMachineCost = exBudgetCategoryCost assignModel
  ; Îµ = Îµâ‚¬
  ; _âˆ™_ = _âˆ™â‚¬_
  ; costMonoid = isMonoidExBudget
  ; constantMeasure = defaultConstantMeasure 
 } 

countingReport : ExBudget â†’ String 
countingReport (mkExBudget cpu mem) = 
      "\nCPU budget:    " ++ showâ„• cpu 
   ++ "\nMemory budget: " ++ showâ„• mem
```
 ## Tallying budget 

These functions define a type for Budgets that can record detailed cost information
about nodes and builtins.

We need a map from `ExBudgetCategory` into `ExBudget`. 
It's not the most efficient, but the simplest thing to do is to 
transform `ExBudgetCategory` into a string, and use that as keys.

```
mkKeyFromExBudgetCategory : ExBudgetCategory â†’ String 
mkKeyFromExBudgetCategory (BStep x) = "0" ++ showStepKind x
mkKeyFromExBudgetCategory (BBuiltinApp x _) = "1"++ showBuiltin x
mkKeyFromExBudgetCategory BStartup = "2"

TallyingBudget : Set 
TallyingBudget = Map ExBudget Ã— ExBudget

lookup : Map ExBudget â†’ ExBudgetCategory â†’ ExBudget
lookup m k with lookupAVL (mkKeyFromExBudgetCategory k) m 
... | just x = x
... | nothing = Îµâ‚¬
```

As required, `TallyingBudget` is a monoid. 

```
--unit of TallyingBudget 
ÎµT : TallyingBudget
ÎµT = empty , Îµâ‚¬

-- adding TallyingBudgets
_âˆ™T_ : TallyingBudget â†’ TallyingBudget â†’ TallyingBudget
(m , x) âˆ™T (n , y) = unionWith u m n , (x âˆ™â‚¬ y)
   where u : ExBudget â†’ Maybe (ExBudget) â†’ ExBudget
         u x (just y) = x âˆ™â‚¬ y
         u x nothing = x

-- TODO : Prove these postulates.
postulate TallyingBudget-assoc : Algebra.Associative _â‰¡_ _âˆ™T_
postulate Tallying-budget-identityÊ³ : Algebra.RightIdentity _â‰¡_ ÎµT _âˆ™T_

isMonoidTallyingBudget : IsMonoid _â‰¡_ _âˆ™T_ ÎµT
isMonoidTallyingBudget = record { 
       isSemigroup = record { 
           isMagma = record { isEquivalence = isEquivalence 
                            ; âˆ™-cong = Î» {refl refl â†’ refl }} 
           ; assoc = TallyingBudget-assoc } 
     ; identity = (Î» x â†’ refl) ,, Tallying-budget-identityÊ³ }


tallyingCekMachineCost' : CostModel â†’ ExBudgetCategory â†’ TallyingBudget
tallyingCekMachineCost' cm k = 
      let spent = exBudgetCategoryCost' cm k 
      in singleton (mkKeyFromExBudgetCategory k) spent , spent

tallyingMachineParameters' : CostModel â†’ MachineParameters TallyingBudget
tallyingMachineParameters' cm = record { 
        cekMachineCost = tallyingCekMachineCost' cm
      ; Îµ = ÎµT
      ; _âˆ™_ = _âˆ™T_
      ; costMonoid = isMonoidTallyingBudget
      ; constantMeasure = defaultConstantMeasure
      } 

tallyingCekMachineCost : ModelAssignment â†’ ExBudgetCategory â†’ TallyingBudget
tallyingCekMachineCost am k = 
      let spent = exBudgetCategoryCost am k 
      in singleton (mkKeyFromExBudgetCategory k) spent , spent

tallyingMachineParameters : ModelAssignment â†’ MachineParameters TallyingBudget
tallyingMachineParameters am = record { 
        cekMachineCost = tallyingCekMachineCost am
      ; Îµ = ÎµT
      ; _âˆ™_ = _âˆ™T_
      ; costMonoid = isMonoidTallyingBudget
      ; constantMeasure = defaultConstantMeasure
      } 

tallyingReport : TallyingBudget â†’ String
tallyingReport (mp , budget) =  
       countingReport budget
    ++ "\n"
    ++ "\n"
    ++ printStepReport mp
    ++ "\n"
    ++ "startup    " ++ budgetToString (lookup mp BStartup) ++ "\n"
    ++ "compute    " ++ budgetToString totalComputeCost ++ "\n"
    -- ++ "AST nodes  " ++ ++ "\n"
    ++ "\n\n"
    ++ printBuiltinReport mp 
    ++ "\n" 
    ++ "Total builtin costs:   " ++ budgetToString totalBuiltinCosts ++ "\n"
     -- We would like to be able to print the following  number as "%4.2f" 
     -- but Agda's printf currently doesn't support it.
    ++ printf "Time spent executing builtins:  %f%%\n" (fromâ„• 100 *f (getCPU totalBuiltinCosts) Ã· (getCPU budget)) ++ "\n"
    ++ "\n"
    ++ "Total budget spent:    " ++ budgetToString budget ++ "\n"
    ++  "Predicted execution time: " ++ formatTimePicoseconds (getCPU budget)
  where 
    totalComputeCost totalBuiltinCosts : ExBudget 
    totalComputeCost = L.foldr (Î» x acc â†’ (lookup mp (BStep x)) âˆ™â‚¬ acc) Îµâ‚¬ stepKindList
    totalBuiltinCosts = L.foldr _âˆ™â‚¬_ Îµâ‚¬ (L.map (lookup mp âˆ˜ (Î» b â†’ BBuiltinApp b (replicate 0))) builtinList)

    getCPU : ExBudget â†’ Float
    getCPU n = fromâ„• (ExCPU n)   

    budgetToString : ExBudget â†’ String 
    budgetToString (mkExBudget cpu mem) = padLeft ' ' 15 (showâ„• cpu) ++ "  " 
                                       ++ padLeft ' ' 15 (showâ„• mem)

    printStepCost : StepKind â†’ ExBudget â†’ String
    printStepCost sk budget = padRight ' ' 10 (showStepKind sk) ++ " " 
                           ++ padLeft ' ' 20 (budgetToString budget) ++ "\n"

    printStepReport : Map ExBudget â†’ String 
    printStepReport mp = L.foldr (Î» s xs â†’ printStepCost s (lookup mp (BStep s)) ++ xs)
                               "" 
                               stepKindList

    printBuiltinCost : Builtin â†’ ExBudget â†’ String 
    printBuiltinCost b (mkExBudget 0 0) = "" 
    printBuiltinCost b budget = padRight ' ' 22 (showBuiltin b) ++ " " 
                             ++ budgetToString budget ++ "\n"

    printBuiltinReport : Map ExBudget â†’ String 
    printBuiltinReport mp = 
        L.foldr (Î» b xs â†’ printBuiltinCost b (lookup mp (BBuiltinApp b (replicate 0))) ++ xs) 
              "" 
              builtinList     
    
    formatTimePicoseconds : Float â†’ String
    formatTimePicoseconds t = if 1e12 â‰¤áµ‡ t then (printf "%f s" (t Ã· 1e12)) else
                              if 1e9 â‰¤áµ‡ t then  (printf "%f ms" (t Ã· 1e9)) else
                              if 1e6 â‰¤áµ‡ t then  (printf "%f Î¼s" (t Ã· 1e6)) else
                              if 1e3 â‰¤áµ‡ t then  (printf "%f ns" (t Ã· 1e3)) else
                              printf "%f ps" t
 ```
