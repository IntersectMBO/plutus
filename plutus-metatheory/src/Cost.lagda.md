
# Costs


This module contains the basic definitions and structures for modelling costs.

```

module Cost where 

```

## Imports

```
open import Function using (_âˆ˜_)
open import Data.Bool using (if_then_else_)
open import Data.Fin using (Fin;zero;suc)
open import Data.Integer using (+_)
open import Data.Nat using (â„•;zero;suc;_+_;_*_;_âˆ¸_;_âŠ”_;_âŠ“_;_<?_)
open import Data.Nat.DivMod using (_/_)
open import Data.Nat.Properties using (+-assoc;+-identityÊ³)
open import Data.Nat.Show using () renaming (show to showâ„•)
open import Data.Integer using (â„¤;âˆ£_âˆ£)
open import Data.Float using (Float;fromâ„•;_Ã·_;_â‰¤áµ‡_) renaming (show to showð”½;_*_ to _*f_)
import Data.List as L

open import Data.Maybe using (Maybe;just;nothing;maybe)
open import Data.Product using (_Ã—_;_,_)
open import Data.String using (String;_++_;padLeft;padRight;length)
open import Data.Vec using (Vec;replicate;[];_âˆ·_;sum;foldr)
open import Algebra using (IsMonoid)
open import Relation.Nullary using (yes;no)
open import Relation.Binary.PropositionalEquality using (_â‰¡_;refl;isEquivalence;congâ‚‚)
open import Text.Printf using (printf)

open import Utils using (_,_;_âˆ·_;[];DATA)
open DATA

open import Relation.Binary using (StrictTotalOrder)
open import Data.Char using (_â‰ˆáµ‡_)
open import Data.String.Properties using (<-strictTotalOrder-â‰ˆ)
open import Data.Tree.AVL.Map <-strictTotalOrder-â‰ˆ as AVL 
          using (Map;empty;unionWith;singleton) 
          renaming (lookup to lookupAVL)
open import Builtin using (Builtin;showBuiltin;builtinList;lengthBS;arity)
open Builtin.Builtin
open import Builtin.Signature using (_âŠ¢â™¯)
open _âŠ¢â™¯
open import RawU using (TmCon) 
open TmCon
open import Builtin.Constant.AtomicType using (AtomicTyCon)
open AtomicTyCon
open import Cost.Base 
``` 

## Execution Budget

An execution budget consist of two costs: CPU and memory costs.

```
record ExBudget : Set where
  constructor mkExBudget 
  field
    ExCPU : CostingNat -- CPU usage
    ExMem : CostingNat -- Memory usage

open ExBudget
```

The type for execution budget should be a Monoid.
We show that this is the case for `ExBudget`.

```
-- unit of the monoid
Îµâ‚¬ : ExBudget 
Îµâ‚¬ = mkExBudget 0 0 

-- binary operation
_âˆ™â‚¬_ : ExBudget â†’ ExBudget â†’ ExBudget 
(mkExBudget xCPU xMem) âˆ™â‚¬ (mkExBudget yCPU yMem) = mkExBudget (xCPU + yCPU) (xMem + yMem)

-- Note: working with `Monoid` instances would imply working in Setâ‚, so we avoid it
-- and just state that `ExBudget` is a Monoid

isMonoidExBudget : IsMonoid _â‰¡_ _âˆ™â‚¬_ Îµâ‚¬
isMonoidExBudget = record { 
       isSemigroup = record { 
           isMagma = record { isEquivalence = isEquivalence ; âˆ™-cong = Î» {refl refl â†’ refl }} 
           ; assoc = Î» x y z â†’ congâ‚‚ mkExBudget (+-assoc (ExCPU x) (ExCPU y) (ExCPU z)) 
                                                (+-assoc (ExMem x) (ExMem y) (ExMem z)) } 
     ; identity = (Î» x â†’ refl) , Î» x â†’ congâ‚‚ mkExBudget (+-identityÊ³ (ExCPU x)) (+-identityÊ³ (ExMem x)) }
``` 

## Memory usage of type constants

For each type constant we calculate its size, as a measure of memory usage.

First we bring some functions from Haskell world.

```
postulate â„•toWords : â„¤ â†’ CostingNat 
postulate g1ElementCost : CostingNat
postulate g2ElementCost : CostingNat
postulate mlResultElementCost : CostingNat 

{-# FOREIGN GHC {-# LANGUAGE MagicHash #-} #-}
{-# FOREIGN GHC import GHC.Exts (Int (I#)) #-}
{-# FOREIGN GHC import GHC.Integer  #-}
{-# FOREIGN GHC import GHC.Integer.Logarithms  #-}
{-# FOREIGN GHC import GHC.Prim #-}
{-# FOREIGN GHC import PlutusCore.Crypto.BLS12_381.G1 as BLS12_381.G1 #-}
{-# FOREIGN GHC import PlutusCore.Crypto.BLS12_381.G2 as BLS12_381.G2 #-}
{-# FOREIGN GHC import PlutusCore.Crypto.BLS12_381.Pairing as BLS12_381.Pairing #-}

{-# COMPILE GHC  â„•toWords = \i -> fromIntegral $ I# (integerLog2# (abs i) `quotInt#` integerToInt 64) + 1 #-}
{-# COMPILE GHC g1ElementCost = toInteger (BLS12_381.G1.memSizeBytes `div` 8) #-}
{-# COMPILE GHC g2ElementCost = toInteger (BLS12_381.G2.memSizeBytes `div` 8) #-}
{-# COMPILE GHC mlResultElementCost = toInteger (BLS12_381.Pairing.mlResultMemSizeBytes `div` 8) #-}
```

For each constant we return the corresponding size.

```
byteStringSize : Utils.ByteString â†’ CostingNat 
byteStringSize x = let n = âˆ£ lengthBS x âˆ£ in ((n âˆ¸ 1) / 8) + 1

defaultConstantMeasure : TmCon â†’ CostingNat
defaultConstantMeasure (tmCon (atomic aInteger) x) = â„•toWords x
defaultConstantMeasure (tmCon (atomic aBytestring) x) = byteStringSize x
defaultConstantMeasure (tmCon (atomic aString) x) = length x -- each Char costs 1
defaultConstantMeasure (tmCon (atomic aUnit) x) = 1
defaultConstantMeasure (tmCon (atomic aBool) x) = 1
defaultConstantMeasure (tmCon (atomic aData) (ConstrDATA _ [])) = 0
defaultConstantMeasure (tmCon (atomic aData) (ConstrDATA i (x âˆ· xs))) = 
     defaultConstantMeasure (tmCon (atomic aData) x) 
   + defaultConstantMeasure (tmCon (atomic aData) (ConstrDATA i xs))
defaultConstantMeasure (tmCon (atomic aData) (MapDATA [])) = 0
defaultConstantMeasure (tmCon (atomic aData) (MapDATA ((x , y) âˆ· xs))) =
     defaultConstantMeasure (tmCon (atomic aData) x) 
   + defaultConstantMeasure (tmCon (atomic aData) y) 
   + defaultConstantMeasure (tmCon (atomic aData) (MapDATA xs)) 
defaultConstantMeasure (tmCon (atomic aData) (ListDATA [])) = 0
defaultConstantMeasure (tmCon (atomic aData) (ListDATA (x âˆ· xs))) =
     defaultConstantMeasure (tmCon (atomic aData) x) 
   + defaultConstantMeasure (tmCon (atomic aData) (ListDATA xs))
defaultConstantMeasure (tmCon (atomic aData) (iDATA x)) =  â„•toWords x
defaultConstantMeasure (tmCon (atomic aData) (bDATA x)) = byteStringSize x
defaultConstantMeasure (tmCon (atomic aBls12-381-g1-element) x) = g1ElementCost
defaultConstantMeasure (tmCon (atomic aBls12-381-g2-element) x) = g2ElementCost
defaultConstantMeasure (tmCon (atomic aBls12-381-mlresult) x) = mlResultElementCost
defaultConstantMeasure (tmCon (list t) []) = 0
defaultConstantMeasure (tmCon (list t) (x âˆ· xs)) = 
       defaultConstantMeasure (tmCon t x) 
     + defaultConstantMeasure (tmCon (list t) xs)
defaultConstantMeasure (tmCon (pair t u) (x , y)) = 
      1 + defaultConstantMeasure (tmCon t x) 
        + defaultConstantMeasure (tmCon u y)
```

## Builtin Cost Models

### Basic Definitions

```
Intercept : Set 
Intercept = CostingNat 

Slope : Set 
Slope = CostingNat 
```

### Models

A model is indexed by the number of arguments.

For example, for `ifThenElse` we would assign a model `constantCost : CostingModel 3`,
which is a model for a builtin that takes three arguments.

For `linearCost` the model takes an index indicating on which argument the cost
should be calculated.

TODO: Finish all models.

``` 
data CostingModel : â„• â†’ Set where 
   -- Any number of variables 
  constantCost       :  âˆ€{n} â†’ CostingNat â†’ CostingModel n
  linearCost         : âˆ€{n} â†’ Fin n â†’ Intercept â†’ Slope â†’ CostingModel n
  -- at least two arguments
  addedSizes         : âˆ€{n} â†’ Intercept â†’ Slope â†’ CostingModel (2 + n) 
  multipliedSizes    : âˆ€{n} â†’ Intercept â†’ Slope â†’ CostingModel (2 + n)
  minSize            : âˆ€{n} â†’ Intercept â†’ Slope â†’ CostingModel (2 + n)
  maxSize            : âˆ€{n} â†’ Intercept â†’ Slope â†’ CostingModel (2 + n)
   -- exactly two arguments 
--  twoArgumentsLinearInXAndY      : Intercept â†’ Slope â†’ Slope â†’ CostingModel 2
  twoArgumentsSubtractedSizes    : Intercept â†’ Slope â†’ CostingNat â†’ CostingModel 2

--  twoArgumentsLinearOnDiagonal   : CostingNat â†’ Intercept â†’ Slope â†’ CostingModel 2
  twoArgumentsConstAboveDiagonal : CostingNat â†’ CostingModel 2 â†’ CostingModel 2
--  twoArgumentsConstBelowDiagonal : CostingNat â†’ CostingModel 2 â†’ CostingModel 2
``` 

A model of a builtin consists of a pair of costing models, one for CPU and one for memory.

```
record BuiltinModel (b : Builtin) : Set where 
    field 
        costingCPU costingMem : CostingModel (arity b)
        
open BuiltinModel 
```

For now, we define a static function to assign a model to the arithmetic builtins,
and `ifThenElse`.

TODO: Construct the assignment for all builtins from json file.

```
assignModel : (b : Builtin) â†’ BuiltinModel b
assignModel addInteger = 
    record { costingCPU = maxSize 205665 812 
           ; costingMem = maxSize 1 1 }
assignModel subtractInteger = 
    record { costingCPU = maxSize 205665 812 
           ; costingMem = maxSize 1 1 }
assignModel multiplyInteger = 
    record { costingCPU = addedSizes 69522 11687 
           ; costingMem = addedSizes 0 1 }    
assignModel divideInteger =
    record { costingCPU = twoArgumentsConstAboveDiagonal 196500 (multipliedSizes 453240 220) 
           ; costingMem = twoArgumentsSubtractedSizes 0 1 1 }    
assignModel quotientInteger =
    record { costingCPU = twoArgumentsConstAboveDiagonal 196500 (multipliedSizes 453240 220) 
           ; costingMem = twoArgumentsSubtractedSizes 0 1 1 }    
assignModel remainderInteger =
    record { costingCPU = twoArgumentsConstAboveDiagonal 196500 (multipliedSizes 453240 220) 
           ; costingMem = twoArgumentsSubtractedSizes 0 1 1 }
assignModel modInteger =
    record { costingCPU = twoArgumentsConstAboveDiagonal 196500 (multipliedSizes 453240 220) 
           ; costingMem = twoArgumentsSubtractedSizes 0 1 1 }
assignModel equalsInteger =
    record { costingCPU = minSize 208512 421 
           ; costingMem = constantCost 1 }
assignModel lessThanInteger =
    record { costingCPU = minSize 208896 511 
           ; costingMem = constantCost 1 }
assignModel lessThanEqualsInteger =
    record { costingCPU = minSize 204924 473 
           ; costingMem = constantCost 1 }
assignModel ifThenElse = 
    record { costingCPU = constantCost 80556 
           ; costingMem = constantCost 1 }
assignModel _ = --TODO rest of builtins (or rather implement constructing model from json)
    record { costingCPU = constantCost 0 
           ; costingMem = constantCost 0 }

``` 

### Model interpretations

Some helper functions.

```
scaleLinearly : Intercept â†’ Slope â†’ CostingNat â†’ CostingNat 
scaleLinearly intercept slope n = intercept + slope * n

prod : âˆ€ {n} â†’ Vec â„• n â†’ â„•
prod = foldr _ _*_ 1

maximum minimum : âˆ€ {n} â†’ Vec â„• (suc n) â†’ â„•
maximum (a âˆ· xs) = foldr _ _âŠ”_ a xs
minimum (a âˆ· xs) = foldr _ _âŠ“_ a xs
``` 

Given a model and the sizes of the arguments we can compute a cost.

```
runModel : âˆ€{n} â†’ CostingModel n â†’ Vec CostingNat n â†’ CostingNat 
runModel (constantCost x) _ = x
runModel (linearCost zero i s) (a âˆ· _) = i + s * a
runModel (linearCost (suc n) i s) (_ âˆ· xs) = runModel (linearCost n i s) xs
runModel (addedSizes i s) xs = i + s * (sum xs)
runModel (multipliedSizes i s) xs = i + s * (prod xs)
runModel (minSize i s) xs = i + s * minimum xs
runModel (maxSize i s) xs = i + s * maximum xs
runModel (twoArgumentsSubtractedSizes i s min) (a âˆ· b âˆ· []) = i + s * (min âŠ” (a âˆ¸ b))
runModel (twoArgumentsConstAboveDiagonal c m) (a âˆ· b âˆ· [])  with a <? b 
... | yes _  = c 
... | no  _  = runModel m (a âˆ· b âˆ· [])
```

### Cost of executing a builtin

To calculate the cost of a builtin we obtain the corresponding builtin model, 
and run the cpu and memory model using the vector of argument sizes.

```
builtinCost : (b : Builtin) â†’ Vec CostingNat (arity b) â†’ ExBudget
builtinCost b cs = let bc = assignModel b 
                   in mkExBudget (runModel (costingCPU bc) cs) (runModel (costingMem bc) cs)
```

## Default Machine Parameters

The default machine parameters for `ExBudget`.

TODO : For now we will define fixed costs. Later, we should 
implement getting these values from the `cekMachineCosts.json` file.
Probably, we will do this by reusing Haskell code.
 
```
defaultCekMachineCost : StepKind â†’ ExBudget
defaultCekMachineCost _ = mkExBudget 23000 100

defaultCekStartupCost : ExBudget 
defaultCekStartupCost = mkExBudget 100 100

exBudgetCategoryCost : ExBudgetCategory â†’ ExBudget 
exBudgetCategoryCost (BStep x) = defaultCekMachineCost x
exBudgetCategoryCost (BBuiltinApp b cs) = builtinCost b cs
exBudgetCategoryCost BStartup = defaultCekStartupCost

defaultMachineParameters : MachineParameters ExBudget
defaultMachineParameters = record {
    startupCost = defaultCekStartupCost 
  ; cekMachineCost = exBudgetCategoryCost 
  ; Îµ = Îµâ‚¬
  ; _âˆ™_ = _âˆ™â‚¬_
  ; costMonoid = isMonoidExBudget
  ; constantMeasure = defaultConstantMeasure 
 } 

countingReport : ExBudget â†’ String 
countingReport (mkExBudget cpu mem) = 
      "\nCPU budget:    " ++ showâ„• cpu 
   ++ "\nMemory budget: " ++ showâ„• mem
```
 
 ## Tallying budget 

These functions define a type for Budgets that can record detailed cost information
about nodes and builtins.

We need a map from `ExBudgetCategory` into `ExBudget`. 
It's not the most efficient, but the simplest thing to do is to 
transform `ExBudgetCategory` into a string, and use that as keys.

```
mkKeyFromExBudgetCategory : ExBudgetCategory â†’ String 
mkKeyFromExBudgetCategory (BStep x) = "0" ++ showStepKind x
mkKeyFromExBudgetCategory (BBuiltinApp x _) = "1"++ showBuiltin x
mkKeyFromExBudgetCategory BStartup = "2"

TallyingBudget : Set 
TallyingBudget = Map ExBudget Ã— ExBudget

lookup : Map ExBudget â†’ ExBudgetCategory â†’ ExBudget
lookup m k with lookupAVL (mkKeyFromExBudgetCategory k) m 
... | just x = x
... | nothing = Îµâ‚¬
```

As required, `TallyingBudget` is a monoid. 

```
--unit of TallyingBudget 
ÎµT : TallyingBudget
ÎµT = empty , Îµâ‚¬

-- adding TallyingBudgets
_âˆ™T_ : TallyingBudget â†’ TallyingBudget â†’ TallyingBudget
(m , x) âˆ™T (n , y) = unionWith u m n , x âˆ™â‚¬ y
   where u : ExBudget â†’ Maybe (ExBudget) â†’ ExBudget
         u x (just y) = x âˆ™â‚¬ y
         u x nothing = x

-- TODO : Prove these postulates.
postulate TallyingBudget-assoc : Algebra.Associative _â‰¡_ _âˆ™T_
postulate Tallying-budget-identityÊ³ : Algebra.RightIdentity _â‰¡_ ÎµT _âˆ™T_

isMonoidTallyingBudget : IsMonoid _â‰¡_ _âˆ™T_ ÎµT
isMonoidTallyingBudget = record { 
       isSemigroup = record { 
           isMagma = record { isEquivalence = isEquivalence 
                            ; âˆ™-cong = Î» {refl refl â†’ refl }} 
           ; assoc = TallyingBudget-assoc } 
     ; identity = (Î» x â†’ refl) , Tallying-budget-identityÊ³ }

tallyingCekMachineCost : ExBudgetCategory â†’ TallyingBudget
tallyingCekMachineCost k = 
      let spent = exBudgetCategoryCost k 
      in singleton (mkKeyFromExBudgetCategory k) spent , spent

tallyingMachineParameters : MachineParameters TallyingBudget
tallyingMachineParameters = record { 
        startupCost = tallyingCekMachineCost BStartup 
      ; cekMachineCost = tallyingCekMachineCost
      ; Îµ = ÎµT
      ; _âˆ™_ = _âˆ™T_
      ; costMonoid = isMonoidTallyingBudget
      ; constantMeasure = defaultConstantMeasure
      } 

tallyingReport : TallyingBudget â†’ String
tallyingReport (mp , budget) =  
       countingReport budget
    ++ "\n"
    ++ " DEBUG : â„•toWords 1 = " ++ showâ„• (â„•toWords (+ 1)) 
    ++ " DEBUG : â„•toWords 2 = " ++ showâ„• (â„•toWords (+ 2))
    ++ "\n"
    ++ printStepReport mp
    ++ "\n"
    ++ "startup    " ++ budgetToString (lookup mp BStartup) ++ "\n"
    ++ "compute    " ++ budgetToString totalComputeCost ++ "\n"
    -- ++ "AST nodes  " ++ ++ "\n"
    ++ "\n\n"
    ++ printBuiltinReport mp 
    ++ "\n" 
    ++ "Total builtin costs:   " ++ budgetToString totalBuiltinModels ++ "\n"
     -- We would like to be able to print the following  number as "%4.2f" 
     -- but Agda's printf currently doesn't support it.
    ++ printf "Time spent executing builtins:  %f%%\n" (fromâ„• 100 *f (getCPU totalBuiltinModels) Ã· (getCPU budget)) ++ "\n"
    ++ "\n"
    ++ "Total budget spent:    " ++ budgetToString budget ++ "\n"
    ++  "Predicted execution time: " ++ formatTimePicoseconds (getCPU budget)
  where 
    totalComputeCost totalBuiltinModels : ExBudget 
    totalComputeCost = L.foldr (Î» x acc â†’ (lookup mp (BStep x)) âˆ™â‚¬ acc) Îµâ‚¬ stepKindList
    totalBuiltinModels = L.foldr _âˆ™â‚¬_ Îµâ‚¬ (L.map (lookup mp âˆ˜ (Î» b â†’ BBuiltinApp b (replicate 0))) builtinList)

    getCPU : ExBudget â†’ Float
    getCPU n = fromâ„• (ExCPU n)   

    budgetToString : ExBudget â†’ String 
    budgetToString (mkExBudget cpu mem) = padLeft ' ' 15 (showâ„• cpu) ++ "  " 
                                       ++ padLeft ' ' 15 (showâ„• mem)

    printStepCost : StepKind â†’ ExBudget â†’ String
    printStepCost sk budget = padRight ' ' 10 (showStepKind sk) ++ " " 
                           ++ padLeft ' ' 20 (budgetToString budget) ++ "\n"

    printStepReport : Map ExBudget â†’ String 
    printStepReport mp = L.foldr (Î» s xs â†’ printStepCost s (lookup mp (BStep s)) ++ xs)
                               "" 
                               stepKindList

    printBuiltinModel : Builtin â†’ ExBudget â†’ String 
    printBuiltinModel b (mkExBudget 0 0) = "" 
    printBuiltinModel b budget = padRight ' ' 22 (showBuiltin b) ++ " " 
                             ++ budgetToString budget ++ "\n"

    printBuiltinReport : Map ExBudget â†’ String 
    printBuiltinReport mp = 
        L.foldr (Î» b xs â†’ printBuiltinModel b (lookup mp (BBuiltinApp b (replicate 0))) ++ xs) 
              "" 
              builtinList     
    
    formatTimePicoseconds : Float â†’ String
    formatTimePicoseconds t = if 1e12 â‰¤áµ‡ t then (printf "%f s" (t Ã· 1e12)) else
                              if 1e9 â‰¤áµ‡ t then  (printf "%f ms" (t Ã· 1e9)) else
                              if 1e6 â‰¤áµ‡ t then  (printf "%f Î¼s" (t Ã· 1e6)) else
                              if 1e3 â‰¤áµ‡ t then  (printf "%f ns" (t Ã· 1e3)) else
                              printf "%f ps" t
 ```