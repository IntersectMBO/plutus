<!DOCTYPE HTML>
<html><head><meta charset="utf-8"><title>Function.Nary.NonDependent.Base</title><link rel="stylesheet" href="Agda.css"></head><body><pre class="Agda"><a id="1" class="Comment">------------------------------------------------------------------------</a>
<a id="74" class="Comment">-- The Agda standard library</a>
<a id="103" class="Comment">--</a>
<a id="106" class="Comment">-- Heterogeneous N-ary Functions: basic types and operations</a>
<a id="167" class="Comment">------------------------------------------------------------------------</a>

<a id="241" class="Symbol">{-#</a> <a id="245" class="Keyword">OPTIONS</a> <a id="253" class="Pragma">--cubical-compatible</a> <a id="274" class="Pragma">--safe</a> <a id="281" class="Symbol">#-}</a>

<a id="286" class="Keyword">module</a> <a id="293" href="Function.Nary.NonDependent.Base.html" class="Module">Function.Nary.NonDependent.Base</a> <a id="325" class="Keyword">where</a>

<a id="332" class="Comment">------------------------------------------------------------------------</a>
<a id="405" class="Comment">-- Concrete examples can be found in README.Nary. This file&#39;s comments</a>
<a id="476" class="Comment">-- are more focused on the implementation details and the motivations</a>
<a id="546" class="Comment">-- behind the design decisions.</a>
<a id="578" class="Comment">------------------------------------------------------------------------</a>

<a id="652" class="Keyword">open</a> <a id="657" class="Keyword">import</a> <a id="664" href="Level.html" class="Module">Level</a> <a id="670" class="Keyword">using</a> <a id="676" class="Symbol">(</a><a id="677" href="Agda.Primitive.html#742" class="Postulate">Level</a><a id="682" class="Symbol">;</a> <a id="684" href="Level.html#521" class="Function">0ℓ</a><a id="686" class="Symbol">;</a> <a id="688" href="Agda.Primitive.html#961" class="Primitive Operator">_⊔_</a><a id="691" class="Symbol">)</a>
<a id="693" class="Keyword">open</a> <a id="698" class="Keyword">import</a> <a id="705" href="Data.Nat.Base.html" class="Module">Data.Nat.Base</a> <a id="719" class="Keyword">using</a> <a id="725" class="Symbol">(</a><a id="726" href="Agda.Builtin.Nat.html#203" class="Datatype">ℕ</a><a id="727" class="Symbol">;</a> <a id="729" href="Agda.Builtin.Nat.html#221" class="InductiveConstructor">zero</a><a id="733" class="Symbol">;</a> <a id="735" href="Agda.Builtin.Nat.html#234" class="InductiveConstructor">suc</a><a id="738" class="Symbol">)</a>
<a id="740" class="Keyword">open</a> <a id="745" class="Keyword">import</a> <a id="752" href="Data.Product.Base.html" class="Module">Data.Product.Base</a> <a id="770" class="Keyword">using</a> <a id="776" class="Symbol">(</a><a id="777" href="Data.Product.Base.html#1618" class="Function Operator">_×_</a><a id="780" class="Symbol">;</a> <a id="782" href="Agda.Builtin.Sigma.html#235" class="InductiveConstructor Operator">_,_</a><a id="785" class="Symbol">)</a>
<a id="787" class="Keyword">open</a> <a id="792" class="Keyword">import</a> <a id="799" href="Data.Unit.Polymorphic.Base.html" class="Module">Data.Unit.Polymorphic.Base</a>
<a id="826" class="Keyword">open</a> <a id="831" class="Keyword">import</a> <a id="838" href="Function.Base.html" class="Module">Function.Base</a> <a id="852" class="Keyword">using</a> <a id="858" class="Symbol">(</a><a id="859" href="Function.Base.html#3626" class="Function Operator">_∘′_</a><a id="863" class="Symbol">;</a> <a id="865" href="Function.Base.html#3839" class="Function Operator">_$′_</a><a id="869" class="Symbol">;</a> <a id="871" href="Function.Base.html#725" class="Function">const</a><a id="876" class="Symbol">;</a> <a id="878" href="Function.Base.html#1638" class="Function">flip</a><a id="882" class="Symbol">)</a>

<a id="885" class="Keyword">private</a>
  <a id="895" class="Keyword">variable</a>
    <a id="908" href="Function.Nary.NonDependent.Base.html#908" class="Generalizable">a</a> <a id="910" href="Function.Nary.NonDependent.Base.html#910" class="Generalizable">b</a> <a id="912" href="Function.Nary.NonDependent.Base.html#912" class="Generalizable">c</a> <a id="914" class="Symbol">:</a> <a id="916" href="Agda.Primitive.html#742" class="Postulate">Level</a>
    <a id="926" href="Function.Nary.NonDependent.Base.html#926" class="Generalizable">A</a> <a id="928" class="Symbol">:</a> <a id="930" href="Agda.Primitive.html#388" class="Primitive">Set</a> <a id="934" href="Function.Nary.NonDependent.Base.html#908" class="Generalizable">a</a>
    <a id="940" href="Function.Nary.NonDependent.Base.html#940" class="Generalizable">B</a> <a id="942" class="Symbol">:</a> <a id="944" href="Agda.Primitive.html#388" class="Primitive">Set</a> <a id="948" href="Function.Nary.NonDependent.Base.html#910" class="Generalizable">b</a>
    <a id="954" href="Function.Nary.NonDependent.Base.html#954" class="Generalizable">C</a> <a id="956" class="Symbol">:</a> <a id="958" href="Agda.Primitive.html#388" class="Primitive">Set</a> <a id="962" href="Function.Nary.NonDependent.Base.html#912" class="Generalizable">c</a>

<a id="965" class="Comment">------------------------------------------------------------------------</a>
<a id="1038" class="Comment">-- Type Definitions</a>

<a id="1059" class="Comment">-- We want to define n-ary function spaces and generic n-ary operations</a>
<a id="1131" class="Comment">-- on them such as (un)currying, zipWith, alignWith, etc.</a>

<a id="1190" class="Comment">-- We want users to be able to use these seamlessly whenever n is concrete.</a>

<a id="1267" class="Comment">-- In other words, we want Agda to infer the sets `A₁, ⋯, Aₙ` when we</a>
<a id="1337" class="Comment">-- write `uncurryₙ n f` where `f` has type `A₁ → ⋯ → Aₙ → B`. For this</a>
<a id="1408" class="Comment">-- to happen, we need the structure in which these Sets are stored to</a>
<a id="1478" class="Comment">-- effectively η-expand to `A₁, ⋯, Aₙ` when the parameter `n` is known.</a>

<a id="1551" class="Comment">-- Hence the following definitions:</a>
<a id="1587" class="Comment">------------------------------------------------------------------------</a>

<a id="1661" class="Comment">-- First, a &quot;vector&quot; of `n` Levels (defined by induction on n so that it</a>
<a id="1734" class="Comment">-- may be built by η-expansion and unification). Each Level will be that</a>
<a id="1807" class="Comment">-- of one of the Sets we want to take the n-ary product of.</a>

<a id="Levels"></a><a id="1868" href="Function.Nary.NonDependent.Base.html#1868" class="Function">Levels</a> <a id="1875" class="Symbol">:</a> <a id="1877" href="Agda.Builtin.Nat.html#203" class="Datatype">ℕ</a> <a id="1879" class="Symbol">→</a> <a id="1881" href="Agda.Primitive.html#388" class="Primitive">Set</a>
<a id="1885" href="Function.Nary.NonDependent.Base.html#1868" class="Function">Levels</a> <a id="1892" href="Agda.Builtin.Nat.html#221" class="InductiveConstructor">zero</a>    <a id="1900" class="Symbol">=</a> <a id="1902" href="Data.Unit.Polymorphic.Base.html#489" class="Function">⊤</a>
<a id="1904" href="Function.Nary.NonDependent.Base.html#1868" class="Function">Levels</a> <a id="1911" class="Symbol">(</a><a id="1912" href="Agda.Builtin.Nat.html#234" class="InductiveConstructor">suc</a> <a id="1916" href="Function.Nary.NonDependent.Base.html#1916" class="Bound">n</a><a id="1917" class="Symbol">)</a> <a id="1919" class="Symbol">=</a> <a id="1921" href="Agda.Primitive.html#742" class="Postulate">Level</a> <a id="1927" href="Data.Product.Base.html#1618" class="Function Operator">×</a> <a id="1929" href="Function.Nary.NonDependent.Base.html#1868" class="Function">Levels</a> <a id="1936" href="Function.Nary.NonDependent.Base.html#1916" class="Bound">n</a>

<a id="1939" class="Comment">-- The overall Level of a `n` Sets of respective levels `l₁, ⋯, lₙ` will</a>
<a id="2012" class="Comment">-- be the least upper bound `l₁ ⊔ ⋯ ⊔ lₙ` of all of the Levels involved.</a>
<a id="2085" class="Comment">-- Hence the following definition of n-ary least upper bound:</a>

<a id="⨆"></a><a id="2148" href="Function.Nary.NonDependent.Base.html#2148" class="Function">⨆</a> <a id="2150" class="Symbol">:</a> <a id="2152" class="Symbol">∀</a> <a id="2154" href="Function.Nary.NonDependent.Base.html#2154" class="Bound">n</a> <a id="2156" class="Symbol">→</a> <a id="2158" href="Function.Nary.NonDependent.Base.html#1868" class="Function">Levels</a> <a id="2165" href="Function.Nary.NonDependent.Base.html#2154" class="Bound">n</a> <a id="2167" class="Symbol">→</a> <a id="2169" href="Agda.Primitive.html#742" class="Postulate">Level</a>
<a id="2175" href="Function.Nary.NonDependent.Base.html#2148" class="Function">⨆</a> <a id="2177" href="Agda.Builtin.Nat.html#221" class="InductiveConstructor">zero</a>    <a id="2185" class="Symbol">_</a>        <a id="2194" class="Symbol">=</a> <a id="2196" href="Agda.Primitive.html#915" class="Primitive">Level.zero</a>
<a id="2207" href="Function.Nary.NonDependent.Base.html#2148" class="Function">⨆</a> <a id="2209" class="Symbol">(</a><a id="2210" href="Agda.Builtin.Nat.html#234" class="InductiveConstructor">suc</a> <a id="2214" href="Function.Nary.NonDependent.Base.html#2214" class="Bound">n</a><a id="2215" class="Symbol">)</a> <a id="2217" class="Symbol">(</a><a id="2218" href="Function.Nary.NonDependent.Base.html#2218" class="Bound">l</a> <a id="2220" href="Agda.Builtin.Sigma.html#235" class="InductiveConstructor Operator">,</a> <a id="2222" href="Function.Nary.NonDependent.Base.html#2222" class="Bound">ls</a><a id="2224" class="Symbol">)</a> <a id="2226" class="Symbol">=</a> <a id="2228" href="Function.Nary.NonDependent.Base.html#2218" class="Bound">l</a> <a id="2230" href="Agda.Primitive.html#961" class="Primitive Operator">⊔</a> <a id="2232" class="Symbol">(</a><a id="2233" href="Function.Nary.NonDependent.Base.html#2148" class="Function">⨆</a> <a id="2235" href="Function.Nary.NonDependent.Base.html#2214" class="Bound">n</a> <a id="2237" href="Function.Nary.NonDependent.Base.html#2222" class="Bound">ls</a><a id="2239" class="Symbol">)</a>

<a id="2242" class="Comment">-- Second, a &quot;vector&quot; of `n` Sets whose respective Levels are determined</a>
<a id="2315" class="Comment">-- by the `Levels n` input.</a>

<a id="Sets"></a><a id="2344" href="Function.Nary.NonDependent.Base.html#2344" class="Function">Sets</a> <a id="2349" class="Symbol">:</a> <a id="2351" class="Symbol">∀</a> <a id="2353" href="Function.Nary.NonDependent.Base.html#2353" class="Bound">n</a> <a id="2355" class="Symbol">(</a><a id="2356" href="Function.Nary.NonDependent.Base.html#2356" class="Bound">ls</a> <a id="2359" class="Symbol">:</a> <a id="2361" href="Function.Nary.NonDependent.Base.html#1868" class="Function">Levels</a> <a id="2368" href="Function.Nary.NonDependent.Base.html#2353" class="Bound">n</a><a id="2369" class="Symbol">)</a> <a id="2371" class="Symbol">→</a> <a id="2373" href="Agda.Primitive.html#388" class="Primitive">Set</a> <a id="2377" class="Symbol">(</a><a id="2378" href="Agda.Primitive.html#931" class="Primitive">Level.suc</a> <a id="2388" class="Symbol">(</a><a id="2389" href="Function.Nary.NonDependent.Base.html#2148" class="Function">⨆</a> <a id="2391" href="Function.Nary.NonDependent.Base.html#2353" class="Bound">n</a> <a id="2393" href="Function.Nary.NonDependent.Base.html#2356" class="Bound">ls</a><a id="2395" class="Symbol">))</a>
<a id="2398" href="Function.Nary.NonDependent.Base.html#2344" class="Function">Sets</a> <a id="2403" href="Agda.Builtin.Nat.html#221" class="InductiveConstructor">zero</a>    <a id="2411" class="Symbol">_</a>        <a id="2420" class="Symbol">=</a> <a id="2422" href="Data.Unit.Polymorphic.Base.html#489" class="Function">⊤</a>
<a id="2424" href="Function.Nary.NonDependent.Base.html#2344" class="Function">Sets</a> <a id="2429" class="Symbol">(</a><a id="2430" href="Agda.Builtin.Nat.html#234" class="InductiveConstructor">suc</a> <a id="2434" href="Function.Nary.NonDependent.Base.html#2434" class="Bound">n</a><a id="2435" class="Symbol">)</a> <a id="2437" class="Symbol">(</a><a id="2438" href="Function.Nary.NonDependent.Base.html#2438" class="Bound">l</a> <a id="2440" href="Agda.Builtin.Sigma.html#235" class="InductiveConstructor Operator">,</a> <a id="2442" href="Function.Nary.NonDependent.Base.html#2442" class="Bound">ls</a><a id="2444" class="Symbol">)</a> <a id="2446" class="Symbol">=</a> <a id="2448" href="Agda.Primitive.html#388" class="Primitive">Set</a> <a id="2452" href="Function.Nary.NonDependent.Base.html#2438" class="Bound">l</a> <a id="2454" href="Data.Product.Base.html#1618" class="Function Operator">×</a> <a id="2456" href="Function.Nary.NonDependent.Base.html#2344" class="Function">Sets</a> <a id="2461" href="Function.Nary.NonDependent.Base.html#2434" class="Bound">n</a> <a id="2463" href="Function.Nary.NonDependent.Base.html#2442" class="Bound">ls</a>

<a id="2467" class="Comment">-- Third, a function type whose domains are given by a &quot;vector&quot; of `n`</a>
<a id="2538" class="Comment">-- Sets `A₁, ⋯, Aₙ` and whose codomain is `B`. `Arrows` forms such a</a>
<a id="2607" class="Comment">-- type of shape `A₁ → ⋯ → Aₙ → B` by induction on `n`.</a>

<a id="Arrows"></a><a id="2664" href="Function.Nary.NonDependent.Base.html#2664" class="Function">Arrows</a> <a id="2671" class="Symbol">:</a> <a id="2673" class="Symbol">∀</a> <a id="2675" href="Function.Nary.NonDependent.Base.html#2675" class="Bound">n</a> <a id="2677" class="Symbol">{</a><a id="2678" href="Function.Nary.NonDependent.Base.html#2678" class="Bound">r</a> <a id="2680" href="Function.Nary.NonDependent.Base.html#2680" class="Bound">ls</a><a id="2682" class="Symbol">}</a> <a id="2684" class="Symbol">→</a> <a id="2686" href="Function.Nary.NonDependent.Base.html#2344" class="Function">Sets</a> <a id="2691" href="Function.Nary.NonDependent.Base.html#2675" class="Bound">n</a> <a id="2693" href="Function.Nary.NonDependent.Base.html#2680" class="Bound">ls</a> <a id="2696" class="Symbol">→</a> <a id="2698" href="Agda.Primitive.html#388" class="Primitive">Set</a> <a id="2702" href="Function.Nary.NonDependent.Base.html#2678" class="Bound">r</a> <a id="2704" class="Symbol">→</a> <a id="2706" href="Agda.Primitive.html#388" class="Primitive">Set</a> <a id="2710" class="Symbol">(</a><a id="2711" href="Function.Nary.NonDependent.Base.html#2678" class="Bound">r</a> <a id="2713" href="Agda.Primitive.html#961" class="Primitive Operator">⊔</a> <a id="2715" class="Symbol">(</a><a id="2716" href="Function.Nary.NonDependent.Base.html#2148" class="Function">⨆</a> <a id="2718" href="Function.Nary.NonDependent.Base.html#2675" class="Bound">n</a> <a id="2720" href="Function.Nary.NonDependent.Base.html#2680" class="Bound">ls</a><a id="2722" class="Symbol">))</a>
<a id="2725" href="Function.Nary.NonDependent.Base.html#2664" class="Function">Arrows</a> <a id="2732" href="Agda.Builtin.Nat.html#221" class="InductiveConstructor">zero</a>    <a id="2740" class="Symbol">_</a>        <a id="2749" href="Function.Nary.NonDependent.Base.html#2749" class="Bound">b</a> <a id="2751" class="Symbol">=</a> <a id="2753" href="Function.Nary.NonDependent.Base.html#2749" class="Bound">b</a>
<a id="2755" href="Function.Nary.NonDependent.Base.html#2664" class="Function">Arrows</a> <a id="2762" class="Symbol">(</a><a id="2763" href="Agda.Builtin.Nat.html#234" class="InductiveConstructor">suc</a> <a id="2767" href="Function.Nary.NonDependent.Base.html#2767" class="Bound">n</a><a id="2768" class="Symbol">)</a> <a id="2770" class="Symbol">(</a><a id="2771" href="Function.Nary.NonDependent.Base.html#2771" class="Bound">a</a> <a id="2773" href="Agda.Builtin.Sigma.html#235" class="InductiveConstructor Operator">,</a> <a id="2775" href="Function.Nary.NonDependent.Base.html#2775" class="Bound">as</a><a id="2777" class="Symbol">)</a> <a id="2779" href="Function.Nary.NonDependent.Base.html#2779" class="Bound">b</a> <a id="2781" class="Symbol">=</a> <a id="2783" href="Function.Nary.NonDependent.Base.html#2771" class="Bound">a</a> <a id="2785" class="Symbol">→</a> <a id="2787" href="Function.Nary.NonDependent.Base.html#2664" class="Function">Arrows</a> <a id="2794" href="Function.Nary.NonDependent.Base.html#2767" class="Bound">n</a> <a id="2796" href="Function.Nary.NonDependent.Base.html#2775" class="Bound">as</a> <a id="2799" href="Function.Nary.NonDependent.Base.html#2779" class="Bound">b</a>

<a id="2802" class="Comment">-- We introduce a notation for this definition</a>

<a id="2850" class="Keyword">infixr</a> <a id="2857" class="Number">0</a> <a id="2859" href="Function.Nary.NonDependent.Base.html#2863" class="Function Operator">_⇉_</a>
<a id="_⇉_"></a><a id="2863" href="Function.Nary.NonDependent.Base.html#2863" class="Function Operator">_⇉_</a> <a id="2867" class="Symbol">:</a> <a id="2869" class="Symbol">∀</a> <a id="2871" class="Symbol">{</a><a id="2872" href="Function.Nary.NonDependent.Base.html#2872" class="Bound">n</a> <a id="2874" href="Function.Nary.NonDependent.Base.html#2874" class="Bound">ls</a> <a id="2877" href="Function.Nary.NonDependent.Base.html#2877" class="Bound">r</a><a id="2878" class="Symbol">}</a> <a id="2880" class="Symbol">→</a> <a id="2882" href="Function.Nary.NonDependent.Base.html#2344" class="Function">Sets</a> <a id="2887" href="Function.Nary.NonDependent.Base.html#2872" class="Bound">n</a> <a id="2889" href="Function.Nary.NonDependent.Base.html#2874" class="Bound">ls</a> <a id="2892" class="Symbol">→</a> <a id="2894" href="Agda.Primitive.html#388" class="Primitive">Set</a> <a id="2898" href="Function.Nary.NonDependent.Base.html#2877" class="Bound">r</a> <a id="2900" class="Symbol">→</a> <a id="2902" href="Agda.Primitive.html#388" class="Primitive">Set</a> <a id="2906" class="Symbol">(</a><a id="2907" href="Function.Nary.NonDependent.Base.html#2877" class="Bound">r</a> <a id="2909" href="Agda.Primitive.html#961" class="Primitive Operator">⊔</a> <a id="2911" class="Symbol">(</a><a id="2912" href="Function.Nary.NonDependent.Base.html#2148" class="Function">⨆</a> <a id="2914" href="Function.Nary.NonDependent.Base.html#2872" class="Bound">n</a> <a id="2916" href="Function.Nary.NonDependent.Base.html#2874" class="Bound">ls</a><a id="2918" class="Symbol">))</a>
<a id="2921" href="Function.Nary.NonDependent.Base.html#2863" class="Function Operator">_⇉_</a> <a id="2925" class="Symbol">=</a> <a id="2927" href="Function.Nary.NonDependent.Base.html#2664" class="Function">Arrows</a> <a id="2934" class="Symbol">_</a>

<a id="2937" class="Comment">------------------------------------------------------------------------</a>
<a id="3010" class="Comment">-- Operations on Sets</a>

<a id="3033" class="Comment">-- Level-respecting map</a>

<a id="3058" class="Keyword">infixr</a> <a id="3065" class="Number">-1</a> <a id="3068" href="Function.Nary.NonDependent.Base.html#3075" class="Function Operator">_&lt;$&gt;_</a>

<a id="_&lt;$&gt;_"></a><a id="3075" href="Function.Nary.NonDependent.Base.html#3075" class="Function Operator">_&lt;$&gt;_</a> <a id="3081" class="Symbol">:</a> <a id="3083" class="Symbol">(∀</a> <a id="3086" class="Symbol">{</a><a id="3087" href="Function.Nary.NonDependent.Base.html#3087" class="Bound">l</a><a id="3088" class="Symbol">}</a> <a id="3090" class="Symbol">→</a> <a id="3092" href="Agda.Primitive.html#388" class="Primitive">Set</a> <a id="3096" href="Function.Nary.NonDependent.Base.html#3087" class="Bound">l</a> <a id="3098" class="Symbol">→</a> <a id="3100" href="Agda.Primitive.html#388" class="Primitive">Set</a> <a id="3104" href="Function.Nary.NonDependent.Base.html#3087" class="Bound">l</a><a id="3105" class="Symbol">)</a> <a id="3107" class="Symbol">→</a>
        <a id="3117" class="Symbol">∀</a> <a id="3119" class="Symbol">{</a><a id="3120" href="Function.Nary.NonDependent.Base.html#3120" class="Bound">n</a> <a id="3122" href="Function.Nary.NonDependent.Base.html#3122" class="Bound">ls</a><a id="3124" class="Symbol">}</a> <a id="3126" class="Symbol">→</a> <a id="3128" href="Function.Nary.NonDependent.Base.html#2344" class="Function">Sets</a> <a id="3133" href="Function.Nary.NonDependent.Base.html#3120" class="Bound">n</a> <a id="3135" href="Function.Nary.NonDependent.Base.html#3122" class="Bound">ls</a> <a id="3138" class="Symbol">→</a> <a id="3140" href="Function.Nary.NonDependent.Base.html#2344" class="Function">Sets</a> <a id="3145" href="Function.Nary.NonDependent.Base.html#3120" class="Bound">n</a> <a id="3147" href="Function.Nary.NonDependent.Base.html#3122" class="Bound">ls</a>
<a id="3150" href="Function.Nary.NonDependent.Base.html#3075" class="Function Operator">_&lt;$&gt;_</a> <a id="3156" href="Function.Nary.NonDependent.Base.html#3156" class="Bound">f</a> <a id="3158" class="Symbol">{</a><a id="3159" href="Agda.Builtin.Nat.html#221" class="InductiveConstructor">zero</a><a id="3163" class="Symbol">}</a>   <a id="3167" href="Function.Nary.NonDependent.Base.html#3167" class="Bound">as</a>        <a id="3177" class="Symbol">=</a> <a id="3179" class="Symbol">_</a>
<a id="3181" href="Function.Nary.NonDependent.Base.html#3075" class="Function Operator">_&lt;$&gt;_</a> <a id="3187" href="Function.Nary.NonDependent.Base.html#3187" class="Bound">f</a> <a id="3189" class="Symbol">{</a><a id="3190" href="Agda.Builtin.Nat.html#234" class="InductiveConstructor">suc</a> <a id="3194" href="Function.Nary.NonDependent.Base.html#3194" class="Bound">n</a><a id="3195" class="Symbol">}</a>  <a id="3198" class="Symbol">(</a><a id="3199" href="Function.Nary.NonDependent.Base.html#3199" class="Bound">a</a> <a id="3201" href="Agda.Builtin.Sigma.html#235" class="InductiveConstructor Operator">,</a> <a id="3203" href="Function.Nary.NonDependent.Base.html#3203" class="Bound">as</a><a id="3205" class="Symbol">)</a>  <a id="3208" class="Symbol">=</a> <a id="3210" href="Function.Nary.NonDependent.Base.html#3187" class="Bound">f</a> <a id="3212" href="Function.Nary.NonDependent.Base.html#3199" class="Bound">a</a> <a id="3214" href="Agda.Builtin.Sigma.html#235" class="InductiveConstructor Operator">,</a> <a id="3216" class="Symbol">(</a><a id="3217" href="Function.Nary.NonDependent.Base.html#3187" class="Bound">f</a> <a id="3219" href="Function.Nary.NonDependent.Base.html#3075" class="Function Operator">&lt;$&gt;</a> <a id="3223" href="Function.Nary.NonDependent.Base.html#3203" class="Bound">as</a><a id="3225" class="Symbol">)</a>

<a id="3228" class="Comment">-- Level-modifying generalised map</a>

<a id="lmap"></a><a id="3264" href="Function.Nary.NonDependent.Base.html#3264" class="Function">lmap</a> <a id="3269" class="Symbol">:</a> <a id="3271" class="Symbol">(</a><a id="3272" href="Agda.Primitive.html#742" class="Postulate">Level</a> <a id="3278" class="Symbol">→</a> <a id="3280" href="Agda.Primitive.html#742" class="Postulate">Level</a><a id="3285" class="Symbol">)</a> <a id="3287" class="Symbol">→</a> <a id="3289" class="Symbol">∀</a> <a id="3291" href="Function.Nary.NonDependent.Base.html#3291" class="Bound">n</a> <a id="3293" class="Symbol">→</a> <a id="3295" href="Function.Nary.NonDependent.Base.html#1868" class="Function">Levels</a> <a id="3302" href="Function.Nary.NonDependent.Base.html#3291" class="Bound">n</a> <a id="3304" class="Symbol">→</a> <a id="3306" href="Function.Nary.NonDependent.Base.html#1868" class="Function">Levels</a> <a id="3313" href="Function.Nary.NonDependent.Base.html#3291" class="Bound">n</a>
<a id="3315" href="Function.Nary.NonDependent.Base.html#3264" class="Function">lmap</a> <a id="3320" href="Function.Nary.NonDependent.Base.html#3320" class="Bound">f</a> <a id="3322" href="Agda.Builtin.Nat.html#221" class="InductiveConstructor">zero</a>    <a id="3330" href="Function.Nary.NonDependent.Base.html#3330" class="Bound">ls</a>       <a id="3339" class="Symbol">=</a> <a id="3341" class="Symbol">_</a>
<a id="3343" href="Function.Nary.NonDependent.Base.html#3264" class="Function">lmap</a> <a id="3348" href="Function.Nary.NonDependent.Base.html#3348" class="Bound">f</a> <a id="3350" class="Symbol">(</a><a id="3351" href="Agda.Builtin.Nat.html#234" class="InductiveConstructor">suc</a> <a id="3355" href="Function.Nary.NonDependent.Base.html#3355" class="Bound">n</a><a id="3356" class="Symbol">)</a> <a id="3358" class="Symbol">(</a><a id="3359" href="Function.Nary.NonDependent.Base.html#3359" class="Bound">l</a> <a id="3361" href="Agda.Builtin.Sigma.html#235" class="InductiveConstructor Operator">,</a> <a id="3363" href="Function.Nary.NonDependent.Base.html#3363" class="Bound">ls</a><a id="3365" class="Symbol">)</a> <a id="3367" class="Symbol">=</a> <a id="3369" href="Function.Nary.NonDependent.Base.html#3348" class="Bound">f</a> <a id="3371" href="Function.Nary.NonDependent.Base.html#3359" class="Bound">l</a> <a id="3373" href="Agda.Builtin.Sigma.html#235" class="InductiveConstructor Operator">,</a> <a id="3375" href="Function.Nary.NonDependent.Base.html#3264" class="Function">lmap</a> <a id="3380" href="Function.Nary.NonDependent.Base.html#3348" class="Bound">f</a> <a id="3382" href="Function.Nary.NonDependent.Base.html#3355" class="Bound">n</a> <a id="3384" href="Function.Nary.NonDependent.Base.html#3363" class="Bound">ls</a>

<a id="smap"></a><a id="3388" href="Function.Nary.NonDependent.Base.html#3388" class="Function">smap</a> <a id="3393" class="Symbol">:</a> <a id="3395" class="Symbol">∀</a> <a id="3397" href="Function.Nary.NonDependent.Base.html#3397" class="Bound">f</a> <a id="3399" class="Symbol">→</a> <a id="3401" class="Symbol">(∀</a> <a id="3404" class="Symbol">{</a><a id="3405" href="Function.Nary.NonDependent.Base.html#3405" class="Bound">l</a><a id="3406" class="Symbol">}</a> <a id="3408" class="Symbol">→</a> <a id="3410" href="Agda.Primitive.html#388" class="Primitive">Set</a> <a id="3414" href="Function.Nary.NonDependent.Base.html#3405" class="Bound">l</a> <a id="3416" class="Symbol">→</a> <a id="3418" href="Agda.Primitive.html#388" class="Primitive">Set</a> <a id="3422" class="Symbol">(</a><a id="3423" href="Function.Nary.NonDependent.Base.html#3397" class="Bound">f</a> <a id="3425" href="Function.Nary.NonDependent.Base.html#3405" class="Bound">l</a><a id="3426" class="Symbol">))</a> <a id="3429" class="Symbol">→</a>
       <a id="3438" class="Symbol">∀</a> <a id="3440" href="Function.Nary.NonDependent.Base.html#3440" class="Bound">n</a> <a id="3442" class="Symbol">{</a><a id="3443" href="Function.Nary.NonDependent.Base.html#3443" class="Bound">ls</a><a id="3445" class="Symbol">}</a> <a id="3447" class="Symbol">→</a> <a id="3449" href="Function.Nary.NonDependent.Base.html#2344" class="Function">Sets</a> <a id="3454" href="Function.Nary.NonDependent.Base.html#3440" class="Bound">n</a> <a id="3456" href="Function.Nary.NonDependent.Base.html#3443" class="Bound">ls</a> <a id="3459" class="Symbol">→</a> <a id="3461" href="Function.Nary.NonDependent.Base.html#2344" class="Function">Sets</a> <a id="3466" href="Function.Nary.NonDependent.Base.html#3440" class="Bound">n</a> <a id="3468" class="Symbol">(</a><a id="3469" href="Function.Nary.NonDependent.Base.html#3264" class="Function">lmap</a> <a id="3474" href="Function.Nary.NonDependent.Base.html#3397" class="Bound">f</a> <a id="3476" href="Function.Nary.NonDependent.Base.html#3440" class="Bound">n</a> <a id="3478" href="Function.Nary.NonDependent.Base.html#3443" class="Bound">ls</a><a id="3480" class="Symbol">)</a>
<a id="3482" href="Function.Nary.NonDependent.Base.html#3388" class="Function">smap</a> <a id="3487" href="Function.Nary.NonDependent.Base.html#3487" class="Bound">f</a> <a id="3489" href="Function.Nary.NonDependent.Base.html#3489" class="Bound">F</a> <a id="3491" href="Agda.Builtin.Nat.html#221" class="InductiveConstructor">zero</a>    <a id="3499" href="Function.Nary.NonDependent.Base.html#3499" class="Bound">as</a>       <a id="3508" class="Symbol">=</a> <a id="3510" class="Symbol">_</a>
<a id="3512" href="Function.Nary.NonDependent.Base.html#3388" class="Function">smap</a> <a id="3517" href="Function.Nary.NonDependent.Base.html#3517" class="Bound">f</a> <a id="3519" href="Function.Nary.NonDependent.Base.html#3519" class="Bound">F</a> <a id="3521" class="Symbol">(</a><a id="3522" href="Agda.Builtin.Nat.html#234" class="InductiveConstructor">suc</a> <a id="3526" href="Function.Nary.NonDependent.Base.html#3526" class="Bound">n</a><a id="3527" class="Symbol">)</a> <a id="3529" class="Symbol">(</a><a id="3530" href="Function.Nary.NonDependent.Base.html#3530" class="Bound">a</a> <a id="3532" href="Agda.Builtin.Sigma.html#235" class="InductiveConstructor Operator">,</a> <a id="3534" href="Function.Nary.NonDependent.Base.html#3534" class="Bound">as</a><a id="3536" class="Symbol">)</a> <a id="3538" class="Symbol">=</a> <a id="3540" href="Function.Nary.NonDependent.Base.html#3519" class="Bound">F</a> <a id="3542" href="Function.Nary.NonDependent.Base.html#3530" class="Bound">a</a> <a id="3544" href="Agda.Builtin.Sigma.html#235" class="InductiveConstructor Operator">,</a> <a id="3546" href="Function.Nary.NonDependent.Base.html#3388" class="Function">smap</a> <a id="3551" href="Function.Nary.NonDependent.Base.html#3517" class="Bound">f</a> <a id="3553" href="Function.Nary.NonDependent.Base.html#3519" class="Bound">F</a> <a id="3555" href="Function.Nary.NonDependent.Base.html#3526" class="Bound">n</a> <a id="3557" href="Function.Nary.NonDependent.Base.html#3534" class="Bound">as</a>

<a id="3561" class="Comment">------------------------------------------------------------------------</a>
<a id="3634" class="Comment">-- Operations on Functions</a>

<a id="3662" class="Comment">-- mapping under n arguments</a>

<a id="mapₙ"></a><a id="3692" href="Function.Nary.NonDependent.Base.html#3692" class="Function">mapₙ</a> <a id="3697" class="Symbol">:</a> <a id="3699" class="Symbol">∀</a> <a id="3701" href="Function.Nary.NonDependent.Base.html#3701" class="Bound">n</a> <a id="3703" class="Symbol">{</a><a id="3704" href="Function.Nary.NonDependent.Base.html#3704" class="Bound">ls</a><a id="3706" class="Symbol">}</a> <a id="3708" class="Symbol">{</a><a id="3709" href="Function.Nary.NonDependent.Base.html#3709" class="Bound">as</a> <a id="3712" class="Symbol">:</a> <a id="3714" href="Function.Nary.NonDependent.Base.html#2344" class="Function">Sets</a> <a id="3719" href="Function.Nary.NonDependent.Base.html#3701" class="Bound">n</a> <a id="3721" href="Function.Nary.NonDependent.Base.html#3704" class="Bound">ls</a><a id="3723" class="Symbol">}</a> <a id="3725" class="Symbol">→</a> <a id="3727" class="Symbol">(</a><a id="3728" href="Function.Nary.NonDependent.Base.html#940" class="Generalizable">B</a> <a id="3730" class="Symbol">→</a> <a id="3732" href="Function.Nary.NonDependent.Base.html#954" class="Generalizable">C</a><a id="3733" class="Symbol">)</a> <a id="3735" class="Symbol">→</a> <a id="3737" href="Function.Nary.NonDependent.Base.html#3709" class="Bound">as</a> <a id="3740" href="Function.Nary.NonDependent.Base.html#2863" class="Function Operator">⇉</a> <a id="3742" href="Function.Nary.NonDependent.Base.html#940" class="Generalizable">B</a> <a id="3744" class="Symbol">→</a> <a id="3746" href="Function.Nary.NonDependent.Base.html#3709" class="Bound">as</a> <a id="3749" href="Function.Nary.NonDependent.Base.html#2863" class="Function Operator">⇉</a> <a id="3751" href="Function.Nary.NonDependent.Base.html#954" class="Generalizable">C</a>
<a id="3753" href="Function.Nary.NonDependent.Base.html#3692" class="Function">mapₙ</a> <a id="3758" href="Agda.Builtin.Nat.html#221" class="InductiveConstructor">zero</a>    <a id="3766" href="Function.Nary.NonDependent.Base.html#3766" class="Bound">f</a> <a id="3768" href="Function.Nary.NonDependent.Base.html#3768" class="Bound">v</a> <a id="3770" class="Symbol">=</a> <a id="3772" href="Function.Nary.NonDependent.Base.html#3766" class="Bound">f</a> <a id="3774" href="Function.Nary.NonDependent.Base.html#3768" class="Bound">v</a>
<a id="3776" href="Function.Nary.NonDependent.Base.html#3692" class="Function">mapₙ</a> <a id="3781" class="Symbol">(</a><a id="3782" href="Agda.Builtin.Nat.html#234" class="InductiveConstructor">suc</a> <a id="3786" href="Function.Nary.NonDependent.Base.html#3786" class="Bound">n</a><a id="3787" class="Symbol">)</a> <a id="3789" href="Function.Nary.NonDependent.Base.html#3789" class="Bound">f</a> <a id="3791" href="Function.Nary.NonDependent.Base.html#3791" class="Bound">g</a> <a id="3793" class="Symbol">=</a> <a id="3795" href="Function.Nary.NonDependent.Base.html#3692" class="Function">mapₙ</a> <a id="3800" href="Function.Nary.NonDependent.Base.html#3786" class="Bound">n</a> <a id="3802" href="Function.Nary.NonDependent.Base.html#3789" class="Bound">f</a> <a id="3804" href="Function.Base.html#3626" class="Function Operator">∘′</a> <a id="3807" href="Function.Nary.NonDependent.Base.html#3791" class="Bound">g</a>

<a id="3810" class="Comment">-- compose function at the n-th position</a>

<a id="3852" class="Keyword">infix</a> <a id="3858" class="Number">1</a> <a id="3860" href="Function.Nary.NonDependent.Base.html#3868" class="Function Operator">_%=_⊢_</a>

<a id="_%=_⊢_"></a><a id="3868" href="Function.Nary.NonDependent.Base.html#3868" class="Function Operator">_%=_⊢_</a> <a id="3875" class="Symbol">:</a> <a id="3877" class="Symbol">∀</a> <a id="3879" href="Function.Nary.NonDependent.Base.html#3879" class="Bound">n</a> <a id="3881" class="Symbol">{</a><a id="3882" href="Function.Nary.NonDependent.Base.html#3882" class="Bound">ls</a><a id="3884" class="Symbol">}</a> <a id="3886" class="Symbol">{</a><a id="3887" href="Function.Nary.NonDependent.Base.html#3887" class="Bound">as</a> <a id="3890" class="Symbol">:</a> <a id="3892" href="Function.Nary.NonDependent.Base.html#2344" class="Function">Sets</a> <a id="3897" href="Function.Nary.NonDependent.Base.html#3879" class="Bound">n</a> <a id="3899" href="Function.Nary.NonDependent.Base.html#3882" class="Bound">ls</a><a id="3901" class="Symbol">}</a> <a id="3903" class="Symbol">→</a> <a id="3905" class="Symbol">(</a><a id="3906" href="Function.Nary.NonDependent.Base.html#926" class="Generalizable">A</a> <a id="3908" class="Symbol">→</a> <a id="3910" href="Function.Nary.NonDependent.Base.html#940" class="Generalizable">B</a><a id="3911" class="Symbol">)</a> <a id="3913" class="Symbol">→</a> <a id="3915" href="Function.Nary.NonDependent.Base.html#3887" class="Bound">as</a> <a id="3918" href="Function.Nary.NonDependent.Base.html#2863" class="Function Operator">⇉</a> <a id="3920" class="Symbol">(</a><a id="3921" href="Function.Nary.NonDependent.Base.html#940" class="Generalizable">B</a> <a id="3923" class="Symbol">→</a> <a id="3925" href="Function.Nary.NonDependent.Base.html#954" class="Generalizable">C</a><a id="3926" class="Symbol">)</a> <a id="3928" class="Symbol">→</a> <a id="3930" href="Function.Nary.NonDependent.Base.html#3887" class="Bound">as</a> <a id="3933" href="Function.Nary.NonDependent.Base.html#2863" class="Function Operator">⇉</a> <a id="3935" class="Symbol">(</a><a id="3936" href="Function.Nary.NonDependent.Base.html#926" class="Generalizable">A</a> <a id="3938" class="Symbol">→</a> <a id="3940" href="Function.Nary.NonDependent.Base.html#954" class="Generalizable">C</a><a id="3941" class="Symbol">)</a>
<a id="3943" href="Function.Nary.NonDependent.Base.html#3943" class="Bound">n</a> <a id="3945" href="Function.Nary.NonDependent.Base.html#3868" class="Function Operator">%=</a> <a id="3948" href="Function.Nary.NonDependent.Base.html#3948" class="Bound">f</a> <a id="3950" href="Function.Nary.NonDependent.Base.html#3868" class="Function Operator">⊢</a> <a id="3952" href="Function.Nary.NonDependent.Base.html#3952" class="Bound">g</a> <a id="3954" class="Symbol">=</a> <a id="3956" href="Function.Nary.NonDependent.Base.html#3692" class="Function">mapₙ</a> <a id="3961" href="Function.Nary.NonDependent.Base.html#3943" class="Bound">n</a> <a id="3963" class="Symbol">(</a><a id="3964" href="Function.Base.html#3626" class="Function Operator">_∘′</a> <a id="3968" href="Function.Nary.NonDependent.Base.html#3948" class="Bound">f</a><a id="3969" class="Symbol">)</a> <a id="3971" href="Function.Nary.NonDependent.Base.html#3952" class="Bound">g</a>

<a id="3974" class="Comment">-- partially apply function at the n-th position</a>

<a id="4024" class="Keyword">infix</a> <a id="4030" class="Number">1</a> <a id="4032" href="Function.Nary.NonDependent.Base.html#4040" class="Function Operator">_∷=_⊢_</a>

<a id="_∷=_⊢_"></a><a id="4040" href="Function.Nary.NonDependent.Base.html#4040" class="Function Operator">_∷=_⊢_</a> <a id="4047" class="Symbol">:</a> <a id="4049" class="Symbol">∀</a> <a id="4051" href="Function.Nary.NonDependent.Base.html#4051" class="Bound">n</a> <a id="4053" class="Symbol">{</a><a id="4054" href="Function.Nary.NonDependent.Base.html#4054" class="Bound">ls</a><a id="4056" class="Symbol">}</a> <a id="4058" class="Symbol">{</a><a id="4059" href="Function.Nary.NonDependent.Base.html#4059" class="Bound">as</a> <a id="4062" class="Symbol">:</a> <a id="4064" href="Function.Nary.NonDependent.Base.html#2344" class="Function">Sets</a> <a id="4069" href="Function.Nary.NonDependent.Base.html#4051" class="Bound">n</a> <a id="4071" href="Function.Nary.NonDependent.Base.html#4054" class="Bound">ls</a><a id="4073" class="Symbol">}</a> <a id="4075" class="Symbol">→</a> <a id="4077" href="Function.Nary.NonDependent.Base.html#926" class="Generalizable">A</a> <a id="4079" class="Symbol">→</a> <a id="4081" href="Function.Nary.NonDependent.Base.html#4059" class="Bound">as</a> <a id="4084" href="Function.Nary.NonDependent.Base.html#2863" class="Function Operator">⇉</a> <a id="4086" class="Symbol">(</a><a id="4087" href="Function.Nary.NonDependent.Base.html#926" class="Generalizable">A</a> <a id="4089" class="Symbol">→</a> <a id="4091" href="Function.Nary.NonDependent.Base.html#940" class="Generalizable">B</a><a id="4092" class="Symbol">)</a> <a id="4094" class="Symbol">→</a> <a id="4096" href="Function.Nary.NonDependent.Base.html#4059" class="Bound">as</a> <a id="4099" href="Function.Nary.NonDependent.Base.html#2863" class="Function Operator">⇉</a> <a id="4101" href="Function.Nary.NonDependent.Base.html#940" class="Generalizable">B</a>
<a id="4103" href="Function.Nary.NonDependent.Base.html#4103" class="Bound">n</a> <a id="4105" href="Function.Nary.NonDependent.Base.html#4040" class="Function Operator">∷=</a> <a id="4108" href="Function.Nary.NonDependent.Base.html#4108" class="Bound">x</a> <a id="4110" href="Function.Nary.NonDependent.Base.html#4040" class="Function Operator">⊢</a> <a id="4112" href="Function.Nary.NonDependent.Base.html#4112" class="Bound">g</a> <a id="4114" class="Symbol">=</a> <a id="4116" href="Function.Nary.NonDependent.Base.html#3692" class="Function">mapₙ</a> <a id="4121" href="Function.Nary.NonDependent.Base.html#4103" class="Bound">n</a> <a id="4123" class="Symbol">(</a><a id="4124" href="Function.Base.html#3839" class="Function Operator">_$′</a> <a id="4128" href="Function.Nary.NonDependent.Base.html#4108" class="Bound">x</a><a id="4129" class="Symbol">)</a> <a id="4131" href="Function.Nary.NonDependent.Base.html#4112" class="Bound">g</a>

<a id="4134" class="Comment">-- hole at the n-th position</a>

<a id="holeₙ"></a><a id="4164" href="Function.Nary.NonDependent.Base.html#4164" class="Function">holeₙ</a> <a id="4170" class="Symbol">:</a> <a id="4172" class="Symbol">∀</a> <a id="4174" href="Function.Nary.NonDependent.Base.html#4174" class="Bound">n</a> <a id="4176" class="Symbol">{</a><a id="4177" href="Function.Nary.NonDependent.Base.html#4177" class="Bound">ls</a><a id="4179" class="Symbol">}</a> <a id="4181" class="Symbol">{</a><a id="4182" href="Function.Nary.NonDependent.Base.html#4182" class="Bound">as</a> <a id="4185" class="Symbol">:</a> <a id="4187" href="Function.Nary.NonDependent.Base.html#2344" class="Function">Sets</a> <a id="4192" href="Function.Nary.NonDependent.Base.html#4174" class="Bound">n</a> <a id="4194" href="Function.Nary.NonDependent.Base.html#4177" class="Bound">ls</a><a id="4196" class="Symbol">}</a> <a id="4198" class="Symbol">→</a> <a id="4200" class="Symbol">(</a><a id="4201" href="Function.Nary.NonDependent.Base.html#926" class="Generalizable">A</a> <a id="4203" class="Symbol">→</a> <a id="4205" href="Function.Nary.NonDependent.Base.html#4182" class="Bound">as</a> <a id="4208" href="Function.Nary.NonDependent.Base.html#2863" class="Function Operator">⇉</a> <a id="4210" href="Function.Nary.NonDependent.Base.html#940" class="Generalizable">B</a><a id="4211" class="Symbol">)</a> <a id="4213" class="Symbol">→</a> <a id="4215" href="Function.Nary.NonDependent.Base.html#4182" class="Bound">as</a> <a id="4218" href="Function.Nary.NonDependent.Base.html#2863" class="Function Operator">⇉</a> <a id="4220" class="Symbol">(</a><a id="4221" href="Function.Nary.NonDependent.Base.html#926" class="Generalizable">A</a> <a id="4223" class="Symbol">→</a> <a id="4225" href="Function.Nary.NonDependent.Base.html#940" class="Generalizable">B</a><a id="4226" class="Symbol">)</a>
<a id="4228" href="Function.Nary.NonDependent.Base.html#4164" class="Function">holeₙ</a> <a id="4234" href="Agda.Builtin.Nat.html#221" class="InductiveConstructor">zero</a>    <a id="4242" href="Function.Nary.NonDependent.Base.html#4242" class="Bound">f</a> <a id="4244" class="Symbol">=</a> <a id="4246" href="Function.Nary.NonDependent.Base.html#4242" class="Bound">f</a>
<a id="4248" href="Function.Nary.NonDependent.Base.html#4164" class="Function">holeₙ</a> <a id="4254" class="Symbol">(</a><a id="4255" href="Agda.Builtin.Nat.html#234" class="InductiveConstructor">suc</a> <a id="4259" href="Function.Nary.NonDependent.Base.html#4259" class="Bound">n</a><a id="4260" class="Symbol">)</a> <a id="4262" href="Function.Nary.NonDependent.Base.html#4262" class="Bound">f</a> <a id="4264" class="Symbol">=</a> <a id="4266" href="Function.Nary.NonDependent.Base.html#4164" class="Function">holeₙ</a> <a id="4272" href="Function.Nary.NonDependent.Base.html#4259" class="Bound">n</a> <a id="4274" href="Function.Base.html#3626" class="Function Operator">∘′</a> <a id="4277" href="Function.Base.html#1638" class="Function">flip</a> <a id="4282" href="Function.Nary.NonDependent.Base.html#4262" class="Bound">f</a>

<a id="4285" class="Comment">-- function constant in its n first arguments</a>

<a id="4332" class="Comment">-- Note that its type will only be inferred if it is used in a context</a>
<a id="4403" class="Comment">-- specifying what the type of the function ought to be. Just like the</a>
<a id="4474" class="Comment">-- usual const: there is no way to infer its domain from its argument.</a>

<a id="constₙ"></a><a id="4546" href="Function.Nary.NonDependent.Base.html#4546" class="Function">constₙ</a> <a id="4553" class="Symbol">:</a> <a id="4555" class="Symbol">∀</a> <a id="4557" href="Function.Nary.NonDependent.Base.html#4557" class="Bound">n</a> <a id="4559" class="Symbol">{</a><a id="4560" href="Function.Nary.NonDependent.Base.html#4560" class="Bound">ls</a> <a id="4563" href="Function.Nary.NonDependent.Base.html#4563" class="Bound">r</a><a id="4564" class="Symbol">}</a> <a id="4566" class="Symbol">{</a><a id="4567" href="Function.Nary.NonDependent.Base.html#4567" class="Bound">as</a> <a id="4570" class="Symbol">:</a> <a id="4572" href="Function.Nary.NonDependent.Base.html#2344" class="Function">Sets</a> <a id="4577" href="Function.Nary.NonDependent.Base.html#4557" class="Bound">n</a> <a id="4579" href="Function.Nary.NonDependent.Base.html#4560" class="Bound">ls</a><a id="4581" class="Symbol">}</a> <a id="4583" class="Symbol">{</a><a id="4584" href="Function.Nary.NonDependent.Base.html#4584" class="Bound">b</a> <a id="4586" class="Symbol">:</a> <a id="4588" href="Agda.Primitive.html#388" class="Primitive">Set</a> <a id="4592" href="Function.Nary.NonDependent.Base.html#4563" class="Bound">r</a><a id="4593" class="Symbol">}</a> <a id="4595" class="Symbol">→</a> <a id="4597" href="Function.Nary.NonDependent.Base.html#4584" class="Bound">b</a> <a id="4599" class="Symbol">→</a> <a id="4601" href="Function.Nary.NonDependent.Base.html#4567" class="Bound">as</a> <a id="4604" href="Function.Nary.NonDependent.Base.html#2863" class="Function Operator">⇉</a> <a id="4606" href="Function.Nary.NonDependent.Base.html#4584" class="Bound">b</a>
<a id="4608" href="Function.Nary.NonDependent.Base.html#4546" class="Function">constₙ</a> <a id="4615" href="Agda.Builtin.Nat.html#221" class="InductiveConstructor">zero</a>    <a id="4623" href="Function.Nary.NonDependent.Base.html#4623" class="Bound">v</a> <a id="4625" class="Symbol">=</a> <a id="4627" href="Function.Nary.NonDependent.Base.html#4623" class="Bound">v</a>
<a id="4629" href="Function.Nary.NonDependent.Base.html#4546" class="Function">constₙ</a> <a id="4636" class="Symbol">(</a><a id="4637" href="Agda.Builtin.Nat.html#234" class="InductiveConstructor">suc</a> <a id="4641" href="Function.Nary.NonDependent.Base.html#4641" class="Bound">n</a><a id="4642" class="Symbol">)</a> <a id="4644" href="Function.Nary.NonDependent.Base.html#4644" class="Bound">v</a> <a id="4646" class="Symbol">=</a> <a id="4648" href="Function.Base.html#725" class="Function">const</a> <a id="4654" class="Symbol">(</a><a id="4655" href="Function.Nary.NonDependent.Base.html#4546" class="Function">constₙ</a> <a id="4662" href="Function.Nary.NonDependent.Base.html#4641" class="Bound">n</a> <a id="4664" href="Function.Nary.NonDependent.Base.html#4644" class="Bound">v</a><a id="4665" class="Symbol">)</a>
</pre></body></html>